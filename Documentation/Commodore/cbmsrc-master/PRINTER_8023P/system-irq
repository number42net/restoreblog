;
;**********************
;* PRINT CHARACTER    *
;* ENTRY ROUTINE      *
;**********************
;
SPRINT
	LDA #$80
	STA PTRRDY
	LDA CONTRL
	AND #NOPAPR
	BEQ SP30
SP10
	LDA CONTRL
	AND #$40
	STA FLG
	LDA CONDM
	BEQ SP115
	STA CONDF
SP115	LDA #0
	STA ERC
	STA ERC+1
	STA SPBYTE
	STA CONT
	STA DOT
	STA DOT+1
	STA STOP
	STA DANGER
	STA DAN
	STA CRER
	LDA #<INISTP
	STA CRCLK
	LDA #>INISTP
	STA CRCLK+1
	LDA CRTM
	STA CRCLK+2
	LDA CRTM+1
	STA CRCLK+3
	LDA PFCLK+1
	LDA #$C0
	STA IER
	LDA CONTRL
	ORA #CRHOLD
	STA CONTRL
	LDX #0
	JSR CRSET
	RTS
;
;
SP30
	JSR INITF
	LDA #$7F
	STA IER
	LDA TSTATE
	ORA #$80
	STA TSTATE
	LDA #1
	STA CFLAG
	LDX #12
	JSR CRSET
	LDA #250
	STA LDCLKD
	STA LDCLKE
	RTS
;
;
SSWEEP
	LDA CONTRL
	BMI SP60
	LDA #1
	STA TEST
	BNE SSW10
SP60
	LDA #2
	STA TEST
SSW10
	LDA CONTRL
	BMI SP40
	LDA #0
	STA CRPOS
	STA CRPOS+1
	LDA #FORWRD
	STA CTRAVL
	LDA #8
	STA TSTATE
	JMP SP10
SP40
	LDA #<CRFUDG
	STA CRPOS
	LDA #>CRFUDG
	STA CRPOS+1
	LDA #REVERS
	STA CTRAVL
	LDA #10
	STA TSTATE
	JMP SP10
;
;
;***********************
;* IRQ ENTRY           *
;***********************
;
CSTEPS	.BYTE %0101,%0110,%1010,%1001
FSTEPS	.BYTE %10010000,%10100000
	.BYTE %01100000,%01010000
IRQ
	SEI
	PHA
	TXA
	PHA
	TYA
	PHA
	LDA IER
	AND IFR
	STA TEST1
	ASL A
	BMI IRQ30
	ASL A
	BMI IRQ40
	LDA NDCLKE
	BPL IRQEND
	BMI NCLK
;
;
;
IRQ30
	LDA CRCLK
	LDA CRCLK+3
	STA CRCLK+1
	LDA CONTRL
	AND #$40
	CMP FLG
	STA FLG
	BNE NC20
	INC ERC
	LDA ERC
	CMP #200
	BCC IN30
	LDA TSTATE
	ORA #$80
	STA TSTATE
	LDX #14
	JSR CRSET
	JMP FAILUR
NC20	LDA #0
	STA ERC
	STA ERC+1
;
IN30	JMP (CRINV)
NCLK
	LDA #$FF
	STA NEEDLS
	LDA NDCLKD
	LDA TSTATE
	BPL IRQEND
	JMP (CRINV)
;
;
IRQ40
	LDA CONDF
	BNE IRQ410
	JMP PFMOTR
IRQ410
	LDA PFCLK
	LDA PFCLK+1
	LDA #$02
	STA IER
	LDA TSTATE
	CMP #22
	BNE IR44
	LDA ACCSEL
	ORA STOP
	BNE IRQ401
	JMP IM502
IR44	LDA CONDF
	BMI IRQ401
	LDA CTRAVL
	BPL L10
	JMP RPR10
L10	JMP LPR10
IRQ401
	JSR COUNT
	JMP IRQEND
;
;
IRQEND
	PLA
	TAY
	PLA
	TAX
	PLA
	CLI
	RTI
;
;
;
;
;
CRSET
	LDA CRJMP,X
	STA CRINV
	LDA CRJMP+1,X
	STA CRINV+1
	RTS
;
CRJMP
	.WORD ACCELF ;#0
	.WORD LPRINT ;#2
	.WORD RPRINT ;#4
	.WORD SEKPOS ;#6
	.WORD SWEEP ;#8
	.WORD REWIND ;#10
	.WORD FIXP ;#12
	.WORD FAIL ;#14
	.WORD SPDWN ;#16
	.WORD QUIT1 ;#18
	.WORD QUIT ;#20
	.WORD IMAGE ;#22
;
;CR STATE ...
; STATE #0
;
ACCELF
	JSR CHAGP
	LDX SPBYTE
	CPX #0
	BNE A10
	LDA CTRAVL
	CMP ACCEL
	BEQ A11
A10	JSR COUNT
A11	LDX SPBYTE
	CPX #48
	BCS S100
	LDA CONDM
	BEQ A22
	LDA SDATA3,X
	STA CRCLK
	INX
	LDA SDATA3,X
	STA CRCLK+1
	INX
	JMP S101
A22	LDA SDATA,X
	STA CRCLK
	INX
	LDA SDATA,X
	STA CRCLK+1
	INX
S101	STX SPBYTE
	CPX #84
	BCC ACC10
	LDX TSTATE
	JSR CRSET
ACC10	JMP IRQEND
S100	INX
	INX
	JMP S101
;
;
; STATE #16
;
SPDWN
	JSR CHAGP
	LDX SPBYTE
	CPX #47
	BCS S102
	LDA CONDM
	BEQ SP33
	LDA SDATA3,X
	TAY
	DEX
	LDA SDATA3,X
	STA CRCLK
	TYA
	STA CRCLK+1
	JMP SP34
SP33	LDA SDATA,X
	TAY
	DEX
	LDA SDATA,X
	STA CRCLK
	TYA
	STA CRCLK+1
SP34	JSR COUNT
	CPX #0
	BEQ SPD1
	DEX
S103	STX SPBYTE
	JMP IRQEND
SPD1
	STX SPBYTE
	LDX #18
	JSR CRSET
	JMP IRQEND
S102	DEX
	DEX
	JSR COUNT
	JMP S103
;
; STATE #18
;
QUIT1	JSR CHAGP
	JSR COUNT
	LDA #<CRDTIM
	STA CRCLK
	LDA #>CRDTIM
	STA CRCLK+1
	LDX #20
	JSR CRSET
	JMP IRQEND
;
; STATE #20
;
QUIT	JSR COUNT
	LDA #0
	STA CONDF
	LDA #$60
	STA IER
	LDA CONTRL
	AND #$FD
	STA CONTRL
;LDA DOT ;WITHOUT WAIT
;STA ACCA
;LDA DOT+1
;STA ACCA+1
;LDA #<DLIM1
;STA ACCB
;LDA #>DLIM1
;STA ACCB+1
;JSR COMP
;BCC QUIT95
;LDA #<DLIM2
;STA ACCB
;LDA #>DLIM2
;STA ACCB+1
;JSR COMP
;BCS QUIT99
;LDA #TLIM1
;STA TDATA
;JMP QUIT25
;QUIT99 LDA #<DLIM3
;STA ACCB
;LDA #>DLIM3
;STA ACCB+1
;JSR COMP
;BCS QUIT93
;LDA #TLIM2
;STA TDATA
;JMP QUIT25
;QUIT93 LDA #<DLIM4
;STA ACCB
;LDA #>DLIM4
;STA ACCB+1
;JSR COMP
;BCS QUIT94
;LDA #TLIM3
;STA TDATA
;JMP QUIT25
;QUIT94 LDA #TLIM4
;STA TDATA
;QUIT25 JSR TIMER
QUIT95
QUI10
	LDA TEST
	BNE QUIT20
	LDA NFEEDS
	BEQ QUIT50
	JSR PFSTRT
QUIT50
	LDA PTRRDY
	AND #$7F
	STA PTRRDY
	JMP IRQEND
QUIT20
	DEC TEST
	JSR SSW10
	JMP IRQEND
;
;
; STATE #2
;
LPRINT
	LDA CONDF
	AND #$7F
	STA CONDF
	LDA CTRAVL
	STA ACCEL
	JSR CHAGP
LPR10	LDA CONTRL
	BMI LP10
	LDA #0
	STA CRPOS
	STA CRPOS+1
	JMP IRQEND
LP10
	JSR TRANSB
	JSR COMP
	BCC LP20
;BEQ LP20
	JSR DWPOS
	JMP IRQEND
LP20	JSR DWPOS
	JMP NDLSON
;
; STATE #4
;
RPRINT
	LDA CONDF
	AND #$7F
	STA CONDF
	LDA CTRAVL
	STA ACCEL
	JSR CHAGP
RPR10	LDA CONTRL
	BMI RP10
RPR	LDA #0
	STA CRPOS
	STA CRPOS+1
	LDA STPORT
	AND #$0F
	STA DANGER
PPR1	JMP IRQEND
;PPR LDA #$FF
;STA DAN
;BNE RPR
RP10	LDA DANGER
	BEQ RPR102
;TAX
;LDA DAN
;BNE PPPR
;CPX #9
;BEQ PPR
PPPR	LDA STPORT
	AND #$0F
	CMP #$0A
	BNE RPR
	LDA #0
	STA DANGER
	STA DAN
RPR102	LDA CRPOS
	ORA CRPOS+1
	BNE RP102
	LDA TEST1
	ASL A
	BPL RPR
RP102	JSR UPPOS
	JSR TRANSA
	JSR COMP
	BCC RP21
	BEQ RP21
	BNE RP20
RP21
	JMP IRQEND
RP20
	JMP NDLSON
;
; STATE #6
;
SEKPOS	JSR CHAGP
	LDA CTRAVL
	STA ACCEL
	LDA CONTRL
	BMI SE80
	LDA #0
	STA CRPOS
	STA CRPOS+1
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
SE80	LDA SEEKF
	CMP #1
	BNE SE10
	LDA CTRAVL
	BMI SE20
	LDA QST
	CMP RLIM
	BCS SE200
	LDX #10
	JSR CRSET
	JMP IRQEND
SE200	JSR DWPOS
	JSR TRANSA
	JSR COMP
	BCC SE30
	JMP IRQEND
SE20
	JSR TRANSB
	JSR COMP
	BCC SE31
	BEQ SE31
	JSR UPPOS
	JMP SE30
SE31	JSR UPPOS
	JMP IRQEND
SE10
	CMP #2
	BNE SE50
	JSR UPPOS
	JMP SE30
SE50
	JSR DWPOS
SE30
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
;
; STATE #8
;
SWEEP
	JSR CHAGP
	LDA CONTRL
	BMI SSW40
	LDA #0
	STA CRPOS
	STA CRPOS+1
	JMP IRQEND
SSW40
	JSR UPPOS
	JSR TRANSA
	LDA CRLIMB
	STA ACCB
	LDA CRLIMB+1
	STA ACCB+1
	JSR COMP
	BCS SW10
	JMP IRQEND
SW10
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
;
; STATE #10
;
REWIND
	JSR CHAGP
	LDA CONTRL
	BMI RE10
	LDA #0
	STA CRPOS
	STA CRPOS+1
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
RE10	JSR DWPOS
	JMP IRQEND
;
;
NDLSON
	LDA CONTRL
	BMI ND10
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
ND10
	LDY COUNT1
	LDA (IP)Y
	EOR RVS
	STA DOTDAT
	EOR #$FF
	STA NEEDLS
	LDA PORTB
	AND #$FE
	STA PORTB
	CLI
	LDA #NDTIME
	STA NDCLKD
	STA NDCLKE
ND20
	LDA NDCLKE
	BMI GETCHR
	CMP #NDTIME-12
	BCS ND20
	LDA PORTB
	ORA #$01
	STA PORTB
GETCHR
;LDA DOTDAT ;WITHOUT WAIT
;TAY
;AND #$0F
;TAX
;LDA DOTNUM,X
;STA DOTDAT
;TYA
;LSR A
;LSR A
;LSR A
;LSR A
;TAX
;LDA DOTNUM,X
;CLC
;ADC DOTDAT
;STA ACCB
;LDA #0
;STA ACCB+1
;LDA DOT
;STA ACCA
;LDA DOT+1
;STA ACCA+1
;JSR ADDAB
;LDA ACCA
;STA DOT
;LDA ACCA+1
;STA DOT+1
	LDA CTRAVL
	BMI GE10
	LDA COUNT1
	BEQ GE20
	DEC COUNT1
GE40	JMP IRQEND
GE10
	DEC COUNT3
	BPL GE11
	DEC COUNT2
	BPL GE11
	INC COUNT1
	LDA LMODE
	BNE GE77
	LDA #0
	STA COUNT3
	BEQ GE88
GE77	LDA #1
	STA COUNT3
GE88	LDA ENHN
	STA COUNT2
GE11
	LDA COUNT1
	CMP #6
	BCS GE20
	BCC GE40
GE20
	JSR RMOVE
	TAX
	BNE GE50
	LDA CTRAVL
	BMI GE60
	LDA #5
	STA COUNT1
	BNE GE40
GE60
	LDA #0
	STA COUNT1
	BEQ GE40
GE50
	BMI GE70
	LDA CONDF
	BEQ  GE34
	ORA #$80
	STA CONDF
GE34	LDA #1
	STA NFEEDS
	LDA #$80
	STA FTRA
GE70
	LDA CONDF
	BEQ GE33
	ORA #$80
	STA CONDF
GE33	LDA CTRAVL
	BPL GE71
GE72	LDA QEND
	CMP LLIM
	BCC GE99
	LDX #8
	JSR CRSET
	JMP IRQEND
GE71
	LDA QST
	CMP RLIM
	BCS GE99
	LDX #10
	JSR CRSET
	JMP IRQEND
GE99
	LDA #83
	STA SPBYTE
	LDX #16
	STX STOP
	JSR CRSET
	JMP IRQEND
DOTNUM
	.BYTE 0 ; #0
	.BYTE 1 ; #1
	.BYTE 1 ; #2
	.BYTE 2 ; #3
	.BYTE 1 ; #4
	.BYTE 2 ; #5
	.BYTE 2 ; #6
	.BYTE 3 ; #7
	.BYTE 1 ; #8
	.BYTE 2 ; #9
	.BYTE 2 ; #10
	.BYTE 3 ; #11
	.BYTE 2 ; #12
	.BYTE 3 ; #13
	.BYTE 3 ; #14
	.BYTE 4 ; #15
;
;
SDATA
	.WORD 9635
	.WORD 4036
	.WORD 3060
	.WORD 2604
	.WORD 2279
	.WORD 2200
	.WORD 2083
	.WORD 1953
	.WORD 1823
	.WORD 1693
	.WORD 1562
	.WORD 1497
	.WORD 1432
	.WORD 1387
	.WORD 1341
	.WORD 1309
	.WORD 1276
	.WORD 1237
	.WORD 1211
	.WORD 1185
	.WORD 1159
	.WORD 1133
	.WORD 1113
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
	.WORD 1100
SDATA3
	.WORD 9635
	.WORD 4036
	.WORD 3060
	.WORD 2604
	.WORD 2279
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
	.WORD 2200
;
; STATE #12
;
FIXP
	LDA LDCLKD
	JSR SWLED
	BNE FP10
	LDA PEM
	BEQ FP103
	LDA #0
	STA PEM
	JMP IRQEND
FP103	LDA TSTATE
	AND #$7F
	STA TSTATE
	JSR SPRINT
	JMP IRQEND
FP10
	LDA #250
	STA LDCLKD
	STA LDCLKE
	JMP IRQEND
SWLED
	LDA CFLAG
	BNE SW101
	LDA #FEEDSW
	AND CONTRL
	BEQ SW15
	BNE SW20
SW101
	LDA #FEEDSW
	AND CONTRL
	BNE SW30
	LDA #0
	STA CFLAG
SW15
	LDA CONTRL
	ORA #LED
	STA CONTRL
	RTS
SW20
LEDON
	LDA CONTRL
	AND #$FF-LED
	STA CONTRL
	LDA #0
	RTS
SW30
	LDA CONTRL
	EOR #LED
	STA CONTRL
	LDA #1
	RTS
;
;STATE #14
;
FAILUR
	JSR INITF
	LDA #1
	STA CFLAG
FAIL
	JSR SWLED
	BNE FL10
	JMP INIT
FL10
	LDA #64
	STA LDCLKD
	STA LDCLKE
	JMP IRQEND
;**********************
;* PAPER FEED MOTOR   *
;**********************
;
PFMOTR
	LDX SPBYTE
	CPX #10
	BCS P10
	LDA SDATA1,X
	STA PFCLK
	INX
	LDA SDATA1,X
	STA PFCLK+1
	INX
	STX SPBYTE
	JMP P11
P10	LDA #<PFTIME
	STA PFCLK
	LDA #>PFTIME
	STA PFCLK+1
P11	LDA FTRAVL
	BMI PFDOWN
	BEQ PFOFF
	JSR FDSTP
PF10
	DEC FTRAVL
	BNE PF20
	DEC NFEEDS
	BEQ PF10
	LDA FDINC
	STA FTRAVL
PF20
	JMP IRQEND
;
;
PFDOWN
	LDA #<PFDTIM
	STA PFCLK
	LDA #>PFDTIM
	STA PFCLK+1
	LDA #0
	STA FTRAVL
	JMP IRQEND
;
;
PFOFF
	LDA #$20
	STA IER
	LDA PFCLK
	LDA CONTRL
	AND #$FF-PFHOLD
	STA CONTRL
	LDA PTRRDY
	AND #$80
	STA PTRRDY
	JMP IRQEND
;
;
PFSTRT
	PHP
	SEI
	LDA FDINC
	STA FTRAVL
	LDA CONTRL
	ORA #PFHOLD
	STA CONTRL
	LDA #<PFITIM
	STA PFCLK
	LDA #>PFITIM
	STA PFCLK+1
	LDA PTRRDY
	ORA #$40
	STA PTRRDY
	LDA #$A0
	STA IER
	LDA #0
	STA SPBYTE
	PLP
	RTS
SDATA1
	.WORD 15000
	.WORD 10000
	.WORD 7500
	.WORD 6000
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
	.WORD 5600
;
TIMER
	LDA #<TLIM
	STA CRCLK
	LDA #>TLIM
	STA CRCLK+1
TIMLOP	LDA CRCLK+9
	AND #$40
	BEQ TIMLOP
	DEC TDATA
	BNE TIMER
	RTS
COUNT	LDA CONTRL
	BMI C10
	LDA #0
	STA CRPOS
	STA CRPOS+1
	RTS
C10
	LDA CTRAVL
	BMI C20
	JSR DWPOS
	RTS
C20
	JSR UPPOS
	RTS
FDSTP
	LDA FTRA
	BMI FT10
	INC FPHASE
	JSR FDOSTP
	RTS
FT10
	DEC FPHASE
	JSR FDOSTP
	RTS
FDOSTP
	LDA FPHASE
	AND #3
	TAY
	LDA STPORT
	AND #$F
	ORA FSTEPS,Y
	STA STPORT
	RTS
	.END
