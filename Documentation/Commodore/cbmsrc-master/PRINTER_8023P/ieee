	.PAGE 'IEEE'
;
;
;
; *MAIN LOOP WHEN DEVICE IS IDLE
;
; USER CAN FEED PAPER
; PRINTER IS WAITING FOR ATN
;
; *THEN FORMATTER USES THE 'GET'
;  ENTRY POINT WHEN THE IEEE
;  ROUTINE WILL DETERMINE IF NUMERIC DATA
;
;
;  IEEE RETURNS WITH THE OVERFLOW
;  SET IF DIFFERENT SA
;
;  DATA RETURNS IN .A
;
;
GET
IEEE
; TEST IF ATN LOW
	BIT CIEEE
	BPL ATNMOD ; IT IS ATNMODE
;
	LDA FACT ; TEST IF ALREADY ADRSED
	BEQ ALLWPF ; ALLOW PAPER FEED
;
	JMP DATMOD ; IN DATA MODE
;
ALLWPF	JSR FFTEST
	JMP IEEE
;
ATNMOD	LDA #0 ; CLEAR ADRS FLAGS
	STA FACT
	STA SA
;
;PULL DAC LOW
	LDA CIEEE
	ORA #NDAC
	STA CIEEE
;
ATNM10
;PULL NRFD HIGH
	LDA CIEEE
	ORA #NRFD
	STA CIEEE
;
ATNM15	BIT CIEEE ; TEST FOR ATN LOW
	BMI ATNM80 ; IT'S HIGH
;
; WAIT FOR DAV LOW
	BVS ATNM15
;PULL NRFD LOW
	LDA CIEEE
	AND #$FF-NRFD
	STA CIEEE
;
	LDA DIEEE ; READ AND INVERT DATA
	EOR #$FF
	STA DATA
;
;PULL DAC HIGH
	LDA CIEEE
	AND #$FF-NDAC
	STA CIEEE
;
ATNM20
	BIT CIEEE ; WAIT FOR DAV HI
	BVC ATNM20
;
;PULL DAC LOW
	LDA CIEEE
	ORA #NDAC
	STA CIEEE
;
	LDA DATA ; DECODE COMMAND
;
	CMP #UNLSN
	BNE ATNM25 ; NOT UNLISTEN
;
	LDA #0
	STA FACT
	STA SA
	BEQ ATNMA
;
ATNM25	CMP DBN ; IS IT US?
	BNE ATNM30
;
	LDA #$FF ; SET JUST RECEIVED OUR PA
	STA FACT
	BNE ATNMB
;
ATNM30	AND #$60 ;TEST IF SA
	CMP #$60
	BNE ATNMA ; GARBAGE
;
	LDA FACT ; JUST RECEIVED PA?
	BPL ATNMB ; NOT ADDRESSED  OR NOT FOR US
;
	LDA DATA
	AND #$1F
	STA SA ; SAVE THE SA
;
ATNMA	LDA FACT
	BPL ATNMB
;
	LDA #$7F
	STA FACT
;
ATNMB	BIT CIEEE ; TEST ATN STILL LOW
	BMI ATNM80 ; ATN IS GONE
;
	JMP ATNM10 ; MORE TO LISTEN TO
;
ATNM80	LDA FACT ; TEST IF ADRSED
	BNE ATNM85 ; YES
;
;PULL NRFD HIGH
	LDA CIEEE
	ORA #NRFD
	STA CIEEE
;PULL DAC HIGH
	LDA CIEEE
	AND #$FF-NDAC
	STA CIEEE
;
DATL90	JMP IEEE
;
ATNM85	;ACTIVE LISTEN NOW
;PULL NRFD LOW
	LDA CIEEE
	AND #$FF-NRFD
	STA CIEEE
;PULL DAC LOW
	LDA CIEEE
	ORA #NDAC
	STA CIEEE
;
DATMOD	; ASK FOR BYTE
;PULL NRFD HIGH
	LDA CIEEE
	ORA #NRFD
	STA CIEEE
;
DATL10	BIT CIEEE ; TEST FOR ATN ASSERTED
	BPL DATL90 ;IT IS
; WAIT FOR BYTE
	BVS DATL10
;PULL NRFD LOW
	LDA CIEEE
	AND #$FF-NRFD
	STA CIEEE
;
	LDA DIEEE
	EOR #$FF
	STA DATA
;
;PULL DAC HIGH
	LDA CIEEE
	AND #$FF-NDAC
	STA CIEEE
;
DATL20
	BIT CIEEE ; WAIT FOR DAV HI
	BVC DATL20
;
;PULL DAC LOW
	LDA CIEEE
	ORA #NDAC
	STA CIEEE
;
;
; SET OVERFLOW IF DIFFERENT SA
;
	LDA SA
	CMP OLDSA
	BEQ I3E50
;
	STA OLDSA ; ITS A NEW ONE
	LDA #$40
	PHA
	PLP ; SET OVERFLOW
	BVS GET20
;
I3E50	CLV
;
GET20	LDA DATA ; TEST IF NUMERIC
	CMP #'0
	BCC GET30
;
	CMP #':
	RTS
;
GET30	SEC
	RTS
;
;
;
	.END
