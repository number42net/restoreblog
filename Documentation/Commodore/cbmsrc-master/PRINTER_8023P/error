	.PAG 'ERROR'
;
;*NORMAL ERROR AND FORMAT ERROR PROCESSOR
;
ERROR	LDX DIAG
	BEQ ERR40
	STY LW
FMTE40	LDX #7 ;FORMAT ERROR ENTRY POINT
	STX RW
ERR10	LDA EMSG,X
ERR20	JSR INSERT
	DEC RW
	LDX RW
	BMI ERR30
	CPX #2
	BNE ERR10
	LDA LW
	BNE ERR20
ERR30	INX ;NOW X=0
ERR40	STX SA
	STX OLDSA ;FOOL IEEE ROUTINE
	JMP MAIN03 ;PROCESS CURRENT CHAR
;
;*FORMAT ERROR PROCESSOR
;
FMTERR	STY LW
	LDA #CR ;FORCE BUFFER DUMP
	JSR INSERT
	LDX DIAG ;DO WE PRINT A MSG?
	BEQ ERR40 ;IF EQ, NO
	LDA #0
	STA RW
FMTE10	LDY RW
	CPY FV
	BEQ FMTE15
	LDA F,Y
	TAX
	AND #$40
	BEQ *+4
	LDX #$20
	TXA
	CMP #$20 ;CONVERT CONTROL CHARS TO BLANKS
	BCS *+4
	ORA #$40
	JSR INSERT
	INC RW
	BNE FMTE10
FMTE15	LDA #CR
	JSR INSERT
	DEC FP
	BMI FMTE30
	LDA FP ;CHECK IF FP IS AT
	CMP #80 ;VERY END OF FORMAT STRING
	BNE FMTE20 ;IF NE, NOT AT END.
	DEC FP ;PUT FP INTO RANGE
FMTE20	LDA #BLANK ;SKIP OUT TO ERROR
	JSR INSERT
	DEC FP
	BPL FMTE20
FMTE30	LDA #'^
	JSR INSERT
	BCC FMTE40
;NOTE: ERROR MESSAGE IS REVERSED BELOW
EMSG	.BYT CR,'* :EP*',CR
	.END
