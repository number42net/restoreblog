;***************************
;* SYSTEM APL. LAB         *
;* IRQ AND LOGIC SEEKING   *
;***************************
;
TPOFF
	LDA #2
	STA IER
	LDA PORTA
	RTS
;SUBROUTINE LIST
;
COMP	LDA ACCA+1
	CMP ACCB+1
	BCC COMP10
	BNE COMP10
	LDA ACCA
	CMP ACCB
COMP10	RTS
;
;
ADDAB
	CLC
	LDA ACCA
	ADC ACCB
	STA ACCA
	LDA ACCA+1
	ADC ACCB+1
	STA ACCA+1
	RTS
;
;
SUBAB
	SEC
	LDA ACCA
	SBC ACCB
	STA ACCA
	LDA ACCA+1
	SBC ACCB+1
	STA ACCA+1
	RTS
;
;
TRANSA
	LDA STPOS
	STA ACCB
	LDA STPOS+1
	STA ACCB+1
	JMP ENT1
;
;
TRANSB
	LDA ENDPOS
	STA ACCB
	LDA ENDPOS+1
	STA ACCB+1
	JMP ENT1
;
;
ENT1
	LDA CRPOS
	STA ACCA
	LDA CRPOS+1
	STA ACCA+1
	RTS
;
;
TRNAB
	LDX ACCA
	LDY ACCB
	STX ACCB
	STY ACCA
	LDX ACCA+1
	LDY ACCB+1
	STX ACCB+1
	STY ACCA+1
	RTS
;
;
DIR
	SEC
	LDA ENDPOS
	SBC CRPOS
	STA ACCA
	LDA ENDPOS+1
	SBC CRPOS+1
	STA ACCA+1
	SEC
	LDA CRPOS
	SBC STPOS
	STA ACCB
	LDA CRPOS+1
	SBC STPOS+1
	STA ACCB+1
	RTS
;
;
CHAGP
	LDA CONDF
	BEQ CHA100
	LDA #$A0
	STA IER
	LDA #<CONDES
	STA PFCLK
	LDA #>CONDES
	STA PFCLK+1
CHA100	LDA CTRAVL
	BMI CHA1
	DEC CPHASE
	JSR DOSTEP
	RTS
CHA1
	INC CPHASE
	JSR DOSTEP
	RTS
;
;
UPPOS
	LDA CRPOS
	CMP #$FF
	BEQ UP10
	INC CRPOS
	RTS
UP10
	LDA #0
	STA CRPOS
	INC CRPOS+1
	RTS
;
;
DWPOS
	LDA CRPOS
	BNE DW10
	LDA #$FF
	STA CRPOS
	DEC CRPOS+1
	RTS
DW10
	DEC CRPOS
	RTS
;
;
LOGIC
	LDA CRPOS
	ORA CRPOS+1
	BEQ LOGIC3
	JSR TRANSA
	JSR COMP
	BCS LOGIC1
	JSR TRNAB
	JSR SUBAB
	LDA #0
	STA ACCB+1
	LDA DDATA
	STA ACCB
	JSR COMP
	BCC LOGIC2
	LDA #FORWRD
	STA CTRAVL
	BNE LOGIC3
LOGIC2
	LDA #REVERS
	STA CTRAVL
	LDA #3
	STA SEEKF
	BNE LOGIC5
LOGIC1
	JSR TRANSB
	JSR COMP
	BCC LOGIC6
	JSR SUBAB
	LDA #0
	STA ACCB+1
	LDA DDATA
	STA ACCB
	JSR COMP
	BCC LOGIC7
	LDA #REVERS
	STA CTRAVL
	BNE LOGIC4
LOGIC7
	LDA #FORWRD
	STA CTRAVL
	LDA #2
	STA SEEKF
	BNE LOGIC5
LOGIC6
	LDA #1
	STA SEEKF
	JSR DIR
	JSR COMP
	BCS LOGIC8
	LDA #FORWRD
	STA CTRAVL
	BNE LOGIC5
LOGIC8
	LDA #REVERS
	STA CTRAVL
	BNE LOGIC5
LOGIC3
	LDA #FORWRD
	STA CTRAVL
	LDA #4
	STA TSTATE
	LDA #0
	STA SEEKF
	STA COUNT1
	BEQ END1
LOGIC4
	LDA #2
	STA TSTATE
	LDA #5
	STA COUNT1
	LDA #0
	STA SEEKF
	BEQ END1
LOGIC5
	LDA #6
	STA TSTATE
END1	RTS
;
;
DOSTEP
	LDA CPHASE
	AND #3
	TAY
	LDA STPORT
	AND #$F0
	ORA CSTEPS,Y
	STA STPORT
	RTS
;
;
;
;
TESBUF	LDA SFLAG
	STA SFLAG1
	LDX QTAIL
	STX QSTART
	LDA #0
	STA QST
	STA QEND
	LDA RVS
	ORA ENHN
	BEQ TES20
	ORA ONEDIR
	ORA #$40
	STA ONEDIR
	STA ONE
	BNE TES10
TES20
	LDA Q,X
	CMP #$20
	BEQ TES108
	CMP #$A0
	BNE TES10
TES108	INC QST
	INC QEND
	INX
	CPX #QSIZ
	BNE TES59
	LDX #0
TES59	STX QSTART
	JMP TES20
TES10
	LDA Q,X
	CMP #$0D
	BEQ TES30
	CMP #$8D
	BEQ TES30
	PHA
	AND #$7F
	CMP #$20
	PLA
	BCS TES15
	CMP #GRAPH
	STA BF10
	BNE TES21
	LDA #0
	STA SFLAG
	BEQ TES16
TES21	CMP #BUSI
	STA BF10
	BNE TES99
	STA SFLAG
	BEQ TES16
TES99	CMP #RVSON
	BNE TES22
	ORA ONEDIR
	STA ONEDIR
	STA ONE
	BNE TES16
TES22	LDA #$40
	ORA ONEDIR
	STA ONEDIR
	STA ONE
	BNE TES16
TES15	INC QEND
TES16	STX QEND10
	INX
	CPX #QSIZ
	BNE TES10
	LDX #0
	BEQ TES10
TES30	STA DELIM
	INX
	CPX #QSIZ
	BNE TES100
	LDX #0
TES100	STX QTAIL
TES50	LDA QST
	CMP QEND
	BEQ TES55
	LDA ONEDIR
	AND #$3F
	CMP #RVSON
	BEQ TES55
	LDA RVS
	ORA ENHN
	BNE TES55
	LDX QEND10
TES51	LDA Q,X
	CMP #$20
	BNE TES40
	DEC QEND
	CPX #0
	BNE TES25
	LDX #QSIZ-1
	BNE TES51
TES25	DEX
	JMP TES51
TES40	STX QEND10
TES55	RTS
;
;
ADD6
	LDA #0
	STA ACCA
	STA ACCA+1
	STA ACCB+1
	LDA #6
	STA ACCB
	RTS
;
ADD12	LDA #0
	STA ACCA
	STA ACCA+1
	STA ACCB+1
	LDA #12
	STA ACCB
	RTS
;
;
CALPOS	LDA LMODE
	BEQ CAL70
	JSR ADD12
	JMP CAL71
CAL70	JSR ADD6
CAL71	LDA QST
	BEQ CAL50
	LDX QST
CAL30	JSR ADDAB
	DEX
	BNE CAL30
CAL50	LDA ACCA
	STA STPOS
	LDA ACCA+1
	STA STPOS+1
	LDA QEND
	BEQ CAL10
	LDA LMODE
	BEQ CAL73
	JSR ADD12
	JMP CAL74
CAL73	JSR ADD6
CAL74	LDX QEND
CAL40	JSR ADDAB
	DEX
	BNE CAL40
	LDA ACCA
	STA ENDPOS
	LDA ACCA+1
	STA ENDPOS+1
	RTS
CAL10	LDA #0
	STA ENDPOS
	STA ENDPOS+1
	RTS
;
;
RMOVE
	LDA FLAG
	BEQ PR90
	LDA #0
	STA FLAG
	LDA FTRMFG
	BMI PR91
	BNE PR92
	STA SFLAG
PR92	LDA #0
	STA ENHN
	STA RVS
PR91	LDA #0
	STA FTRMFG
	LDA DELIM
	RTS
PR90	LDA CTRAVL
	BMI PR20
PR31	LDX QEND10
	LDA Q,X
	CPX QSTART
	BNE PR32
	LDY #$80
	STY FLAG
PR32
	CPX #0
	BNE PR33
	LDX #QSIZ-1
	BNE PR34
PR33	DEX
PR34	STX QEND10
PR35	PHA
	AND #$7F
	CMP #$20
	PLA
	BCS PR40
	JSR SCONT
	JMP RMOVE
PR20
	LDX QSTART
	LDA Q,X
	CPX QEND10
	BNE PR50
	LDY #80
	STY FLAG
PR50
	INX
	CPX #QSIZ
	BNE PR38
	LDX #0
PR38	STX QSTART
	JMP PR35
PR40
	PHA
	CMP #254
	BNE RM102
	LDA #<PCHAR
	STA IP
	PLA
	LDA #0
	STA IP+1
	RTS
RM102	ROL A
	LDA #>ASCII
	STA BASE+1
	LDY #<ASCII
	LDA SFLAG
	EOR BUSSEL
	BNE RM105
	BCC RM110
	LDA #>GRAFIC
	STA BASE+1
	LDY #<GRAFIC
	JMP RM110
	.IFN KATA <
RM105	PLA
	PHA
	AND #$7F
	BCS RM106
	CMP #'A
	BCC RM110
	BCS RM107
RM106	CMP #'A
	BCS RM110
RM107	LDA #>BUSNES
	STA BASE+1
	LDY #<BUSNES
>
	.IFE KATA <
RM105	BCC RM110
	LDA #>KATAKA
	STA BASE+1
	LDY #<KATAKA
>
RM110	STY BASE
	LDY #0
	STY IP+1
	PLA
	AND #$3F
	ASL A
	STA IP
	ASL A
	ADC IP
	ROL IP+1
	ADC BASE
	STA IP
	LDA IP+1
	ADC BASE+1
	STA IP+1
	TYA
	LDX ENHN
	STX COUNT2
	RTS
;
;
SCONT
	CMP #RVSON
	BNE RM40
	LDA #$FF
RM30	STA RVS
	JMP RM80
RM40	CMP #RVSOFF
	BNE RM50
	LDA #0
	BEQ RM30
RM50	CMP #ENHNCE
	BNE RM60
	INC ENHN
	LDA ENHN
	BNE RM70
RM60	CMP #UNENHN
	BNE RM140
	LDA #0
RM70	STA ENHN
	JMP RM80
RM140	CMP #GRAPH
	BNE RM160
	LDA #0
RM150	STA SFLAG
	BEQ RM80
RM160	CMP #BUSI
	BEQ RM150
RM80	LDX ENHN
	STX COUNT2
	RTS
;
;
CHANC
	LDA QST
	CMP QEND
	BNE CHAN25
	BEQ CHAN29
CHAN25
	LDA CTRAVL
	BPL CHA10
CHAN29	LDA #0
	STA BF10
	LDA SFLAG1
	STA SFLAG
	RTS
CHA10
	LDA BF10
	BNE CHA55
	RTS
CHA55	LDX QEND10
CHA12
	LDA Q,X
	CMP #BUSI
	BNE CHA20
	STX TEMP
	JSR CHASUB
CHA11
	LDA Q,X
	CMP #GRAPH
	BNE CHA30
	LDY TEMP
	STA Q,Y
	JMP CHA12
CHA30
	CMP #BUSI
	BNE CHA31
	STX TEMP
	JSR CHASUB
	JMP CHA11
CHA31
	JSR CHASUB
	JMP CHA11
CHA20
	CMP #GRAPH
	BNE CHA41
	STX TEMP
	JSR CHASUB
CHA43
	LDA Q,X
	CMP #BUSI
	BNE CHA40
	LDY TEMP
	STA Q,Y
	JMP CHA12
CHA40
	CMP #GRAPH
	BNE CHA42
	STX TEMP
CHA42
	JSR CHASUB
	JMP CHA43
CHA41
	JSR CHASUB
	JMP CHA12
;
;
CHASUB
	CPX QSTART
	BNE SUB10
	LDA Q,X
	CMP #BUSI
	BEQ SUB30
	CMP #GRAPH
	BEQ SUB30
	LDA SFLAG1
	BEQ SUB90
	LDA #BUSI
	BNE SUB91
SUB90	LDA #GRAPH
SUB91	LDY TEMP
	STA Q,Y
	LDA #0
	STA BF10
SUB40	PLA
	PLA
	RTS
SUB30
	LDX TEMP
	STA Q,X
	LDA #0
	STA BF10
	JMP SUB40
SUB10
	CPX #0
	BEQ SUB20
	DEX
	RTS
SUB20
	LDX #QSIZ-1
	RTS
HOME	LDA CRPOS
	ORA CRPOS+1
	BEQ HOM20
	LDA CONDM
	TAY
	BEQ HOMM
	LDA #0
	STA CONDM
	LDA PORTB
	ORA #$02
	STA PORTB
	LDA #<CR2TIM
	STA CRTM
	LDA #>CR2TIM
	STA CRTM+1
HOMM	LDA #$80
	STA PTRRDY
	JSR SSW10
HOM10	LDA PTRRDY
	BNE HOM10
	STY CONDM
	TYA
	BEQ HOM20
	LDA #<CR3TIM
	STA CRTM
	LDA #>CR3TIM
	STA CRTM+1
	LDA PORTB
	AND #$FD
	STA PORTB
HOM20	RTS
;
;
	.END
