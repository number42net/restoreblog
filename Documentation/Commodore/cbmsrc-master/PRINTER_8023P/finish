	.PAG 'FINISH'
;
;*THIS MODULE IS ENTERED:
; 1. WHENEVER A CR, CS, OR LF
;    IS DETECTED, OR
; 2. WHENEVER THE SA CHANGES, OR
; 3. WHENEVER A ROUTINE IS FINISHED
;    ACCEPTING INPUT ON THE CURRENT
;    SA, AND IS WAITING FOR THE
;    SA TO CHANGE OR FOR A TERMINATOR.
; 4. WHEN A CHECK IS DESIRED ON THE
;    CURRENT CHAR FOR A TERMINATOR
;    (THE TERM AND TERML ENTRY)
;
FINI10	JSR IEEE ;FETCH ANOTHER CHAR
	BVS FINI90 ;IF VS, ERROR
FINI20	JSR TERML ;THIS CHAR TERMINAL?
	BCC FINI10 ;IF CC, NO
	JSR INSERT ;INSERT IT
	BCC FINI50 ;GO WAIT FOR LF OR SA CHANGE
;
FINI30	JSR IEEE ;FETCH ANOTHER CHAR
	BVS FINI90 ;IF VS, ERROR
FINI40	JSR TERML ;TERMINAL?
	BCC FINI30 ;IF CC, NO
;
FINI50	LDA DATA ;IF CR, FETCH NEXT BYTE
	CMP #CR ;TO SEE IF CRLF SEQUENCE.
	BNE FINI60 ;IF NE, NOT CRLF
	JSR IEEE
	BVS FINI70 ;IF VS, SA CHANGED, SO NOT CRLF
	CMP #LF ;IF LF, THROW AWAY
	BNE FINI70 ;IF NE, IT ISN'T,USE CHAR
FINI60	JMP MAIN00 ;THROW OUT LF
FINI70	JMP MAIN03
;
FINI90	LDY #'T ;MISSING TERMINATOR ERROR
	JMP ERROR
;
;*TERM & TERML-THIS ROUTINE DETECTS
; A CRLF SEQUENCE OR A TERMINATOR.
; IF A TERMINATOR IS DETECTED,
; I.E. A CR, CS OR LF, THE CARRY
; IS RETURNED SET.
;
TERML	LDA DATA
TERM	CMP #CR
	BEQ TRM10
	CMP #CS
	BEQ TRM10
	CMP #LF
	BEQ TRM10
	CLC ;NOT A TERMINATOR
	RTS
TRM10	SEC ;A TERMINATOR
	RTS
	.END
