.PAGE 'NEW'
;NEW: INITIALIZE A DISK, DISK IS 
;  SOFT-SECTORED, BIT AVAIL. MAP,
;  DIRECTORY, & 1ST BLOCK ARE ALL INITED
.SKIP
NEW	JSR ONEDRV
	LDA FILDAT      ;SET UP DRIVE #
	BPL N101
	LDA #BADFN
	JMP CMDERR
N101	AND #1
	STA DRVNUM
	JSR SETLDS
.SKIP
	JSR SETBMP
	LDA DRVNUM
	ASL A
	TAX
	LDY FILTBL+1    ;GET DISK ID
	CPY CMDSIZ      ;?IS THIS NEW OR CLEAR?
	BEQ N108        ;END OF CMD STRING
	LDA CMDBUF,Y
	STA DSKID,X     ;STORE IN PROPER DRIVE
	LDA CMDBUF+1,Y  ;(Y=0)
	STA DSKID+1,X
.SKIP
	LDA #1          ;...IN TRACK, TRACK=1
	STA TRACK
	JSR FORMAT      ;TRANSFER FORMAT TO RAM
	JMP N110
.SKIP
N108	JSR INITDR
	LDY #2
	LDA (BMPNT)Y    ;USE CURRENT VERSION #
	CMP VERNUM
	BEQ N110
	JMP VNERR
N110
	LDA #0
	TAY
.SKIP
N111	STA (BMPNT)Y    ;CLEAR BUFFER
	INY
	BNE N111
.SKIP
	LDA DRVNUM
	CLC
	ADC #BAMJOB     ;BUF #13 + DRVNUM
	STA JOBNUM
	ASL A
	TAX
	LDA #$90
	STA BUFTAB,X
.SKIP
	LDY #1
	STY SECTOR
	LDA #$FF
	STA (BMPNT)Y
	LDA #18
	STA TRACK
;
	JSR DRTWRT      ;CLEAR DIRECTORY
.SKIP
	JSR NEWMAP      ;NEW BAM
	LDY #2
	LDA VERNUM
	STA (BMPNT)Y    ;SET VERSION #
.SKIP
	JSR USEDTS      ;USE 18.1
	DEC SECTOR
	JSR USEDTS      ;USE 18.0
.SKIP
	LDY JOBNUM
	LDX FILTBL
	LDA #27
	JSR TRNAME      ;TRANSFER CMD BUF TO BUF0
.SKIP
	LDY #$12
	LDA DRVNUM      ;SET UP CURRENT I.D.
	ASL A
	TAX
	LDA DSKID,X
	STA (DIRBUF),Y
	INY
	LDA DSKID+1,X
	STA (DIRBUF),Y
.SKIP
	INY
	INY
	LDA #DOSVER+$30
	STA (DIRBUF)Y
	INY
	LDA VERNUM      ;SHOW VER #
	STA (DIRBUF)Y
.SKIP
	JSR DRTWRT      ;WRITE IT OUT
.SKIP
	JMP ENDCMD
.SKIP
.END
