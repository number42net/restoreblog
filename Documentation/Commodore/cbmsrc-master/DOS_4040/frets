.PAGE 'FRETS'
.SKIP 
; MARK A TRACK,SECTOR AS FREE IN BAM
;
FRETS
	JSR SETBMP      ;POINT BMPNT AT BAM
	JSR FREUSE      ;CALC INDEX INTO BAM
	SEC             ;FLAG FOR NO ACTION
	BNE FRERTS      ;FREE ALREADY
	LDA (BMPNT),Y   ;NOT FREE, FREE IT
	ORA BMASK,X
	STA (BMPNT),Y
	LDY TEMP        ;INDEX TO FREE SEC CNT
	LDA (BMPNT),Y   ;ADD ONE (C=1)
	ADC #0
	STA (BMPNT),Y
FRERTS	RTS
;
;TURN ON ACTIVITY LED SPECIFIED
; BY DRVNUM
;
SETLDS	LDA #$E7
	AND PBD2
	PHA
.SKIP
	LDA DRVNUM
	BEQ LEDS0
	PLA
	ORA #LED1
	BNE LEDS1
LEDS0
	PLA
	ORA #LED0
LEDS1
	STA PBD2
	RTS
;
ERROFF
	LDA PBD2
	AND #$FF-ERRLED
	STA PBD2
	RTS
.END
