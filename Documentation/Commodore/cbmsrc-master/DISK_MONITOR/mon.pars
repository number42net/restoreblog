.PAGE 'MON.PARS'
;
;
;
; PARSER FOR THE MONITOR
;
PARMS	LDA #0          ; DEFAULT ERROR RECOVERY ATTEMPTS OF 0
	STA RECOV
;
PAR002	JSR BASIN       ;GET NEXT CHAR
	CMP #SPC        ; IGNORE SPACE
	BEQ PAR002
	CMP #',         ; IGNORE ,
	BEQ PAR002
	CMP #CR         ; END OF LINE
	BNE PAR005
	RTS
;
PAR005	LDX #NUMPRM-1   ; PARM LOOKUP INDEX
PAR003	CMP PRMTBL,X    ; TEST IF PARM LETTER
	BNE PAR004      ; NOPE
;
	TXA             ; YES GET VAR. ADDRESS
	ASL A
	TAX
;
	LDA PRMADR,X
	STA PRMPTR
	LDA PRMADR+1,X
	STA PRMPTR+1
;
	JSR RDOB        ; GET THE VALUE
;
	LDY #0          ; STORE TWO BYTE VALUE
	STA (PRMPTR),Y
	JMP PAR002
;
PAR004	DEX
	BPL PAR003      ; TEST IF DONE
;
	JMP ERROR       ; BAD COMMAND
;
;
;  *GETADR
;
;  CHECK FOR ' ' OR ','
;
;  THEN READ HEX ADDRESS
;
;
GETADR	JSR SYNCHK      ; TEST FOR SPC OR ,
	JMP RDOA        ;GET ADR
;
;
SYNCHK	JSR RDOC        ; GET CHAR
	CMP #SPC
	BEQ SYNC10      ; OK
;
	CMP #',
	BEQ SYNC10      ; OK
;
	JMP ERROR       ; BAD SYNTAXXX
;
SYNC10	RTS
;
;
;
;
;
;
;
;
PRMTBL	.BYTE 'TSDJR'
;
; T: TRACK
; S: SECTOR
; D: DRIVE
; J: JOB #
; R: ROCOVERY
;
PRMADR	.WORD TRACK
	.WORD SECTOR
	.WORD DRIVE
	.WORD JOBNUM
	.WORD RECOV
;
;
.END
