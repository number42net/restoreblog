.PAGE 'MON.TWI'
;
;
;
;
;   *TRACE
;
;  TRACE AND PRINT FILE LINKS
;
;  $T TXX SXX DXX
;
;
TRACE	JSR PARMS       ; GET REQUIRED PARMS
	LDA #0          ; DOIT IN JOB 0
	STA JOBNUM
;
	LDA #SEEK       ; INIT THE JOB HEADER
	JSR DOIT
	JSR CRLF        ; NEXT LINE PLEASE
;
TRAC10	LDA #RDJOB      ; READ FIRST T,S
	JSR DOIT
;
	LDA #0          ; SET ADR1,ADR2,ADR3
	STA ADR1
	LDA #$11        ; ADR1 =$1100
	STA ADR1+1
	STA ADR2+1      ; ADR2= $1102
;
	LDA #02
	STA ADR2
;
	LDA #<BUF+10
	STA ADR3
	LDA #>BUF       ; ADR3 =BUF+10
	STA ADR3+1
;
	JSR MEMRD       ; GO READ DISK INTO PET
;
	LDA BUF+10      ; GET TRACK LINK
	STA TRACK
	JSR WROB        ; WRITE OUT BYTE
	LDA #'.         ; WRITE TT.SS
	JSR WROC
;
	LDA BUF+11      ;GET SECTOR LINK
	STA SECTOR
	JSR WROB        ; WRITE OUT SECTOR
	JSR SPACE
	JSR SPACE
	JSR SPACE
;
	LDA TRACK
	BNE TRAC10
;
	JMP STRT
;
;
;
;
;
;
;
;   *WEDGE
;
;  $>[COMMAND]
;
;  SEND A DIRECT DISK COMMAND
;
WEDGE	LDX #0          ; BUF POINTER
;
WEDG10	JSR BASIN       ; GET COMMAND
	CMP #CR
	BEQ WEDG20      ;DONE
;
	STA BUF,X       ; SAVE AWAY
	INX
	BNE WEDG10
;
WEDG20	DEX             ; DEC BUF LEN
;
	JSR SNDCMD
	JMP PRTERR
;
;
;
;
;
;
;  *INIT
;
;  $IXX
;
;  INIT JOB HEADER WITH DISK ID
;
;
INIT	JSR PARMS
;
	LDA #SEEK       ; DO A SEEK ON JOB
	JSR DOIT
	JMP PRTERR      ; READ ERR CHANNEL
;
;
;
;
.END
