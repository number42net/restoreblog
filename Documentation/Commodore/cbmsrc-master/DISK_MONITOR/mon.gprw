.PAGE 'MON.GPRW'
;
;
;
;  GET/PUT COMMANDS
;  WRITE/READ COMMANDS
;
;  GET - GET DISK MEMORY IMAGE AND
;        PUT INTO PET MEMORY
;
; .$G ADR1,ADR2,ADR3
;
; GET DISK MEMORY (ADR1) TO (ADR2)
; AND PUT INTO PET MEMORY STARTING AT (ADR3)
;
;
GET	JSR RDADR3      ; GET 3 ADDRESS PARMS
	JSR MEMRD       ; READ THE RANGE OVER
	JMP STRT        ; BACK TO MONITOR
;
;
;
; PUT - PUT PET MEMORY IMAGE INTO DISK MEMORY
;
;
; .$P ADR1,ADR2,ADR3
;
; PUT PET MEMORY IMAGE FROM (ADR1) TO (ADR2) INTO DISK MEMORY
; STARTING AT (ADR3)
;
;
PUT	JSR RDADR3      ;READ 3 ADDRESS PARMS
	JSR MEMWRT      ; WRITE MEMORY TO DISK
	JMP STRT        ; BACK TO MONITOR
;
;
;
;
;
;   READ COMMAND
;
; .$R T,S,D,B
;
;  READ (TRACK),(SECTOR),(DRIVE),(BUFFER)
;
;
READ	JSR PARMS       ; READ JOB PARMS
;
	LDA #RDJOB      ; DO A READ JOB
	JSR DOIT
;
	JSR PRTERR      ; PRINT ERROR CHANNEL
;
	JMP STRT
;
;
;
;
;
; WRITE COMMAND
;
; SYNTAX - $W (TRACK),(SECTOR),(DRIVE),(BUFFER)
;
; WRITE TRACK,SECTOR,DRIVE,BUFFER TO DISK
;
;
;
WRITE	JSR PARMS       ; READ JOB PARMS
;
	LDA #WRTJOB     ; DO A WRITE SECTOR
;
	JSR DOIT
;
	JSR PRTERR      ; PRINT ERROR CHANNEL
	JMP STRT
;
;
;
;   MEMWRT
;   WRITE (ADR1)-(ADR2) TO DISK AT
;  (ADR3)
;
MEMWRT	JSR SETMW       ; STORE M-W
;
	INC ADR2        ; INC ADR2 FOR TEST LATER
	BNE MEMW10
	INC ADR2+1
;
MEMW10	LDA ADR3        ; STORE ADDRESS
	STA BUF+3
	LDA ADR3+1
	STA BUF+4
;
; NUMBER OF CHARS STORED LATER AT BUF+5
;
	LDX #6          ; STORE DATA STARTING AT 6
	LDY #0
;
MEMW20	LDA (ADR1),Y    ;GET DATA
	STA BUF,X
;
	INC ADR1        ; INC FETCH ADRS
	BNE MEMW30
	INC ADR1+1
;
MEMW30	JSR TSAD12      ; TEST IF DONE
;
	BCC MEMW40      ; NOT YET
	BCS MEMW70      ; END OF MEM WRITE
;
MEMW40	CPX #39         ; TEST IF BUFFER FULL
	BCS MEMW60      ;YUP , SEND THE BUFFER
;
	INX             ; NOT YET, KEEP ON STUFFIN
	BNE MEMW20
;
MEMW60	LDA #34
	STA BUF+5       ; SET NUMBER OF CHARS
;
	JSR SNDCMD      ; SEND THE BUFFER
;
	LDA ADR3        ; UPDATE POINTER
	CLC             ; ADVANCE DISK MEM POINTER
	ADC #34         ; 34 MORE BYTES
	STA ADR3
	BCC MEMW80
	INC ADR3+1
;
;
MEMW80	JMP MEMW10      ; BACK TO THE TOP
;
;
MEMW70	STX LEN         ; SET BUFFER LEN
	TXA
	SEC             ; SUB 'M-WLHN' FROM LEN
	SBC #6
	STA BUF+5       ; SET N
;
	JMP SNDCMD      ; SEND IT AND RETURN
;
;
RDADR3	JSR RDADR2      ; READ 3 ADDR PARMS
;
	JSR GETADR
	LDA TMPO        ; GET ADR3
	STA ADR3
	LDA TMPO+1
	STA ADR3+1
;
	RTS
;
;
;
RDADR2	JSR GETADR      ; READ 3 ADDR PARMS
	LDA TMPO        ; GET ADR1
	STA ADR1
	LDA TMPO+1
	STA ADR1+1
;
	JSR GETADR
	LDA TMPO        ; GET ADR2
	STA ADR2
	LDA TMPO+1
	STA ADR2+1
;
	RTS
;
;
;
.END
