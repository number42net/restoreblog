.PAGE 'MON.UTL'
;
;
;  MONITOR UTILITY ROUTINES
;
;
;ROUTINE: SNDCMD
;
; SEND BUF TO COMMAND CHANNEL OF DISK
;
;
SNDCMD	STX LEN         ; LEN OF BUFFER
;
	LDA #$28        ; SETUP LISTEN FA
	STA FA
;
	LDA #$60+15     ; SETUP SA
	STA SA
;
	JSR LISTN       ; TELL DISK TO LISTEN
	LDA SA
	JSR SECND       ; TO COMMAND CHANNEL
;
	LDX #0          ; ZERO BUFFER POINTR
;
SNDC10	LDA BUF,X       ; GET CHAR
	JSR CIOUT       ; SEND THE CHAR
;
	INX             ; ADVANCE BUFFER POINTER
;
	DEC LEN         ;TEST IF DONE
	BPL SNDC10
	JMP UNLSN       ; UNLISTEN DISK
;
;
;
;
; ROUTINE: RDCMD
;
; READ ONE CHAR FROM COMMAND CHANNEL
;
;
RDCMD	LDA #$48        ; SET UP TALK  FA
	STA FA
;
	JSR TALK        ; TELL DISK TO TALK
	LDA #$6F        ; SPECIFY SEC ADR
	JSR TKSA
;
	JMP ACPTR       ; GET THE CHAR
;
;
;
;
; ROUTINE: MEMRD
;
; READ DISK MEMORY FROM (ADR1)-(ADR2) INTO (ADR3)
;
; DONE USING MEMORY READ COMMAND
;
MEMRD	JSR SETMR       ; PUT M-R IN BUF
;
MEMR10	LDA ADR1        ; GET LOW BYTE
	STA BUF+3
;
	LDA ADR1+1      ; GET HI BYTE
	STA BUF+4
;
	LDX #4          ; SEND BUF(4)
	JSR SNDCMD
;
	JSR RDCMD       ; READ CHAR BACK
;
	LDY #0
	STA (ADR3),Y    ; STORE IT AWAY
;
	INC ADR3        ; ADVANCE POINTER
	BNE MEMR15
	INC ADR3+1
;
MEMR15	JSR TSAD12      ; TEST IF DONE
;
	BCC MEMR20      ; NOT YET
;
	RTS             ; IT IS DONE
;
MEMR20	INC ADR1
	BNE MEMR10
	INC ADR1+1
	JMP MEMR10
;
;
;
;
;
; ROUTINE: PRTERR
;
; READ AND PRINT DISK ERROR CHANNEL TO PET
;
;
PRTERR	JSR CRLF        ; DO CRLF
PRTE10	JSR RDCMD
	JSR WROC        ; WRITE OUT CHAR(.A)
	CMP #CR
	BNE PRTE10
	JMP STRT
;
;
;
;
;   *TSAD12
;
;   CMP ADR1 AND ADR2
;
;   .C=1 ADR1>=ADR2
;
;   .C=0 ADR1<ADR2
;
TSAD12	SEC
	LDA ADR1
	SBC ADR2
	LDA ADR1+1
	SBC ADR2+1
	RTS
;
;
;
.END
