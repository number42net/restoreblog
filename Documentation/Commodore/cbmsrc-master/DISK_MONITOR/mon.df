;
.PAGE 'MON.DF'
;
;
;
;   *DUMP
;
;   WRITE BLOCK OF MEMORY TO DISK
;
;   $D ADR1  TXX SXX DXX RXX
;
;
;
;   DUMP PAGE (ADR1) TO DISK D.T.S
;
;
DUMP	JSR GETADR      ; GET ADR1 PART
;
	LDA TMPO
	STA ADR1
	STA ADR2
;
	LDA TMPO+1
	STA ADR1+1      ; SET ADR1
	STA ADR2+1
	INC ADR2+1      ; SET ADR2=ADR1+$100
;
	LDA #0          ; SET ADR3 =$1100
	STA ADR3
	LDA #$11
	STA ADR3+1
;
	JSR PARMS       ; READ PARMS
;
	LDA #0          ; USE ONLY JOBNUM 0
	STA JOBNUM
;
	JSR MEMWRT      ; TRANSFER PET TO DISK
;
	LDA #SEEK       ; SET UP JOB HEADER
	JSR DOIT
;
	LDA #WRTJOB     ; WRITE TO DISK
	JSR DOIT
;
	JMP PRTERR      ; READ ERROR CHANNEL
;
;
;
;
;
;   *FETCH
;
;   READ TRACK.SECTOR INTO PET
;
;
;   $F ADR1 PARMS
;
;   .$F 1000 T01 S01 D00 R04
;
;
;
FETCH	JSR GETADR      ; GET ADR1
	LDA TMPO        ; SETUP ADR3
	STA ADR3
	LDA TMPO+1
	STA ADR3+1
;
	JSR PARMS       ; GET REST OF PARMS
;
	LDA #0          ; FORCE JOBNUM 0
	STA JOBNUM
;
	LDA #SEEK
	JSR DOIT        ; INIT HEADER
;
	LDA #RDJOB      ; READ SECTOR IN
	JSR DOIT
;
	LDA #0          ; SET UP ADR1 AND ADR2
	STA ADR1
	STA ADR2
;
	LDA #$11        ; ADR1= $1100
	STA ADR1+1
	STA ADR2+1      ; ADR2= $1200
	INC ADR2+1
;
	JSR MEMRD       ; READ IT IN
;
	JMP STRT
;
;
;
;
.END
