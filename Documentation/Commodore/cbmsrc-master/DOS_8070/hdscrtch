; SCRATCH FILE(S)
SCRTCH	JSR FS1SET      ;SET UP FOR 1 STREAM
	JSR ALLDRS
	JSR OPTSCH
	LDA #0
	STA R0          ;USED AS FILE COUNT
	JSR FFST
	BMI SC30
SC15	JSR TSTCHN      ;IS IT ACTIVE ?
	BCC SC25        ;YES - DONT SCRATCH
	LDY #0
	LDA (DIRBUF),Y
	AND #$40        ;LOCK BIT
	BNE SC25        ;IT'S LOCKED
;
	LDA (DIRBUF),Y  ;*
	AND #7          ;*
	PHA             ;SAVE FILE TYPE
	JSR DELDIR      ;DELETE DIR ENTRY <FIX>
	PLA             ;RESTORE FILE TYPE
	CMP #RELTYP     ;*
	BNE SC17        ;BR IF NOT REL FILE
	LDY #19         ;IS THIS A RELATIVE ?
	LDA (DIRBUF),Y  ;<HD>
	STA TRACK       ;<HD>- SAVE TRACK
	INY
	LDA (DIRBUF),Y  ;GET SECTOR
	STA SECTOR
	JSR DELFIL      ;DELETE BY LINKS
SC17	LDX ENTFND
	LDA #$20
	AND PATTYP,X
	BNE SC20        ;CREATED, NOT CLOSED
.SKIP
	LDA FILTRK,X    ;DELETE BY LINKS
	STA TRACK
	LDA FILSEC,X
	STA SECTOR
	JSR DELFIL
.SKIP
SC20	INC R0
SC25	JSR FFRE
	BPL SC15
.SKIP
SC30	LDA R0          ;FINISHED, SET
	STA TRACK       ; FILE COUNT
	LDA #1
	LDY #0
	JMP SCREND      ;END OF SCRATCH
.SKIP
DELFIL	JSR FRETS       ;DELETE FILE BY LINKS
	JSR OPNIRD      ;UPDATE BAM
DEL2	LDA #0
	JSR SETPNT
	JSR RDBYT
	STA SECTOR      ;SECTOR FIRST <HD>
	JSR RDBYT
	STA TRACK       ;TRACK SECOND <HD>
	LDA SECTOR
	CMP #255        ;LINK = EOL <HD>
	BNE DEL1
	JSR MAPOUT
	JMP FRECHN
DEL1	JSR FRETS
	JSR NXTBUF
	JMP DEL2
.SKIP
DELDIR	LDY #0          ;DELETE DIR ENTRY
	TYA
	STA (DIRBUF),Y
	JSR WRTOUT
	JMP WATJOB
.SKIP
.END
