.PAGE 'BEGIN DOS'
BEGIN
	LDX #$FF
	TXS             ; INITIALIZE STACK
	LDA PBD2
	AND #255-ERRLED-LED0-LED1
	STA PBD2        ; TURN LEDS OFF
	LDA PBD2        ; COMPUTE PRIMARY ADDR
	AND #7
	ORA #$48        ; TALK ADDRESS
	STA TLKADR
	EOR #$60        ; LISTEN ADDRESS
	STA LSNADR
;
; INITIALIZE BUFFER POINTER TABLE
;
INTTAB
	LDX #0
	LDY #0
INTT1
	LDA #0
	STA BUFTAB,X
	INX
	LDA BUFIND,Y
	STA BUFTAB,X
	INX
	INY
	CPY #BFCNT+2
	BNE INTT1
	LDA #<CMDBUF    ; SET POINTER TO COMMAND BUFFER
	STA BUFTAB,X
	INX
	LDA #>CMDBUF
	STA BUFTAB,X
	INX
	LDA #<ERRBUF    ; SET POINTER TO ERROR BUFFER
	STA BUFTAB,X
	INX
	LDA #>ERRBUF
	STA BUFTAB,X
	LDA #$FF
	LDX #MAXSA
DSKIN1	STA LINTAB,X
	DEX
	BPL DSKIN1
	LDX #MXCHNS-1
DSKIN2
	STA BUF0,X      ; SET BUFFERS AS UNUSED
	STA BUF1,X
	STA SS,X
	DEX
	BPL DSKIN2
	LDA #BFCNT+2    ; SET BUFFER POINTERS
	STA BUF0+CMDCHN
	LDA #BFCNT+3
	STA BUF0+ERRCHN
	LDA #ERRCHN
	STA LINTAB+ERRSA
	LDA #CMDCHN+$80
	STA LINTAB+CMDSA
	LDA #LXINT      ; LINDX 0 TO 5 FREE
	STA LINUSE
	LDA #RDYLST
	STA CHNRDY+CMDCHN
	LDA #RDYTLK
	STA CHNRDY+ERRCHN
	LDA #0
	STA BUFUSE
	LDA #$F0
	STA BUFUSE+1
	JSR USRINT      ; INITIALIZE USER JUMP TABLE
;
; SET INDIRECT VECTORS
;
	LDA #<DIAGOK
	STA VNMI
	LDA #>DIAGOK
	STA VNMI+1
	LDA #5          ; SET UP SECTOR ALLOCATE
	STA SECINC
	LDA #5
	STA REVCNT      ; SET UP RECOVERY COUNT
;
; CONFIGURE THE TABLES ACCORDING TO DOS
;
CONFIG
	LDX DOS
	LDA BMSIZE,X
	STA BAMSIZ      ; SET BAM SIZE
	LDA MPOFF,X
	STA MAPOFF      ; SET MAP OFFSET
	LDA LOWTRK,X
	STA LOTRK       ; SET LOW TRACK IN BAM
	LDA DISKNM,X
	STA DSKNAM      ; DISK NAME OFFSET
	LDA DOS
	BEQ CONFI6      ; 4040 DOS
;
; 8050/8250 DOS
;
	LDA #39
	STA DIRTRK      ; DIRECTORY TRACK
	LDA #'C
	STA VERNUM      ; 8050/8250 VERSION
	LDX #4
	LDA NSIDES      ; # OF SIDES ON DISK
	LSR A
	BNE CONFI1      ; DOUBLE SIDED 8250
	DEX             ; SINGLE SIDED 8050
	DEX             ; .X = 2
	BNE CONFI2      ; JUMP AROUND DOUBLE SIDED STUFF
.PAGE
;
; 8250, DOUBLE SIDED
;
CONFI1
	LDA BMT80D,X
	STA BAMTRK,X    ; BAM TRACK TABLE
	LDA BMS80D,X
	STA BAMSEC,X    ; BAM SECTOR TABLE
	DEX
	BPL CONFI1
	INX             ; .X = 0
	STX SWITCH      ; 8250, BIG RELATIVE FILE
	LDA #155
	STA MAXTRK      ; MAX TRACK+1 ON DISK
	LDA #77
	STA MTRACK      ; # OF TRACKS PER SIDE
	BNE CONFI3      ; JUMP AROUND SINGLE SIDED STUFF
;
; 8050, SINGLE SIDED
;
CONFI2
	LDA BMT80S,X
	STA BAMTRK,X    ; BAM TRACK TABLE
	LDA BMS80S,X
	STA BAMSEC,X    ; BAM SECTOR TABLE
	DEX
	BPL CONFI2
	STX SWITCH      ; 8050, NO BIG RELATIVE FILE
	LDA #78
	STA MAXTRK      ; MAX TRACK+1 ON DISK
	LDX #0
	STX MTRACK      ; 0 FOR SINGLE SIDED
;
; 8050/8250 COMMON STUFF
;
CONFI3
	LDX #6
	LDA NSEC80,X
	STA TLGAP       ; MAX TAIL GAP
	DEX
CONFI4
	LDA NSEC80,X
	STA NUMSEC,X    ; # OF SECTORS/ZONE AND GAPS
	DEX
	BPL CONFI4
	LDX #7
CONFI5
	LDA NTRK80,X
	STA TRKNUM,X    ; TRACK ZONE BOUNDARIES
	DEX
	BPL CONFI5
	BMI CONFI9      ; JUMP AROUND 4040 STUFF
;
; 4040 DOS
;
CONFI6
	LDA #1
	STA NSIDES      ; SINGLE SIDED ONLY FOR 4040
	LDA #18
	STA DIRTRK      ; DIRECTORY TRACK
	STA BAMTRK      ; BAM TRACK FOR 4040
	LDA #'A
	STA VERNUM      ; 4040 VERSION
	LDA #36
	STA MAXTRK      ; MAX TRACK+1 ON DISK
	LDA #0
	STA MTRACK      ; 0 FOR SINGLE SIDED
	STA BAMSEC      ; BAM SECTOR FOR 4040
	LDX #6
	LDA NSEC40,X
	STA TLGAP       ; MAX TAIL GAP
	DEX
CONFI7
	LDA NSEC40,X
	STA NUMSEC,X    ; # OF SECTORS/ZONE AND GAPS
	DEX
	BPL CONFI7
	LDX #3
CONFI8
	LDA NTRK40,X
	STA TRKNUM,X    ; TRACK ZONE BOUNDARIES
	DEX
	BPL CONFI8
;
; COMMON DOS VARIABLES
;
CONFI9
	LDX #7
CONF10
	LDA MSCCOM,X
	STA NZONES,X    ; COMMON DOS VARIABLES
	DEX
	BPL CONF10
;
; SET VECTOR TO IDLE LOOP
;
	LDA #<VIDLE
	STA VECIDL
	LDA #>VIDLE
	STA VECIDL+1
;
; SET UP POWER UP MESSAGE
;
MESAGE
	LDA #CBMVER     ; DOS VERSION NUMBER
	JSR ERRTS0      ; TRANSFER MESSAGE
;
; ALLOW 'ATN' TO INTERRUPT
;
	STA ATNPE
	JMP BOOT        ; CHECK POWER UP BOOT
;
; MUST BE CONTIGUOUS TO IDLE ROUTINE
;
.END
