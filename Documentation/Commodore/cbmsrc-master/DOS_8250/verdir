.PAGE 'VER-DIR'
;
; VALIDATE FILES WITH BAM
; CREATE NEW BAM ACCORDING TO
; CONTENTS OF FILES ENTERED IN DIR
;
VERDIR
VALDAT
	JSR SIMPRS      ; EXTRACT DRIVE #
	JSR INITDR
	JSR VSETB
	LDX DRVNUM
	STA NDBL,X      ; .A = 0
	STA NDBH,X      ; CLEAR FOR NEW MAP
	JSR NEWMPV      ; SET NEW BAM
	LDA #0
	STA DELIND
	JSR SRCHST      ; SEARCH FOR FIRST FILE
	BNE VD25        ; FOUND ONE
VD10
	LDA #0          ; SET DIRECTORY SECTORS
	STA SECTOR      ; IN BAM
	LDA DIRTRK
	STA TRACK
	JSR VMKBAM
	LDA DRVNUM
	JSR WTMAPS      ; WRITE OUT BAMS
	JMP ENDCMD
VD15
	INY
	LDA (DIRBUF),Y
	PHA             ; SAVE TRACK
	INY
	LDA (DIRBUF),Y
	PHA             ; SAVE SECTOR
	LDY #19         ; GET SS TRACK
	LDA (DIRBUF),Y  ; IS THIS RELATIVE?
	BEQ VD17        ; NO
	STA TRACK       ; YES, SAVE TRACK
	INY
	LDA (DIRBUF),Y  ; GET SS SECTOR
	STA SECTOR
	JSR VMKBAM      ; VALIDATE SS BY LINKS
VD17
	PLA
	STA SECTOR      ; NOW DO DATA BLOCKS
	PLA
	STA TRACK
	JSR VMKBAM      ; SET BIT USED IN BAM
VD20
	JSR SRRE        ; SEARCH FOR MORE
	BEQ VD10        ; NO MORE FILES
VD25
	LDY #0
	LDA (DIRBUF),Y
	BMI VD15
	JSR DELDIR      ; NOT CLOSED DELETE DIR
	JMP VD20
VMKBAM
	JSR TSCHK
	JSR VUSED
	JSR OPNIRD
MRK2
	LDA #0
	JSR SETPNT
	JSR GETBYT
	STA TRACK
	JSR GETBYT
	STA SECTOR
	LDA TRACK
	BNE MRK1
	JMP FRECHN
MRK1
	JSR VUSED
	JSR NXTBUF
	JMP MRK2
;
; MARK TRACK, SECTOR AS USED
;
VUSED
	LDA #>VBAMS-1
	STA BMPNT+1
	LDA #<VBAMS
	STA BMPNT
	LDY #HITRK
VU10
	INC BMPNT+1
	LDA DOS
	BEQ VUSED1      ; 4040 BAM
	LDA TRACK
	CMP (BMPNT),Y
	BCS VU10
VUSED1
	JSR MBAM        ; .Y = BAM INDEX
	STY TEMP        ; SAVE INDEX
	JSR AVCK        ; CHECK TRACK FOR MISMATCH BITS
	LDA SECTOR      ; .A = SECTOR/8
	LSR A
	LSR A
	LSR A           ; FOR WHICH OF THREE BYTES
	SEC
	ADC TEMP        ; CALC INDEX
	TAY
	LDA SECTOR      ; BIT IN THAT BYTE
	AND #7
	TAX
	LDA (BMPNT),Y   ; GET THE BYTE
	AND BMASK,X     ; TEST IT
	BEQ USDERR      ; USED, ERROR
	LDA (BMPNT),Y   ; GET BITS
	EOR BMASK,X     ; MARK SECTOR USED
	STA (BMPNT),Y
	LDY TEMP        ; INDEX TO FREE SEC CNT
	LDA (BMPNT),Y   ; GET COUNT
	SEC
	SBC #1          ; DEC ONE (C = 0)
	STA (BMPNT),Y   ; SAVE IT
	LDA TRACK
	CMP DIRTRK
	BEQ VU20
	LDX DRVNUM
	LDA NDBL,X
	BNE VU15
	DEC NDBH,X
VU15
	DEC NDBL,X
VU20
	RTS
USDERR
	LDA #NOBLK
	JSR CMDER2
.PAGE
VSETB
	LDA #>VBAMS
	STA BMPNT+1
	LDA #<VBAMS
	STA BMPNT
	JMP CLRBAM
WTMAPS
	LDA #0          ; START WITH 1ST BAM
	STA R0
WM10
	LDX R0
	LDA VBMBUF,X
	STA JOBNUM
	ASL A
	ASL A
	ASL A
	TAY
	LDA BAMTRK+1,X  ; CHECK HEADER OF BAMS
	BNE WM20        ; NULL TERMINATES LIST
	RTS
WM20
	LDA BAMTRK,X    ; SET HEADER OF BAMS
	STA HDRS+2,Y
	LDA BAMSEC,X
	STA HDRS+3,Y
	LDA DRVNUM
	ASL A
	TAX
	LDA DSKID,X
	STA HDRS,Y
	LDA DSKID+1,X
	STA HDRS+1,Y
	INC R0
	JSR DOWRIT      ; WRITE IT OUT
	JMP WM10
;
VBAMS	= $1D00         ; IMAGE OF $1100
VBMBUF
	.BYTE 0,1,2,3
.END
