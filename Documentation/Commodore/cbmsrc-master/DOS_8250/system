.PAGE 'SYSTEM ROUTINES'
;
; KILL PROTECTION
;
KILLP
	PHA
	LDA #1
	STA CFLG2       ; TELL CONTOLLER
KILLP2
	LDA CFLG2       ; WAIT UNTIL HE'S GOT IT
	BNE KILLP2
	PLA
	RTS
;
; CHECK FOR BIG RELATIVE FILE
;
BIGREL
	LDA DOS
	BNE BIGRE1      ; 8050/8250
	LDA #$FF
	RTS             ; 4040
BIGRE1
	LDA SWITCH      ; BIG REL SWITCH
	RTS
;
; READ/WRITE THE SUPER SIDE SECTOR (8250)
;
RDSSS
	LDX LINDX
	LDA #254
	CMP SSSGRP,X
	BNE RDSSSA      ; SS NOT RESIDENT, READ IT
	RTS
RDSSSA
	LDA #READ
	.BYTE $2C       ; SKIP NEXT TWO BYTES
WRTSSS
	LDA #WRITE
	PHA
	JSR SETDRN
	LDX LINDX
	LDA SSSTRK,X
	STA TRACK       ; SET TRACK
	LDA SSSSEC,X
	STA SECTOR      ; SET SECTOR
	LDA #255
	STA SSSGRP,X
	LDA SS,X        ; GET SS BUFFER #
	STA JOBNUM
	JSR SETH        ; SET THE JOB HEADER
	PLA             ; GET JOB
	JSR DOJOB       ; DO THE JOB
	LDX LINDX
	LDA #254
	STA SSSGRP,X    ; MARK SSS RESIDENT
	RTS
;
; READ LAST GROUP (8250)
;
RDLG
	LDA #$5A
	STA GRPNUM
	JSR RDSS1
	BNE RDLG1
	RTS
RDLG1
	DEC GRPNUM
	LDA GRPNUM
	JSR RDSS1C
	BNE RDLG1
	RTS
;
; READ SIDE SECTOR 1 OF GROUP N (8250)
;
RDSS1
	LDX LINDX
	CMP SSSGRP,X
	BNE RDSS1A
	RTS
RDSS1A
	PHA             ; SAVE DESIRED GROUP #
	JSR RDSSS       ; READ THE SSS
RDSS1D
	JSR SETDRN      ; SET THE DRIVE #
	LDA #3
	JSR SSDIR       ; POINT TO THE FIRST GROUP
	PLA             ; RESTORE DESIRED GROUP #
RDSS1C
	TAX             ; SAVE DESIRED GROUP #
	ASL A
	TAY
	LDA (DIRBUF),Y  ; GET THE TRACK
	BNE RDSS1B      ; DOES EXSIT
	ORA #255        ; DOES NOT EXIST (RETURN CODE)
	RTS
RDSS1B
	STA TRACK
	INY
	LDA (DIRBUF),Y  ; GET THE SECTOR
	STA SECTOR
	TXA             ; RESTORE DESIRED GROUP #
	PHA
	LDA #255
	LDX LINDX
	STA SSSGRP,X    ; MARK GROUP NONRESIDENT
	LDA SS,X        ; GET SS BUFFER #
	STA JOBNUM
	JSR SETH        ; SET THE JOB HEADER
	LDA #READ
	JSR DOJOB       ; READ THE SS IN
	LDX LINDX
	PLA
	STA SSSGRP,X    ; MARK THE GROUP AS RESIDENT
	LDA #0          ; RETURN CODE
	RTS
;
; CHECK FOR LEGAL TRACK AND SECTOR
; .C = 0 IF LEGAL
; .C = 1 IF ILLEGAL
;
JILLTS
	LDA TRACK
	BEQ ILLTS1
	CMP MAXTRK
	BCS ILLTS1
	JSR MAXSEC
	CMP SECTOR
	BEQ ILLTS1
	BCC ILLTS1
	CLC
	RTS
ILLTS1
	SEC
	RTS
.END
