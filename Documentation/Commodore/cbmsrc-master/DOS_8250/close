.PAGE 'CLOSE'
;
; CLOSE THE FILE ASSOCIATED WITH SA
;
CLOSE	LDA SA
	BNE CLS10       ; DIRECTORY CLOSE
	LDA #0
	STA DIRLST      ; CLEAR DIR LIST
	JSR FRECHN
CLS05
	JMP FREICH
CLS10
	CMP #$F
	BEQ CLSALL      ; CLOSE CMD CHANL
	JSR CLSCHN      ; CLOSE CHANNEL
	LDA SA
	CMP #2
	BCC CLS05
	JMP ENDCMD
;
; CLOSE ALL SECONDARY CHANNELS
;
CLSALL
	LDA #14
	STA SA
CLS20
	JSR CLSCHN
	DEC SA
	BPL CLS20
	JMP ENDCMD
;
; CLOSE SECONDARY ADDRESS (SA)
;
CLSCHN
	LDX SA
	LDA LINTAB,X
	CMP #$FF
	BNE CLSC28
	RTS
CLSC28
	AND #$F
	STA LINDX
	JSR TYPFIL
	CMP #DIRTYP
	BEQ CLSC30      ; DIRECT CHANNEL
	CMP #RELTYP
	BEQ CLSREL
	JSR FNDWCH      ; LOOK FOR WRITE CHANNEL
	BCS CLSC31
	JSR CLSWRT      ; CLOSE SEQ WRITE
	JSR CLSDIR      ; CLOSE DIRECTORY
CLSC30
	JSR MAPOUT      ; WRITE BAM OUT
CLSC31
	JMP FRECHN
CLSREL
	JSR SCRUB
	JSR DBLBUF
	JSR SSEND
	LDX SSNUM
	STX T4
	JSR BIGREL      ; BIG RELATIVE FILE?
	BEQ CLSBIG      ; YES
	INC T4
	LDA #0
	STA T1
	STA T2
	LDA SSIND
	SEC
	SBC #SSIOFF-2
	STA T3
	JSR SSCALC
	LDX LINDX
	LDA T1
	STA NBKL,X
	LDA T2
	STA NBKH,X
CLSRE1
	LDA #DYFILE
	JSR TSTFLG      ; TEST IF FILE WRITTEN TO
	BEQ CLSR1
	JSR CLSDIR
CLSR1
	JMP FRECHN
CLSBIG
	LDA SSIND
	SEC
	SBC #SSIOFF-2
	STA T3
	LDA GRPNUM
	STA R3
	JSR SSSCAL
	LDX #0
	LDA #2
	JSR ADDLIT
	LDX LINDX
	LDA RESULT
	STA NBKL,X
	LDA RESULT+1
	STA NBKH,X
	JMP CLSRE1
;
; CLOSE A WRITE CHANL
;
CLSWRT
	LDX LINDX
	LDA NBKL,X
	ORA NBKH,X
	BNE CLSW10      ; AT LEAST 1 BLOCK WRITTEN
	JSR GETPNT
	CMP #2
	BNE CLSW10      ; AT LEAST 1 BYTE WRITTEN
	LDA #CR
	JSR PUTBYT
CLSW10
	JSR GETPNT
	CMP #2
	BNE CLSW20      ; NOT MT BUFFER
	JSR DBLBUF      ; SWITCH BUFS
	LDX LINDX
	LDA NBKL,X
	BNE CLSW15
	DEC NBKH,X
CLSW15
	DEC NBKL,X
	JSR GETLNK
	JSR FRETS       ; FREE EXTRA SECTOR
	LDA #0
CLSW20
	SEC
	SBC #1          ; BACK UP 1
	PHA             ; SAVE IT
	LDA #0
	JSR SETPNT
	JSR PUTBYT      ; TRACK LINK = 0
	PLA             ; LSTCHR COUNT
	JSR PUTBYT
	JSR WRTBUF      ; WRITE OUT LAST BUFFER
	JSR WATJOB      ; FINISH JOB UP
	JMP DBLBUF      ; MAKE SURE BOTH BUFS OK
;
; DIRECTORY CLOSE ON OPEN WRITE FILE
;
CLSDIR
	LDX LINDX       ; SAVE LINDX
	STX WLINDX      ; AND SA
	LDA SA
	PHA
	LDA DSEC,X      ; GET DIRECTORY SECTOR
	STA SECTOR
	LDA DIND,X      ; GET SECTOR OFFSET
	STA INDEX
	LDA FILTYP,X    ; DRIVE # IN FILTYP
	AND #1
	STA DRVNUM
	LDA DIRTRK
	STA TRACK
	JSR GETACT      ; ALLOCATE A BUFFER
	PHA
	STA JOBNUM
	JSR DRTRD       ; READ DIRECTORY SECTOR
	LDY #0
	LDA BUFIND,X    ; .X IS JOB#
	STA R0+1
	LDA INDEX
	STA R0
	LDA (R0),Y
	AND #$20
	BEQ CLSD5
	JSR TYPFIL
	CMP #RELTYP
	BEQ CLSD6
	LDA (R0),Y
	AND #$8F        ; REPLACE FILE
	STA (R0),Y
	INY
	LDA (R0),Y
	STA TRACK
	STY TEMP+2
	LDY #27         ; EXTRACT REPLACEMENT LINK
	LDA (R0),Y      ; TO LAST SECTOR
	PHA
	DEY
	LDA (R0),Y
	BNE CLSD4
	STA TRACK
	PLA
	STA SECTOR
	LDA #$67
	JSR CMDER2
CLSD4
	PHA
	LDA #0
	STA (R0)Y
	INY
	STA (R0)Y
	PLA
	LDY TEMP+2
	STA (R0),Y
	INY
	LDA (R0),Y
	STA SECTOR
	PLA
	STA (R0),Y
	JSR DELFIL      ; DELETE OLD FILE
	JMP CLSD6       ; SET CLOSE BIT
CLSD5
	LDA (R0),Y
	AND #$F
	ORA #$80
	STA (R0),Y
CLSD6	LDX WLINDX
	LDY #28         ; SET # OF BLOCKS
	LDA NBKL,X
	STA (R0),Y
	INY
	LDA NBKH,X
	STA (R0),Y
	PLA
	TAX
	LDA #WRITE      ; WRITE DIRECTORY SECTOR
	ORA DRVNUM
	JSR DOIT
	PLA
	STA SA
	JMP FNDWCH      ; RESTORE LINDX
.END
