.PAGE 'DUPLICATE'
;
; DUPLICATE DISK
;
DUPLCT
	JSR PRSEQ
	LDA FILDRV+1
	STA DRVNUM
	LDA #LED0+LED1
	ORA PBD2
	STA PBD2
	JSR INITDR      ; INIT SRC
	JSR SETBMP
	LDY #2
	LDA (BMPNT),Y
	CMP VERNUM
	BEQ DUP1
	JMP VNERR
DUP1
	JSR TOGDRV
	ASL A
	TAY
	EOR #2
	TAX
	LDA DSKID,X
	STA DSKID,Y
	LDA DSKID+1,X
	STA DSKID+1,Y
	JSR SETBMP
	LDY #2
	LDA VERNUM
	STA (BMPNT),Y
	JSR CLDCHN
	LDX #1
	STX TRACK
	JSR FORMAT      ; FORMAT DST DRIVE
;
; COPY BLOCKS FROM ONE DRIVE TO OTHER
;
CPYD1
	LDA TRACK
	JSR MAXSEC
	STA SECTOR
	DEC SECTOR
	JSR CPYTRK      ; COPY ONE TRACK
	INC TRACK
	LDA TRACK
	CMP MAXTRK
	BNE CPYD1
	JSR INITDR
	JMP ENDCMD
;
; COPY ONE TRACK
;
CPYTRK
	JSR SETRH
	JSR READS       ; READ 10 SECTORS
	JSR WRITES      ; WRITE 10 SECTORS
	LDA SECTOR
	BPL CPYTRK
	RTS
SETRH
	LDA DRVNUM
	EOR #1
	STA CMD
SETR5
	LDA #10
	STA TEMP+2
SETR3
	LDA TEMP+2
	JSR SETH
	DEC SECTOR
	BMI SETR6
	DEC TEMP+2
	BPL SETR3
	INC TEMP+2
SETR6
	RTS
;
; READ TEMP+2 BLOCKS IN
;
READS
	LDA CMD
	ORA #READ
	STA CMD
	LDX TEMP+2
READS1
	LDA CMD
	JSR SETJOB
	CPX #10
	BEQ READS8
	INX
	BNE READS1
READS8
	LDX TEMP+2
READS3
	JSR WATJOB
READ10
	CPX #10
	BEQ READ15
	INX
	BNE READS3
READ15
	RTS
;
; WRITE TEMP+2 BUFFERS OUT
;
WRITES
	LDA #WRITE
	ORA DRVNUM
	STA CMD
	LDX TEMP+2
WRIT0
	JSR SETJOB
WRIT30
	CPX #10
	BEQ WRIT5
	INX
	BNE WRIT0
WRIT5
	LDX TEMP+2
WRIT10
	JSR WATJOB
WRIT15
	CPX #10
	BEQ WRIT20
	INX
	BNE WRIT10
WRIT20
	RTS
;
; TRANSFER FORMAT CODE TO BUFFER 0
;  & START CONTROLLER FORMATTING
;
FORMAT
	LDY #0
;
; TRANSFER FORMAT CODE
;
FORMA1
	LDA FMTCOD,Y
	STA VBAMS,Y
	LDA FMTCOD+$100,Y
	STA VBAMS+$100,Y
	LDA FMTCOD+$200,Y
	STA VBAMS+$200,Y
	LDA FMTCOD+$300,Y
	STA VBAMS+$300,Y
	INY
	BNE FORMA1
	STY FMTDAT      ; FORMAT DATA, .Y = 0
	INY
	STY FMTTRK      ; STARTING FORMAT TRACK
	LDY TLGAP
	STY MAXGAP      ; MAX TAIL GAP
	LDA #0
	JSR SETH
	LDA DRVNUM
	ORA #EXEC
	STA JOBS
FMT105
	LDA JOBS
	BMI FMT105
	CMP #1
	BEQ FMT110
	LDX #0          ; JOB # 0
	JMP ERROR
FMT110
	RTS
