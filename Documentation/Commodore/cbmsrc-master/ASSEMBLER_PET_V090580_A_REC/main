.PAGE 'MAIN'

PASS1	LDX #STKVAL
	TXS
	CLD
	LDA #0
	STA XREF        ;ASSUME NO CROSS REF
	STX NOBJ
	STA LBOTH       ;LIST ONLY TO PRINTER
	STA CHAN
	STA PASS        ;SET PASS 1
	STA SFILE
	STA NOSYM
	STA NOSYM+1
	STA IFLAGS+1

	LDY #>SYTBST    ;START OF SYMTBL
	LDX #<SYTBST
	STX STSAVE
	STY STSAVE+1
	LDY #0          ;CLEAR FIRST SYM
	TYA
	STA (STSAVE),Y

	LDY SYTBND+1
	LDX SYTBND
	STX ISYEND
	STY ISYEND+1

	JSR RESET

	LDA #1
	STA LPGCT
	LDA #0
	STA LPGCT+1

	LDA #5          ;OPEN KEYBOARD FILE #5
	STA LA
	LDA #0
	STA SA
	STA FA
	STA FNLEN
	JSR OPEN

	LDA #0
	STA FNLEN
	STA SA
	LDA #1          ;OPEN DISPLAY FILE #1
	STA LA
	LDA #3
	STA FA
	JSR OPEN

XXXA2	LDA #$93
	JSR BSOUT
	LDA #>STTMSG
	LDY #<STTMSG
	JSR WSCRN
	JSR CLRCH
	LDX #5
	STX CHAN
	JSR CHKIN
	LDX #40
	STX SAVEX
XXXA3	DEC SAVEX
	BEQ XXXA2
	JSR BASIN
	CMP #' '
	BEQ XXXA3
	CMP #$0D
	BEQ XXXA4
	LDX #0
	STX NOBJ
	STX OBJLEN
	BEQ XXXA6
XXXA8	JSR BASIN
	CMP #' '
	BEQ XXXA7
XXXA6	CMP #$0D
	BEQ XXXA7
	LDX OBJLEN
	CPX #14
	BEQ XXXA2
	STA OBJFIL,X
	INX
	STX OBJLEN
	BNE XXXA8
XXXA7	LDA #','
	STA OBJFIL,X
	INX
	LDA #'S'
	STA OBJFIL,X
	INX
	LDA #','
	STA OBJFIL,X
	INX
	LDA #'W'
	STA OBJFIL,X
	INX
	STX OBJLEN
XXXA4	LDA #>HCMSG
	LDY #<HCMSG
	JSR WSCRN
	JSR BASIN
	LDX #0
	STX NOPRIN
	CMP #'N'
	BEQ RDOPNX

	LDA #>PRTMST
	LDY #<PRTMST
	JSR WSCRN
	JSR BASIN
	CMP #'N'
	BNE IEEEP

; CENTRO PRINTER OPEN SUB.
CENTRO	LDA #$40+$80
	STA NOPRIN
	LDX #$FF
	STX $E843   ;PORT DDR
	LDA #$EC
	STA $E84C
	BIT $E841
	JMP RDOPNX

;
; IEEE PRINTER OPEN SUB.

IEEEP	LDA #$04
	STA LA
	STA FA
	LDA #0
	STA SA
	STA FNLEN
	JSR OPEN        ;OPEN PRINTER FILE
	LDA #$80
	STA NOPRIN

RDOPNX	LDA #>CRRMSG
	LDY #<CRRMSG
	JSR WSCRN
	JSR BASIN
	CMP #'Y'
	BNE XXXB2
	STA XREF
XXXB2	LDA #>SRCMSG
	LDY #<SRCMSG
	JSR WSCRN
	LDX #40
	STX SAVEX
XXXB4	DEC SAVEX
	BEQ XXXA4
	JSR BASIN
	CMP #' '
	BEQ XXXB4
	CMP #$0D
	BNE XXXB5
	JMP BREADY
XXXB5	PHA
	JSR OPNSB1
	PLA
	LDX #0
	STX FNLEN
	BEQ XXXB6
XXXB8	JSR BASIN
	CMP #' '
	BEQ XXXB7
XXXB6	CMP #$0D
	BEQ XXXB7
	LDX FNLEN
	CPX #14
	BEQ RDOPNX
	STA ISRC,X
	INX
	STX FNLEN
	STX ISRCLN
	BNE XXXB8
XXXB7	LDA #','        ;PUT A COMMA AFTER THE NAME
	STA ISRC,X
	INX
	LDA #'S'        ;FOR THE AUTO OBJECT NAME FEATURE
	STA ISRC,X
	INX
	STX FNLEN
	STX ISRCLN

; ****************
; * PASS 1 START
; ****************

	LDA #>CRMSG
	LDY #<CRMSG
	JSR WSCRN

	JSR SOPEN       ;OPEN SOURCE FILE

	LDA #>P1MSG     ;'PASS 1'
	LDY #<P1MSG
	JSR WSCRN

	JSR CLRTTL      ;CLR TITLE BUFF
	JSR PTTTL0      ;FORCE PAGE HEADING
	JMP SNEWLN      ;BEGIN ASSEMBLY

; *******************************
; * OPEN DISK COMMAND CHANNEL
; *******************************

OPNSB1	LDA #7
	STA LA
	LDA #2
	STA FNLEN
	LDA #8
	STA FA
	LDA #15
	STA SA
	LDA #'I'
	STA ISYM
	LDA #'0'
	STA ISYM+1
	LDA #>ISYM
	STA FNADR+1
	LDA #<ISYM
	STA FNADR
	JSR OPEN
	LDA #7
	JSR CLOSE
	LDA #'1'
	STA ISYM+1
	JMP OPEN        ;OPEN COMMAND CHANNEL & SEND I

; ********************
; * OPEN SOURCE FILE
; ********************

SOPEN	LDA #8          ;OPEN SOURCE FILE
	STA FA
	LDA #2
	STA LA
	STA SA
	LDA #>ISRC
	STA FNADR+1
	LDA #<ISRC
	STA FNADR
	LDA ISRCLN
	STA FNLEN
	JSR OPEN        ;OPEN SOURCE FILE
	JSR FTEST
	LDA #$80
	STA SFILE
	RTS

; ********************
; *      PASS2
; ********************

PASS2	LDA XREF        ;DOING A CROSS REF ?
	BNE PXREF       ;YES
	JMP PAS222      ;NO

PXREF	LDA #8          ;OPEN FILE TO SAVE SYMS
	STA LA
	STA FA
	STA SA
	LDA #>XNLAB     ;'0:XRLABEL,S,W'
	STA FNADR+1
	LDA #<XNLAB
	STA FNADR
	LDA #<XN11
	STA FNLEN
	JSR OPEN        ;OPEN CROSS REFERENCE SYMBOL FILE
	JSR FTEST
	JSR CLRCH       ;CLEAR OTHER CHANNELS
	LDX #8          ;CHECK OUT
	STX CHAN
	JSR CKOUT
	LDA STSAVE      ;SET PTR TO BEGINNING
	LDY STSAVE+1
	STA BOTS
	STY BOTS+1
	LDA NOSYM+1     ;# OF SYMS
	LDY NOSYM
	STA DELS
	STY DELS+1
XSYMS	LDY #0          ;SAVE A SYM
XSY100	LDA (BOTS),Y
	JSR BSOUT
	INY
	CPY #8
	BNE XSY100
	LDA DELS        ;DONE ?
	BNE XSY120      ;NO
	DEC DELS+1
XSY120	DEC DELS
	LDA DELS
	ORA DELS+1      ;DONE ?
	BEQ XSY200      ;YES
	TYA             ;BUMP TO NEXT SYM
	CLC
	ADC BOTS
	STA BOTS
	BCC XSYMS       ;NO CARRY
	INC BOTS+1
	BNE XSYMS       ;ALWAYS

XSY200	LDA #8          ;CLOSE THE SYM FIL.
	JSR CLOSE
	JSR CLRCH
	LDA #8
	STA FA
	LDA #9          ;OPEN THE REFS FILE
	STA SA
	STA LA
	LDA #>XNREF     ;'0:XREF1234,S,W'
	STA FNADR+1
	LDA #<XNREF
	STA FNADR
	LDA #<XN21
	STA FNLEN
	JSR OPEN        ;OPEN CROSS REF FILE
	JSR FTEST



PAS222	JSR RESET
	JSR OBJINT
	LDA #>P2MSG
	LDY #<P2MSG
	JSR WSCRN       ;OPEN OBJECT FILE
	LDA NOBJ
	BNE PASS2B

	LDA OBJLEN
	STA FNLEN
	LDA #>OBJFIL
	STA FNADR+1
	LDA #<OBJFIL
	STA FNADR
	LDA #6
	STA LA
	STA SA
	LDA #8
	STA FA
	JSR OPEN        ;OPEN OBJECT FILE
	JSR FTEST

PASS2B	JSR CLRTTL
	JSR SOPEN
	JMP SNEWLN

RESET	LDA #0
	STA IPC
	STA IPC+1
	STA ICRDNO
	STA ICRDNO+1
	STA LTLLEN
	STA LERCT
	STA JOPTYP

	LDA #$0D
	STA LCHAR       ;MAKES CLEAN START

	LDA #%11011100; NOG,NOS,NOE,INT,LIS
	STA IFLAGS

	RTS




WSCRN	PHA             ;WRITE TO PET SCREEN
	LDA #1
	CMP CHAN
	BEQ WSCRN1
	TYA
	PHA
	JSR CLRCH
	LDX #1
	STX CHAN
	JSR CKOUT
	PLA
	TAY
WSCRN1	PLA
	STA TBLPTR+1
	STY TBLPTR
	LDY #0
WSCRN2	LDA (TBLPTR),Y
	INY
	PHA
	AND #$7F
	JSR BSOUT
	PLA
	BPL WSCRN2
	RTS

CLRTTL	LDY #19
	LDA #$20
PASS2H	STA LTLBUF,Y
	DEY
	BPL PASS2H

	LDX ISRCLN      ;DON'T OUTPUT THE DRIVE #
	DEX
	DEX
	STX LTLLEN

PASS2D	LDA CHAN,X
	STA LTLBUF-1,X
	DEX
	BNE PASS2D
	RTS

; ************************
; *
; * -- SCAN NEW LINES --
; *  MAIN LOGIC OF
; *    PASS 1 & PASS 2
; *
; ************************

SNEWLN	JSR CHKBRK
	LDA #0          ;INIT NEXT CARD
	STA LCDPT
	STA IFLAGS+1
	STA ICSB
	STA ICSE
	STA IERR
	STA IMAXCL
	STA ICSL
	STA IEXP
	STA IEXP+1
	STA ILSST
	STA JLABL
	STA JORG
	STA JBYWOR
	STA ICOLP
	LDA LCHAR
	BEQ H8921
	LDX #0
	INC ICRDNO+1
	BNE CARD1
	INC ICRDNO
CARD1	JSR GETCHR      ;INPUT A CHAR
	CMP #$0D
	BEQ CARD3       ;BRANCH IF A C.R.
	CMP #$00        ;IS IT NULL
	BNE CARD2
H8921	JMP H10

CARD2	STA ICRD,X
	INX
	JMP CARD1

CARD3	DEX
	STX IMAXCL

;
;  MAIN LINE BLOCK
;

H87	JSR NFNDNB      ;BLANK CARD?
	BCC H8830B      ;YES
	LDX ICOLP
	LDA ICRD,X
	CMP #'>'        ;IF TERMINATOR CARD
	BNE H8830A
	JMP ENDLN
H8830A	CMP #';'
	BNE H8831       ;NOT A COMMENT CARD
H8830B	JMP H990        ;A COMMENT CARD
H8831	JSR NFNDEN      ;FIND STRING END
	BCS H1          ;END FOUND
	LDA #3          ;ERROR--END NOT FOUND
	LDX ICSB
	TAY
	JMP LTS1

H1	LDX ICSB
	LDA ICRD,X
	CMP #'.'
	BNE H8841       ;NOT ASSEM DIRECT
	JMP H5          ;AN ASSEM DIRECT

H8841	CMP #'*'
	BNE H8832       ;NOT AN ORG
	JMP H102        ;REDEFINE ORG

H8832	LDY ICSL
	CPY #6          ;6 CHARACTERS LONG
	BEQ H76         ;<=
	BCC H76
	LDA #9
	LDY #3
	JMP LTS1

H76	STY KLEN        ;LENGTH OF SYMBOL
	JSR CONSYM      ;CONSTRUCT THE SYMBOL
	BCS H3          ;NO ERRORS IN CONSYM
	LDA #$10
	LDY #3
	JMP LTS1

H3	LDA ICSL        ;LENGTH OF STRING
	CMP #3          ;RIGHT LENGTH FOR LABEL
	BNE H92         ;LABEL PROCESS-OVER 3
	JSR NOPFND      ;FIND A MNEMONIC
	BCC H92         ;FAILED-MUST BE A LABEL
	JMP H201        ;AN OP CODE


; ************************
; *   LABEL PROCESSING
; ************************

H92	LDA JLABL
	BEQ H94
	LDA #$03
	TAY
	LDX ICSB
	JMP LTS1

H94	LDA #1
	STA JLABL
	LDX ICOLP
	JSR NALPH
	BCS H91         ;NO
	LDA #$08
	LDY #$03
	JMP LTS1

H91	LDA ISYM+1
	CMP #' '
	BNE H91X
	LDA ISYM
	CMP #'A'
	BEQ L0820
	CMP #'X'
	BEQ L0820
	CMP #'Y'
	BEQ L0820
	CMP #'S'
	BEQ L0820
	CMP #'P'
	BNE H91X
L0820	LDA #$20
	LDY #$03
	JMP LTS1

H91X	STX ILSST       ;SAVE PARMS FOR EQU
	LDX #0          ;SAVE THE SYMBOL
H8845	LDA ISYM,X
	PHA
	INX
	CPX #6
	BNE H8845

	LDA ICSL        ;SAVE ICSL
	PHA
	LDA ICSE
	STA ICOLP       ;SAVE ICSE
	INC ICOLP
	JSR NFNDNB      ;CHECK FOR EQUATE
	BCC H120        ;ONLY BLANKS FOUND
	LDA ICRD,X
	CMP #'='
	BEQ H121

H120	JSR NFIND       ;SEE IF GOOD LABEL
	BCC H95B        ;NOT PRESENT
	LDA KNVAL       ;IS THE NEW VALUE THE SAME AS THE OLD VALUE ?
	CMP IPC+1
	BNE MR04        ;NO
	LDA KNVAL+1
	CMP IPC
	BEQ H95A        ;OK

MR04	LDA #2          ;PREVIOUSLY DEFINED
	LDY #3
	LDX ILSST
	JMP LTS1

H95B	LDA IPC+1       ;PUT IN SYM TAB
	STA KNVAL
	LDA IPC
	STA KNVAL+1
	JSR NSERT
H95A	LDA IMAXCL      ;TRY FOR OPCODE
	CMP ICOLP       ;OFF CARD CHECK
	BCS H8842
	JMP H990        ;YES--OFF CARD
H8842	JMP H87         ;BACK TO MAIN SECT

.END
