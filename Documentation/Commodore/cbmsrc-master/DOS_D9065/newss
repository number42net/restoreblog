.PAGE 'NEWSS'
;
;*********************************
;* NEWSS: GENERATE NEW SS & FIX  *
;*        OLD SS'S TO REFLECT IT.*
;*                               *
;*********************************
;
NEWSS
	JSR NXTTS       ; GET T&S BASED ON HDR
	JSR DBLBUF      ; USE INACTIVE BUFFER
	JSR SCRUB
	JSR GETACT
	PHA
	JSR CLRBUF
	LDX LINDX
	LDA SS,X        ; SET REGS FOR TRANSFER
	TAY
	PLA
	TAX
	LDA R0
	CMP #NSSL-1
	BCS NEWSS4
	BCC NEWSS1
NEWSS4
	LDA #255
	STA R0
	LDA #0
	JSR SETPNT
	LDY #2
	STA (DIRBUF),Y  ; SIDE SECTOR 0
	LDX LINDX
	LDA RS,X
	INY
	STA (DIRBUF),Y  ; RECORD SIZE
	INY
	BEQ NEWSS1
	BNE NEWSS2
NEWSS1
	LDA #SSIOFF     ; # OF CHARS
	JSR B0TOB0      ; TRANSFER AT BUF(0)
	LDA #0
	JSR SSDIR
	LDY #2
	LDA (DIRBUF),Y  ; GET SS #
	PHA
	LDA #0
	JSR SETPNT
	PLA
	CLC
	ADC #1
	STA (DIRBUF),Y  ; PUT SS # IN NEW SS
	ASL A
	ADC #4
	STA R3          ; SAVE POSITION
	TAY
	SEC
	SBC #2
	STA R4
NEWSS2
	LDA TRACK
	STA R1          ; SAVE FOR SS UPDATE
	STA (DIRBUF),Y  ; PUT TRACK IN SS
	INY
	LDA SECTOR
	STA R2          ; SAVE FOR SS UPDATE
	STA (DIRBUF),Y  ; PUT SECTOR IN SS
	LDY #0
	TYA
	STA (DIRBUF),Y  ; NULL LINK
	INY
	LDA #SSIOFF+1   ; PTR TO LAST BYTE
	STA (DIRBUF),Y
	LDA #SSIOFF
	JSR SETPNT
	JSR WRTAB
	JSR WATJOB
NS20
	LDX LINDX
	LDA SS,X        ; GET SS BUFFER #
	PHA
	JSR GAFLGS
	LDX LINDX
	STA SS,X        ; SWAP ACT-BUF & SS
	PLA
	LDX LBUSED
	STA BUF0,X
	LDA #0
	JSR SETPNT      ; SET LINK TO NEW SS
	LDY #0
	LDA TRACK
	STA (DIRBUF),Y
	INY
	LDA SECTOR
	STA (DIRBUF),Y
	LDA R0
	CMP #255
	BNE NS50
	LDA SECTOR
	PHA
	LDA TRACK
	PHA
	JSR WRTOUT      ; WRITE OLD SS
	JSR WATJOB
	JSR DBLBUF
	JSR RDSSSA      ; READ SUPER SIDE SECTOR
	INC R5          ; NEXT GROUP #
	LDA R5
	ASL A           ; * 2
	CLC
	ADC #3          ; INDEX INTO SSS
	JSR SETSSP      ; POINT TO IT
	PLA
	JSR PUTSS       ; SET THE TRACK
	PLA
	JSR PUTSS       ; SET THE SECTOR
	JSR WRTSSS      ; WRITE THE SSS
	LDA R5
	PHA
	JMP RDSS1D      ; READ THE NEW SS BACK IN
NS40
	JSR GETACT
	LDX LINDX
	JSR IBRD        ; READ NEXT SS
	LDA #0
	JSR SETPNT      ; PTR = 0
NS50
	DEC R4
	DEC R4
	LDY R3          ; GET NEW SS LINK PTR
	LDA R1
	STA (DIRBUF),Y  ; PUT TRACK IN
	INY
	LDA R2
	STA (DIRBUF),Y  ; PUT SECTOR IN
	JSR WRTOUT      ; WRITE IT BACK...
	JSR WATJOB      ; ...& WAIT
	LDY R4
	CPY #3
	BCS NS40        ; MORE SS TO UPDATE!
	JMP DBLBUF      ; RESET ACTIVE BUFFER
.END
