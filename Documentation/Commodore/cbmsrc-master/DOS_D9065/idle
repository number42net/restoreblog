.PAGE 'IDLE'
;
; IDLE LOOP, WAITING FOR SOMETHING TO DO
;
VIDLE
	LDA CMDWAT      ; TEST FOR PENDING COMMAND
	BEQ IDL1        ; NO COMMAND WAITING
	SEI
	LDA #0
	STA CMDWAT
	STA NMIFLG      ; CLEAR DEBOUNCE
	JSR PARSXQ      ; PARSE AND EXECUTE COMMAND
IDL1
	CLI
	LDA #14
	STA TEMP+3
	LDA #0          ; IF FILE OPEN, TURN ON ACT LED
	STA TEMP
	STA TEMP+1
IDL2
	LDX TEMP+3      ; LOOK THRU LINTAB
	LDA LINTAB,X    ; FOR ACTIVE FILE
	CMP #$FF
	BEQ IDL3
	AND #$3F
	STA LINDX
	JSR GETACT
	TAX
	LDA LSTJOB,X    ; DETERMINE WHICH DRIVE IT'S ON
	AND #1
	TAX
	INC TEMP,X
IDL3
	DEC TEMP+3      ; SET FLAG INDICATING DRV
	BPL IDL2        ; HAS FILE OPEN
	LDY #BFCNT-1    ; LOOK THRU JOB QUE FOR
IDL4
	LDA JOBS,Y      ; FOR JOBS STILL RUNNING
	BPL IDL5
	AND #1
	TAX
	INC TEMP,X      ; SET FLAG INDICATING DRIVE
IDL5
	DEY             ; IS ACTIVE
	BPL IDL4
	SEI
	LDA DRVNUM
	PHA             ; SAVE DRIVE
	LDA PBD2
	AND #255-LED1-LED0
	PHA
	LDA #0
	STA DRVNUM
	LDA TEMP
	BEQ IDL7
	LDA WPSW
	BEQ IDL6
	JSR CLDCHN
IDL6
	PLA             ; TURN ON LED IF DRIVE FLAG
	ORA #LED0       ; IF NOT 0
	PHA
IDL7
	INC DRVNUM
	LDA TEMP+1
	BEQ IDL9
	LDA WPSW+1
	BEQ IDL8
	JSR CLDCHN
IDL8
	PLA
	ORA #LED1
	PHA
IDL9
	PLA
	STA PBD2
	PLA
	STA DRVNUM      ; RESTORE DRIVE
	JMP IDL1        ; BACK TO TOP OF LOOP
.END
