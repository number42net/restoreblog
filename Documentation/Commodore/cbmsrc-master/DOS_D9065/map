.PAGE 'NEWMAP'
;
; BUILD A NEW MAP ON DISKETTE
;
NEWMAP
	JSR SETBMP
	JSR CLRBAM
	JSR SETBJ
	JSR SETH
	LDX DRVNUM
	LDA #1
	STA TMPBAM
	STA BAMTRK,X
	LDA #0
	STA BAMSEC,X
	JSR JOB2X       ; .X = JOBNUM*8
	LDA #$FF
	STA HDRS+2,X
	STA HDRS+3,X    ; SET FIRST BACKWARD LINK
	LDY #LOTRK      ; LOW TRACK OFFSET
	LDA #0
	STA TRACK       ; START AT TRACK 0
	STA (BMPNT),Y   ; LOW TRACK IN BAM
	STA HEAD        ; START AT HEAD 0
NM40
	JSR BAMIDX      ; GET THE BAM INDEX
	BCS NM45
	TAY
	CLC
	ADC BAMSIZ
	BEQ NM50
	BCC NM50
NM45
	JSR BAMOUT      ; WRITE THE BAM OUT
	JSR CLRBAM      ; CLEAR THE BAM AREA
	LDY #LOTRK
	LDA TRACK
	STA (BMPNT),Y   ; SET LOW TRACK OF NEXT BAM
	JSR BAMIDX
NM50
	STY R1
	LDA #0
	STA SECTOR      ; START AT SECTOR 0
NM60
	LDA R1          ; R1 = BAM INDEX
	JSR FREUS2      ; GET THE SECTOR INDEX
	JSR FRETS2      ; MARK THE BAM
	JSR GETHED      ; EXTRACT THE HEAD NUMBER
	INC SECTOR
	LDX SECTOR      ; SECTOR = SECTOR+1
	CPX SECTRS
	BCC NM60        ; MORE SECTORS FOR THIS HEAD
	INC HEAD
	LDX HEAD        ; HEAD = HEAD+1
	CPX HEADS
	BCC NM40        ; MORE HEADS FOR THIS TRACK
	INC TRACK
	LDX TRACK       ; TRACK = TRACK+1
	CPX MAXTRK
	BCS NM70        ; NO MORE TRACKS TO DO
	LDX #0
	STX HEAD        ; NEW TRACK, HEAD = 0
	BEQ NM40        ; DO THE NEXT TRACK
NM70
	JSR BAMOUT
	LDX DRVNUM
	LDA #0
	STA MDIRTY,X
	JMP RDBAM       ; READ FIRST BAM (RTS)
;
; BAMOUT - SET LINKS AND WRITE IT
;
BAMOUT
	LDA #0
	STA WRTFLG      ; BAM WRITE ERROR FLAG
	LDX TRKBAM
	DEX
	STX R0          ; MAX BAM TRACKS-1
	JSR JOB2X       ; .X = JOBNUM*8
	LDY #2
	LDA HDRS+2,X
	STA (BMPNT),Y   ; SET BACKWARD LINK (TRACK)
	INY
	LDA HDRS+3,X
	STA (BMPNT),Y   ; SET BACKWARD LINK (SECTOR)
	LDA #0
	STA HDRS+3,X    ; START WRITE AT SECTOR, HEAD 0
	LDY TRACK
	CPY MAXTRK
	BCC BAMOU0      ; NOT THE LAST BAM
	LDA #$FF
BAMOU0
	LDY #1
	STA (BMPNT),Y   ; SET NEXT SECTOR LINK
	LDA TMPBAM
	STA HDRS+2,X    ; START WRITE AT TRACK(TMPBAM)
	LDY TRACK
	CPY MAXTRK
	BCC BAMOU1      ; NOT THE LAST BAM
	LDA #$FF
	BNE BAMOU2      ; JUMP
BAMOU1
	CLC
	ADC TRKBAM
	CMP MAXTRK
	BCC BAMO1
	SBC #1          ; TRKBAM = TRKBAM-1 (.C = 1)
BAMO1
	STA TMPBAM      ; NEXT POSSIBLE BAM TRACK
BAMOU2
	LDY #0
	STA (BMPNT),Y   ; SET NEXT TRACK LINK
	LDY #HITRK
	LDA TRACK
	STA (BMPNT),Y   ; SET HIGH TRACK OF THIS BAM
BAMOU3
	LDA #$FF
	STA JOBRTN      ; RETURN THE ERROR CODE
	JSR DOWRIT      ; TRY THE WRITE
	CMP #2          ; ANY ERRORS?
	BCC BAMOU4      ; NO, IT WORKED
	LDA #$FF
	STA WRTFLG      ; DIDN'T WORK, FLAG IT
	JSR JOB2X       ; .X = JOBNUM*8
	INC HDRS+3,X    ; NEXT SECTOR, HEAD
	LDA HDRS+3,X
	CMP MAXCYL
	BCC BAMOU3      ; STILL MORE SECTORS, HEADS
	INC HDRS+2,X    ; NEXT TRACK
	LDA #0
	STA HDRS+3,X    ; START AT SECTOR, HEAD 0
	DEC R0
	BNE BAMOU3      ; TRY THE NEXT TRACK
	LDA #12
	JMP ERROR       ; FORMAT ERROR
BAMOU4
	LDA WRTFLG
	BNE BAMOU5      ; CHANGE THE LINK OF PREVIOUS BAM
	RTS
BAMOU5
	JSR JOB2X       ; .X = JOBNUM*8
	LDY #LOTRK
	LDA (BMPNT),Y
	BNE BAMOU6      ; HAS A BACKWARD LINK
	LDY DRVNUM
	LDA HDRS+2,X
	STA BAMTRK,Y
	LDA HDRS+3,X
	STA BAMSEC,Y
	RTS
BAMOU6
	LDA HDRS+2,X
	PHA             ; SAVE THE TRACK
	LDA HDRS+3,X
	PHA             ; SAVE THE SECTOR
	JSR PRVBA1      ; READ IN THE PREVIOUS BAM
	LDY #1
	PLA             ; GET THE SECTOR
	STA (BMPNT),Y   ; CHANGE THE SECTOR LINK
	DEY
	PLA             ; GET THE TRACK
	STA (BMPNT),Y   ; CHANGE THE TRACK LINK
	PHA             ; SAVE THE TRACK AGAIN
	JSR DOWRIT      ; WRITE IT OUT
	JSR JOB2X       ; .X = JOBNUM*8
	PLA             ; GET THE TRACK
	STA HDRS+2,X
	LDY #1
	LDA (BMPNT),Y
	STA HDRS+3,X
	RTS
.PAGE 'MAPOUT'
;
; WRITE OUT THE BIT MAP TO
; THE DRIVE IN LSTJOB(ACTIVE)
;
MAPOUT
	JSR GETACT
	TAX
	LDA LSTJOB,X    ; GET DRIVE FROM LAST JOB
MO10
	AND #1
	STA DRVNUM
	JSR SETBJ       ; SET THE BAM JOB
;
; VERIFY THE BAM BLOCK COUNT
; MATCHES THE BITS
;
JSCRBM
	JSR SETBMP
	JSR SETBJ
	LDY DRVNUM
	LDA MDIRTY,Y
	BNE SB10        ; BAM HAS BEEN CHANGED
	RTS
SB10
	LDA HEAD
	PHA
	LDA #0
	STA MDIRTY,Y
	STA HEAD
	LDY #LOTRK
	LDA (BMPNT),Y
MC10
	PHA             ; SAVE THE TRACK NUMBER
	JSR MBAM2       ; GET THE BAM INDEX
	JSR AVCK        ; CHECK THE BITS
	INC HEAD
	LDA HEAD        ; HEAD = HEAD+1
	CMP HEADS
	BCS MC12        ; ALL OF THE HEADS
	PLA             ; GET THE CURRENT TRACK
	JMP MC10        ; NEXT HEAD, SAME TRACK
MC12
	PLA             ; GET THE TRACK NUMBER
	CLC
	ADC #1          ; TRACK = TRACK+1
	LDY #HITRK
	CMP (BMPNT),Y
	BCS MC13        ; NO MORE TO THIS BAM
	LDX #0
	STX HEAD        ; START OVER WITH HEAD 0
	BEQ MC10        ; JUMP
MC13
	JSR DOWRIT      ; WRITE THE BAM
	PLA
	STA HEAD
	RTS
.PAGE 'NUMFRE'
;
; CALC THE NUMBER OF FREE BLOCKS ON DRVNUM
;
NUMFRE
	LDX DRVNUM
	LDA NDBL,X
	STA NBTEMP
	LDA NDBH,X
	STA NBTEMP+1
	RTS
.END
