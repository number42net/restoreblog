.PAGE 'NEW'
;
; NEW: INITIALIZE A DISK, DISK IS
; SOFT-SECTORED, BIT AVAIL. MAP,
; DIRECTORY, & 1ST BLOCK ARE ALL INITED
;
NEW
	JSR ONEDRV
	LDA FILDRV      ; SET UP DRIVE #
	BPL N101
	LDA #BADFN
	JMP CMDERR
N101
	AND #1
	STA DRVNUM
	JSR SETLDS
	JSR SETBMP
	LDA DRVNUM
	ASL A
	TAX
	LDY FILTBL+1    ; GET DISK ID
	CPY CMDSIZ      ; IS THIS NEW OR CLEAR?
	BEQ N108        ; END OF CMD STRING
	LDA CMDBUF,Y
	STA DSKID,X     ; STORE IN PROPER DRIVE
	LDA CMDBUF+1,Y  ; Y = 0
	STA DSKID+1,X
	JSR CLRCHN
	JSR FORMAT      ; FORMAT THE DRIVE
	JMP N110
N108
	JSR INITDR
N110
	JSR NEWMAP      ; CREATE A NEW BAM
	LDY #0          ; USE BUFFER NUMBER 0
	LDA BUFIND,Y    ; GET BUFFER INDEX
	STA IP+1
	LDA #0
	STA IP          ; SET IP TO BUFFER
	JSR MRKBAM      ; MARK THE BAMS USED
	JSR MRKBTS      ; MARK THE BTS AND CONTENTS USED
	LDA #0
	STA TRACK
	STA SECTOR
	JSR USEDTS      ; MARK TRACK, SECTOR AND HEAD 0
	JSR MAKDIR      ; CREATE A DIRECTORY
	JSR SET000      ; SET UP TRACK, SECTOR AND HEAD 0
	JSR SCRBAM
	JSR NF05        ; COUNT BLOCKS FREE
	JMP ENDCMD      ; DONE!!!
;
; TRANSFER THE DIRECTORY HEADER
;
TRNHDR
	LDA JOBNUM
	TAY
	ASL A
	TAX
	LDA #6
	STA BUFTAB,X
	LDX FILTBL
	LDA #27
	JSR TRNAME      ; TRANSFER CMD BUF TO BUF0
	LDY #$12
	LDA DRVNUM      ; SET UP CURRENT I.D.
	ASL A
	TAX
	LDA DSKID,X
	STA (DIRBUF),Y
	INY
	LDA DSKID+1,X
	STA (DIRBUF),Y
	INY
	INY
	LDA #DOSVER+$30
	STA (DIRBUF),Y
	INY
	LDA VERNUM      ; SHOW VER #
	STA (DIRBUF),Y
	RTS
;
; MARK THE BTS SECTORS USED ALONG WITH
; THE BTS CONTENTS
;
MRKBTS
	LDX DRVNUM
	LDA BTSTRK,X    ; START OF BTS (TRACK)
	STA TRACK
	LDA BTSSEC,X    ; START OF BTS (SECTOR)
	STA SECTOR
	JSR NXTBTS      ; MARK THIS SECTOR, READ NEXT ONE
MRKBT1
	LDA (IP),Y
	CMP #$FF        ; NEW TRACK OR END MARKER
	BEQ MRKBT3
	STA SECTOR
	TYA
	PHA
	JSR USEDTS      ; MARK THE SECTOR USED
	PLA
	TAY
MRKBT2
	INY
	BNE MRKBT1
	JSR BTSLNK      ; READ NEXT LINK
	JMP MRKBT1
MRKBT3
	INY
	BNE MRKBT4
	JSR BTSLNK      ; READ NEXT LINK
MRKBT4
	LDA (IP),Y
	CMP #$FF        ; ALREADY HAVE ONE $FF
	BEQ MRKBT5      ; NOW HAVE TWO, DONE
	STA TRACK
	JMP MRKBT2
MRKBT5
	RTS
;
; MARK AND READ THE NEXT BTS
;
NXTBTS
	JSR USEDTS      ; MARK THIS LINK USED
	LDA #0
	STA JOBNUM      ; USE JOB NUMBER 0
	JSR SETH
	JSR DOREAD      ; READ THE NEXT BTS
	LDY #2
	RTS
;
; GET, MARK AND READ THE NEXT BTS
;
BTSLNK
	LDA TRACK
	PHA             ; CURRENT TRACK
	LDA SECTOR
	PHA             ; CURRENT SECTOR
	LDA (IP),Y      ; .Y = 0
	CMP #$FF        ; AT THE END?
	BEQ BTSLN1      ; YES
	STA TRACK
	INY
	LDA (IP),Y
	STA SECTOR
	JSR NXTBTS      ; MARK AND READ NEXT BTS
	PLA
	STA SECTOR      ; RESTORE CURRENT SECTOR
	PLA
	STA TRACK       ; RESTORE CURRENT TRACK
	RTS
BTSLN1
	PLA             ; ON ENTRY SECTOR
	PLA             ; ON ENTRY TRACK
	PLA             ; RETURN FOR MRKBTS
	PLA             ; RETURN FOR MRKBTS
	RTS             ; RETURN TO ORIGINAL CALLER
;
; MARK THE BAMS USED
;
MRKBAM
	LDX DRVNUM
	LDA BAMTRK,X    ; START OF BAM (TRACK)
	STA TRACK
	LDA BAMSEC,X    ; START OF BAM (SECTOR)
MRKBA1
	STA SECTOR
	JSR USEDTS      ; MARK TRACK AND SECTOR
	LDA #0
	STA JOBNUM
	JSR SETH
	JSR DOREAD      ; READ THE BAM
	LDY #0
	LDA (IP),Y      ; TRACK LINK
	CMP #$FF        ; DONE?
	BEQ MRKBA2      ; YES
	STA TRACK
	INY
	LDA (IP),Y      ; SECTOR LINK
	JMP MRKBA1
MRKBA2
	RTS
;
; CREATE THE DIRECTORY
;
MAKDIR
	LDA CYLLOW
	LSR A           ; .A = CYLLOW/2
	STA TRACK
	LDA #0
	STA SECTOR
	JSR NXT0        ; GET A DIRECTORY TRACK AND SECTOR
	LDX DRVNUM
	LDA TRACK
	STA DSTRK,X     ; START OF DIRECTORY (TRACK)
	LDA SECTOR
	STA DSSEC,X     ; START OF DIRECTORY (SECTOR)
	LDA #0
	TAY
	JSR SETBUF      ; CLEAR THE DIRECTORY BUFFER
	STA (IP),Y      ; .A = 0, .Y = 0
	INY
	LDA #$FF
	STA (IP),Y
	LDA #0
	STA JOBNUM
	JSR SETH
	JSR DOWRIT      ; WRITE THE DIRECTORY
	LDX DRVNUM
	LDY #0
	LDA DSTRK,X
	STA (IP),Y
	INY
	LDA DSSEC,X
	STA (IP),Y
	JSR TRNHDR      ; TRANSFER THE DIRECTORY HEADER
	JSR NXT0        ; GET HEADER TRACK AND SECTOR
	LDX DRVNUM
	LDA TRACK
	STA HSTRK,X     ; START OF HEADER (TRACK)
	LDA SECTOR
	STA HSSEC,X     ; START OF HEADER (SECTOR)
	LDA #0
	STA JOBNUM
	JSR SETH
	JMP DOWRIT      ; WRITE THE HEADER (RTS)
;
; SET TRACK, SECTOR AND HEAD 0
;
SET000
	LDA #0
	TAY
	JSR SETBUF      ; CLEAR THE BUFFER
	LDX DRVNUM
	LDA BTSTRK,X    ; START OF BTS (TRACK)
	STA (IP),Y      ; .Y = 0
	INY
	LDA BTSSEC,X    ; START OF BTS (SECTOR)
	STA (IP),Y
	INY             ; .Y = 2
	LDA #0
	STA (IP),Y      ; MARK IDENTIFIER
	INY
	LDA #$FF
	STA (IP),Y
	INY             ; .Y = 4
	LDA DSTRK,X     ; START OF DIRECTORY (TRACK)
	STA (IP),Y
	INY
	LDA DSSEC,X     ; START OF DIRECTORY (SECTOR)
	STA (IP),Y
	INY             ; .Y = 6
	LDA HSTRK,X     ; START OF HEADER (TRACK)
	STA (IP),Y
	INY
	LDA HSSEC,X     ; START OF HEADER (SECTOR)
	STA (IP),Y
	INY             ; .Y = 8
	LDA BAMTRK,X    ; START OF BAM (TRACK)
	STA (IP),Y
	INY
	LDA BAMSEC,X    ; START OF BAM (SECTOR)
	STA (IP),Y
	TXA
	ASL A
	TAX             ; .X = DRVNUM*2
	INY             ; .Y = 10
	LDA DSKID,X
	STA (IP),Y
	INY
	LDA  DSKID+1,X
	STA (IP),Y
	LDA #0
	STA TRACK
	STA SECTOR
	STA JOBNUM
	JSR SETH
	JMP DOWRIT      ; WRITE 000 (RTS)
.END
