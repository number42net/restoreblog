.PAGE 'VER-DIR'
;
; VALIDATE FILES WITH BAM
; CREATE NEW BAM ACCORDING TO
; CONTENTS OF FILES ENTERED IN DIR
;
VERDIR
VALDAT
	JSR SIMPRS      ; EXTRACT DRIVE #
	JSR INITDR
	JSR COMPRS      ; COMPRESS THE DIRECTORY
	LDX DRVNUM
	LDA #0
	STA NDBL,X
	STA NDBH,X      ; CLEAR FOR NEW MAP
	JSR NEWMAP      ; SET NEW BAM
	LDA BUFIND
	STA IP+1
	LDA #0
	STA IP
	JSR MRKBAM      ; MARK THE BAM USED
	JSR MRKBTS      ; MARK THE BTS USED
	LDA #0
	STA TRACK
	STA SECTOR
	JSR USEDTS      ; MARK TRACK, SECTOR AND HEAD 0
	LDA #0
	STA DELIND
	JSR SRCHST      ; SEARCH FOR FIRST FILE
	BNE VD25        ; FOUND ONE
VD10
	LDX DRVNUM
	LDA HSTRK,X
	STA TRACK
	LDA HSSEC,X
	STA SECTOR
	JSR VMKBAM      ; MARK ALL DIRECTORY SECTORS USED
	JSR SCRBAM      ; WRITE LAST BAM
	JSR INITDR      ; INITIALIZE
	JMP ENDCMD      ; DONE
VD15
	INY
	LDA (DIRBUF),Y
	PHA             ; SAVE TRACK
	INY
	LDA (DIRBUF),Y
	PHA             ; SAVE SECTOR
	LDY #19         ; GET SS TRACK
	LDA (DIRBUF),Y  ; IS THIS RELATIVE?
	BEQ VD17        ; NO
	STA TRACK       ; YES, SAVE TRACK
	INY
	LDA (DIRBUF),Y  ; GET SS SECTOR
	STA SECTOR
	JSR VMKBAM      ; VALIDATE SS BY LINKS
VD17
	PLA
	STA SECTOR      ; NOW DO DATA BLOCKS
	PLA
	STA TRACK
	JSR VMKBAM      ; SET BIT USED IN BAM
VD20
	JSR SRRE        ; SEARCH FOR MORE
	BEQ VD10        ; NO MORE FILES
VD25
	LDY #0
	LDA (DIRBUF),Y
	BMI VD15
	JSR DELDIR      ; NOT CLOSED DELETE DIR
	JMP VD20
VMKBAM
	JSR TSCHK
	JSR USEDTS
	JSR OPNIRD
MRK2
	LDA #0
	JSR SETPNT
	JSR GETBYT
	STA TRACK
	JSR GETBYT
	STA SECTOR
	LDA TRACK
	BNE MRK1
	JMP FRECHN
MRK1
	JSR USEDTS
	JSR NXTBUF
	JMP MRK2
.END
