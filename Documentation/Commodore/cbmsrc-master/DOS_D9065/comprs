.PAGE 'COMPRESS DIRECTORY'
COMPRS
	LDA BUFIND
	STA DIRBUF+1    ; USE BUFFER 0
	LDY #1
	LDA #0
	JSR SETBUF      ; SET BUFFER 1 TO ZERO'S
	LDA #1
	STA IP
	LDY DRVNUM
	LDA DSTRK,Y     ; START OF DIRECTORY (TRACK)
	STA TRACK
	LDA DSSEC,Y     ; START OF DIRECTORY (SECTOR)
	STA SECTOR
	LDA #1          ; BUFFER 1 FOR NEW DIRECTORY
	JSR SETH        ; SET HEADER FOR NEW DIRECTORY
	JMP COMPR1
COMPR0
	LDA TRACK
	BEQ COMPR5      ; DONE
COMPR1
	LDA #0
	STA JOBNUM      ; DIRECTORY IN BUFFER 0
	JSR SETH
	JSR DOREAD      ; READ THE DIRECTORY
	LDA #0
	STA DIRBUF
	TAY
	LDA (DIRBUF),Y  ; NEXT SECTOR LINK (TRACK)
	STA TRACK
	BEQ COMPR2      ; NO LINK
	INY
	LDA (DIRBUF),Y  ; NEXT SECTOR LINK (SECTOR)
	STA SECTOR
	JSR FRETS       ; FREE THESE LINKS
COMPR2
	LDA #2
	STA DIRBUF
COMPR3
	LDY #0
	LDA (DIRBUF),Y  ; GET FILE STATUS
	BPL COMPR4      ; DELETE OR NOT CLOSED
	JSR ADDENT      ; ADD THIS ENTRY
COMPR4
	LDA #32
	CLC
	ADC DIRBUF
	STA DIRBUF      ; POINT TO NEXT ENTRY
	BCC COMPR3
	BCS COMPR0      ; READ THE NEXT SECTOR
COMPR5
	LDA #0
	STA IP
	TAY
	STA (IP),Y      ; TRACK LINK = 0, LAST SECTOR
	INY
	LDA #$FF
	STA (IP),Y      ; SECTOR LINK
	LDA #1
	STA JOBNUM
	JMP DOWRIT      ; WRITE LAST SECTOR (RTS)
.PAGE 'ADD ENTRY'
ADDENT
	LDY #0
	LDX #30         ; MOVE 30 CHARACTERS
ADDEN1
	INC IP
	LDA IP
	CMP #1
	BEQ ADDEN2      ; WRITE THIS SECTOR
	TYA
	PHA
	LDA (DIRBUF),Y
	LDY #0
	STA (IP),Y      ; STORE IN NEW DIRECTORY
	PLA
	TAY
	INY
	DEX
	BNE ADDEN1
	INC IP
	INC IP
	LDA IP
	CMP #1
	BNE ADDE1
	DEC IP
ADDE1
	RTS
ADDEN2
	LDA TRACK
	PHA
	LDA SECTOR
	PHA
	LDY DRVNUM
	LDA DSTRK,Y
	STA TRACK
	LDA #0
	STA SECTOR
	JSR NXT0        ; GET NEXT TRACK AND SECTOR
	LDY #0
	LDA SECTOR
	STA (IP),Y      ; SET NEXT SECTOR LINK
	DEC IP
	LDA TRACK
	STA (IP),Y      ; SET NEXT TRACK LINK
	LDA #1
	STA JOBNUM
	JSR DOWRIT
	LDA #1
	JSR SETH        ; SET HEADER FOR NEXT WRITE
	PLA
	STA SECTOR
	PLA
	STA TRACK
	LDY #1
	LDA #0
	JSR SETBUF
	LDA #1
	STA IP
	LDX #30
	BNE ADDEN1      ; JUMP
.END
