.PAGE 'TSTFND '
;
; NEXT TRACK & SECTOR
; RETURNS NEXT AVAILABLE TRACK & SECTOR
; GIVEN CURRENT TRACK AND SECTOR
;
; ALLOCATION IS FROM DSTRK TOWARDS
; 1 & MAXTRK, BY FULL TRACKS
;
JNXTTS
	JSR GETHDR
JNXT0
	JSR GETHED      ; GET THE HEAD NUMBER
	STA TMPHED      ; SAVE IT
	LDA #3
	STA TEMP
NXTDS
	LDA TEMP
	PHA             ; SAVE TEMP
	JSR SETMAP
	PLA
	STA TEMP        ; RESTORE TEMP
	LDA (BMPNT),Y   ; GET # OF FREE BLOCKDS
	BNE FNDNXT      ; HAVE AT LEAST 1 FREE
	INC HEAD        ; NEXT HEAD
	LDA HEAD
	CMP HEADS
	BCC NXT1        ; HEAD < MAX HEAD
	LDA #0
	STA HEAD
NXT1
	CMP TMPHED
	BNE NXTDS       ; CHECK THE NEXT HEAD
	LDY DRVNUM
	LDA TRACK
	CMP DSTRK,Y
	BCC NXT2
	INC TRACK
	LDA TRACK
	CMP MAXTRK
	BNE NXTDS
	LDX DSTRK,Y
	DEX
	STX TRACK
	LDA #0
	STA SECTOR
	DEC TEMP
	BNE NXTDS
NXTERR
	LDX DRVNUM
	LDA DSTRK,X
	STA TRACK
	LDA #0
	STA HEAD
NXTER1
	JSR SETMAP
	LDA (BMPNT),Y
	BNE FNDNXT
	INC HEAD
	LDA HEAD
	CMP HEADS
	BCC NXTER1
	LDA #DSKFUL
	JMP CMDERR
NXT2
	DEC TRACK
	BNE NXTDS
	LDX DSTRK,Y
	INX
	STX TRACK
	LDA #0
	STA SECTOR
	DEC TEMP
	BNE NXTDS
	BEQ NXTERR
;
; FIND THE NEXT OPTIMUM SECTOR
; NEXT SECTOR = CURRENT SECTOR+N
;
FNDNXT
	LDA SECTOR
	CLC
	ADC SECINC
	STA SECTOR
	CMP SECTRS
	BCC FNDN0
	SEC
	LDA SECTOR
	SBC SECTRS
	STA SECTOR
	BEQ FNDN0
	DEC SECTOR
FNDN0
	JSR GETSEC
	BEQ FNDN2
FNDN1
	JMP USEDT0      ; HEAD ALREADY OUT
FNDN2
	LDA #0
	STA SECTOR
	JSR GETSEC
	BNE FNDN1
	JMP DERR
;
; RETURNS OPTIMUM INITIAL TRACK, SECTOR
;
JINTTS
	LDY DRVNUM
	LDX DSTRK,Y
	LDA INTFLG
	EOR #$FF
	STA INTFLG
	BEQ INTTS1
	INX
	BNE INTTS2      ; JUMP
INTTS1
	DEX
INTTS2
	STX TRACK
	LDX #0
	STX SECTOR
	JMP JNXT0
FND2
DERR
	LDA #DIRERR
	JSR CMDER2
;
; SET (INDIRECT) BAM PNTR BY DRVNUM
;
SETBMP
	LDX DRVNUM
	LDA IPBM,X
	STA BMPNT+1
	LDA #0
	STA BMPNT
	RTS
;
; SET BAM AND FIND AVAILABLE SECTOR
; STARTING AT SECTOR
;
GETSEC
	JSR SETMAP
	STY TEMP
	JSR AVCK        ; CHECK BITS & COUNT
GS10
	LDA SECTOR
	CMP SECTRS
	BCS GS20
	JSR FREUS3
	BNE GS30
	INC SECTOR
	BNE GS10        ; JUMP
GS20
	LDA #0
GS30
	RTS             ; (Z = 1), USED
;
; BIT MAP VALIDITY CHECK
;
AVCK
	LDA TEMP
	PHA             ; SAVE TEMP
	LDA #0
	STA TEMP        ; TEMP = 0
	TYA
	STA BMPNT       ; SET LOW BYTE TO TRACK BAM
	LDY BAMSIZ
	DEY
AC10
	LDX #7          ; COUNT THE BITS
AC20
	LDA (BMPNT),Y
	AND BMASK,X
	BEQ AC30
	INC TEMP
AC30
	DEX
	BPL AC20
	DEY
	BNE AC10
	LDA (BMPNT),Y
	CMP TEMP
	BNE AC40        ; COUNTS DO NOT MATCH
	LDA #0
	STA BMPNT       ; RESTORE PNTR
	PLA
	STA TEMP        ; RESTORE TEMP
	RTS
AC40
	LDA #DIRERR
	JSR CMDER2
;
; RETURNS # OF SECTORS ON TRACK
;
JMXSEC
	LDA SECTRS
	RTS
.END
