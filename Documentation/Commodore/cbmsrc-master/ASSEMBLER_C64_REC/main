.PAGE 'MAIN'

PASS1	LDX #STKVAL
	TXS
	CLD
	LDA #0
	STA XREF        ;ASSUME NO CROSS REF
	STX NOBJ
	STA LBOTH       ;LIST ONLY TO PRINTER
	STA CHAN
	STA PASS        ;SET PASS 1
	STA SFILE
	STA NOSYM
	STA NOSYM+1
	STA IFLAGS+1

	STA MACENT
	STA MACENT+1
	STA CURPNT
	STA CURPNT+1
	STA LERCT       ;RESET ERROR FLAG
	STA LERCT+1

	LDY #>SYTBST    ;START OF SYMTBL
	LDX #<SYTBST
	STX STSAVE
	STY STSAVE+1
	LDY #0          ;CLEAR FIRST SYM
	TYA
	STA (STSAVE),Y

	LDY #>SYEND
	LDX #<SYEND
	STX ISYEND
	STY ISYEND+1

	LDY #>MACTAB
	LDX #<MACTAB
	STX MCTBPT
	STY MCTBPT+1

	JSR RESET

	LDA #1
	STA LPGCT
	LDA #0
	STA LPGCT+1

	LDA #5          ;OPEN KEYBOARD FILE #5
	LDY #$FF
	LDX #0
	JSR FILPAR
	LDA #0
	JSR SETNAM
	JSR FOPEN

	LDA #1          ;OPEN DISPLAY FILE #1
	LDY #$FF
	LDX #3
	JSR FILPAR
	JSR FOPEN

XXXA2	LDA #$0D
	JSR BSOUT
	LDA #>STTMSG
	LDY #<STTMSG
	JSR WSCRN
	JSR CLRCH
	LDX #5
	STX CHAN
	JSR CHKIN
	LDX #40
	STX SAVEX
XXXA3	DEC SAVEX
	BEQ XXXA2
	JSR BASIN
	CMP #' '
	BEQ XXXA3
	CMP #$0D
	BEQ XXXA4
	LDX #0
	STX NOBJ
	STX OBJLEN
	BEQ XXXA6
XXXA8	JSR BASIN
	CMP #' '
	BEQ XXXA7
XXXA6	CMP #$0D
	BEQ XXXA7
	LDX OBJLEN
	CPX #18
	BEQ XXXA2
	STA OBJFIL,X
	INX
	STX OBJLEN
	BNE XXXA8
XXXA7	LDA #','
	STA OBJFIL,X
	INX
	LDA #'S'
	STA OBJFIL,X
	INX
	LDA #','
	STA OBJFIL,X
	INX
	LDA #'W'
	STA OBJFIL,X
	INX
	STX OBJLEN
XXXA4	LDA #>HCMSG
	LDY #<HCMSG
	JSR WSCRN
	JSR BASIN
	LDX #0
	STX NOPRIN
	CMP #'N'
	BEQ RDOPNX

; CENTRO PRINTER OPEN SUB.
; (CENTRONICS IS DISABLED)
;CENTRO LDA #$40+$80
;   STA NOPRIN
;   LDX #$FF
;   STX $E843   ;PORT DDR
;   LDA #$EC
;   STA $E84C
;   BIT $E841
;   JMP RDOPNX

;
; IEEE PRINTER OPEN SUB.

IEEEP	LDA #4
	TAX
	LDY #$FF
	JSR FILPAR
	LDA #0
	JSR SETNAM
	JSR FOPEN       ;OPEN PRINTER FILE
	LDA #$80
	STA NOPRIN

RDOPNX	LDA #>CRRMSG
	LDY #<CRRMSG
	JSR WSCRN
	JSR BASIN
	CMP #'Y'
	BNE XXXB2
	STA XREF
XXXB2	LDA #>SRCMSG
	LDY #<SRCMSG
	JSR WSCRN
	LDX #40
	STX SAVEX
XXXB4	DEC SAVEX
	BEQ XXXA4
	JSR BASIN
	CMP #' '
	BEQ XXXB4
	CMP #$0D
	BNE XXXB5
	JMP READY
XXXB5	PHA
	JSR OPNSB1
	PLA
	LDX #0
	STX XFNLEN
	BEQ XXXB6
XXXB8	JSR BASIN
	CMP #' '
	BEQ XXXB7
XXXB6	CMP #$0D
	BEQ XXXB7
	LDX XFNLEN
	CPX #18
	BEQ RDOPNX
	STA ISRC,X
	INX
	STX XFNLEN
	STX ISRCLN
	BNE XXXB8
XXXB7	LDA #','        ;PUT A COMMA AFTER THE NAME
	STA ISRC,X
	INX
	LDA #'S'        ;FOR THE AUTO OBJECT NAME FEATURE
	STA ISRC,X
	INX
	STX ISRCLN

; ****************
; * PASS 1 START
; ****************

	LDA #>CRMSG
	LDY #<CRMSG
	JSR WSCRN

PASS1D	JSR SOPEN       ;OPEN SOURCE FILE

	JSR SCROFF

	LDA #>P1MSG     ;'PASS 1'
	LDY #<P1MSG
	JSR WSCRN

PASS1C	JSR CLRTTL      ;CLR TITLE BUFF
	JSR PTTTL0      ;FORCE PAGE HEADING
	JMP SNEWLN      ;BEGIN ASSEMBLY

; *******************************
; * OPEN DISK COMMAND CHANNEL
; *******************************

OPNSB1	LDA #7
	LDX #8
	LDY #15
	JSR FILPAR
	LDA #'I'
	STA ISYM
	LDA #'0'
	STA ISYM+1
	LDY #9
	LDX #8
	LDA #2
	JSR SETNAM
	JMP FOPEN       ;OPEN COMMAND CHANNEL & SEND I

; ********************
; * OPEN SOURCE FILE
; ********************

SOPEN	LDA #$02        ;OPEN SOURCE FILE
	TAY
	LDX #$08
	JSR FILPAR
	LDY #>ISRC
	LDX #<ISRC
	LDA ISRCLN
	JSR SETNAM
	JSR FOPEN       ;OPEN SOURCE FILE
	JSR FDTEST
	LDA #$80
	STA SFILE
	RTS

SCROFF	LDA NOPRIN
	BPL XXX1
	LDA $D011
	AND #$EF
	STA $D011
XXX1	RTS

SCRON	LDA $D011
	ORA #$10
	STA $D011
	RTS

; ********************
; *      PASS2
; ********************

PASS2	JSR CLRCH       ;CLEAR OPEN CHAN
	LDA XREF        ;DOING A CROSS REF ?
	BNE PXREF       ;YES
	JMP PAS222      ;NO

PXREF	LDA #8          ;OPEN FILE TO SAVE SYMS
	TAX
	TAY
	JSR FILPAR
	LDY #>XNLAB     ;'0:XRLABEL,S,W'
	LDX #<XNLAB
	LDA #<XN11
	JSR SETNAM
	JSR FOPEN       ;OPEN CROSS REFERENCE SYMBOL FILE
	JSR FDTEST
	JSR CLRCH       ;CLEAR OTHER CHANNELS
	LDX #8          ;CHECK OUT
	STX CHAN
	JSR CKOUT
	LDA STSAVE      ;SET PTR TO BEGINNING
	LDY STSAVE+1
	STA BOTS
	STY BOTS+1
	LDA NOSYM+1     ;# OF SYMS
	LDY NOSYM
	STA DELS
	STY DELS+1
XSYMS	LDY #0          ;SAVE A SYM
XSY100	LDA (BOTS),Y
	JSR BSOUT
	INY
	CPY #8
	BNE XSY100
	LDA DELS        ;DONE ?
	BNE XSY120      ;NO
	DEC DELS+1
XSY120	DEC DELS
	LDA DELS
	ORA DELS+1      ;DONE ?
	BEQ XSY200      ;YES
	TYA             ;BUMP TO NEXT SYM
	CLC
	ADC BOTS
	STA BOTS
	BCC XSYMS       ;NO CARRY
	INC BOTS+1
	BNE XSYMS       ;ALWAYS

XSY200	JSR CLRCH
	LDA #8          ;CLOSE THE SYM FIL.
	JSR FCLOSE
	LDA #9          ;OPEN THE REFS FILE
	TAY
	LDX #8
	JSR FILPAR
	LDY #>XNREF     ;'0:XREF1234,S,W'
	LDX #<XNREF
	LDA #<XN21
	JSR SETNAM
	JSR FOPEN       ;OPEN CROSS REF FILE
	JSR FDTEST



PAS222	JSR RESET
	JSR OBJINT
	LDA #>P2MSG
	LDY #<P2MSG
	JSR WSCRN       ;OPEN OBJECT FILE
	LDA NOBJ
	BNE PASS2B

	LDA OBJLEN
	LDY #>OBJFIL
	LDX #<OBJFIL
	JSR SETNAM
	LDA #6
	TAY
	LDX #8
	JSR FILPAR
	JSR FOPEN       ;OPEN OBJECT FILE
	JSR FDTEST

PASS2B	JSR CLRTTL
	JSR SOPEN
PASS2A	JMP SNEWLN

RESET	LDA #0
	STA IPC
	STA IPC+1
	STA ICRDNO
	STA ICRDNO+1
	STA LTLLEN
;   STA LERCT
;   STA LERCT+1
	STA LEVEL

	LDA #$0D
	STA LCHAR       ;MAKES CLEAN START

	LDA #%11011100; NOG,NOS,NOE,INT,LIS
	STA IFLAGS

	LDY #>MCSTCK
	LDX #<MCSTCK
	STX MCSTPT      ;MACRO STACK POINTER
	STY MCSTPT+1
	STX STKFRM      ;STACK FRAME POINTER
	STY STKFRM+1

	LDA #'0'
	LDY #3
RESET1	STA MLABEL,Y
	DEY
	BNE RESET1
	LDA #'L'
	STA MLABEL
	RTS




WSCRN	PHA             ;WRITE TO PET SCREEN
	LDA #1
	CMP CHAN
	BEQ WSCRN1
	TYA
	PHA
	JSR CLRCH
	LDX #1
	STX CHAN
	JSR CKOUT
	PLA
	TAY
WSCRN1	PLA
	STA TBLPTR+1
	STY TBLPTR
	LDY #0
WSCRN2	LDA (TBLPTR),Y
	INY
	PHA
	AND #$7F
	JSR BSOUT
	PLA
	BPL WSCRN2
	RTS

CLRTTL	LDY #19
	LDA #$20
PASS2H	STA LTLBUF,Y
	DEY
	BPL PASS2H

	LDX ISRCLN      ;DON'T OUTPUT THE DRIVE #
	DEX
	DEX
	STX LTLLEN

PASS2D	LDA CHAN,X
	STA LTLBUF-1,X
	DEX
	BNE PASS2D
	RTS

; ************************
; *
; * -- SCAN NEW LINES --
; *  MAIN LOGIC OF
; *    PASS 1 & PASS 2
; *
; ************************

SNEWLN	JSR CHKBRK
	LDA #0          ;INIT NEXT CARD
	STA LCDPT
	STA IFLAGS+1
	STA ICSB
	STA ICSE
	STA IERR
	STA IMAXCL
	STA ICSL
	STA IEXP
	STA IEXP+1
	STA ILSST
	STA JLABL
	STA JORG
	STA JBYWOR
	STA ICOLP
	LDA LCHAR
	BEQ H8921
	JSR CARDIN
CARD0	BCC H87
H8921	JMP H10         ;EXIT ON NULL CHARACTER (NO .END CARD)

CARDIN	LDX #0
	INC ICRDNO+1
	BNE CARD1
	INC ICRDNO
CARD1	JSR GETCHR      ;INPUT A CHAR
	CMP #$0D
	BEQ CARD3       ;BRANCH IF A C.R.
	CMP #$00        ;IS IT NULL
	BNE CARD2
	SEC
	RTS

CARD2	STA ICRD,X
	INX
	JMP CARD1

CARD3	DEX
	STX IMAXCL
	CLC
	RTS

;
;  MAIN LINE BLOCK
;

H87	JSR NFNDNB      ;BLANK CARD?
	BCC H8830B      ;YES
H8830	LDX ICOLP
	LDA ICRD,X
	CMP #'>'        ;IF TERMINATOR CARD
	BNE H8830A
	JMP ENDLN
H8830A	CMP #';'
	BNE H8831       ;NOT A COMMENT CARD
H8830B	JMP H990        ;A COMMENT CARD
H8831	JSR NFNDEN      ;FIND STRING END
	BCS H1          ;END FOUND
	LDA #3          ;ERROR--END NOT FOUND
	LDX ICSB
	TAY
	JMP LTS1

H1	LDX ICSB
	LDA ICRD,X
	CMP #'.'
	BNE H8841       ;NOT ASSEM DIRECT
	JMP H5          ;AN ASSEM DIRECT

H8841	CMP #'*'
	BNE H8832       ;NOT AN ORG
	JMP H102        ;REDEFINE ORG

H8832	LDY ICSL
	CPY #6          ;6 CHARACTERS LONG
	BEQ H76         ;<=
	BCC H76
	LDA #9
	LDY #3
	JMP LTS1

H76	STY KLEN        ;LENGTH OF SYMBOL
	JSR CONSYM      ;CONSTRUCT THE SYMBOL
	BCS H3          ;NO ERRORS IN CONSYM
	LDA #$10
	LDY #3
	JMP LTS1

H3	LDA ICSL        ;LENGTH OF STRING
	CMP #3          ;RIGHT LENGTH FOR LABEL
	BNE H92         ;LABEL PROCESS-OVER 3
	JSR NOPFND      ;FIND A MNEMONIC
	BCC H92         ;FAILED-MUST BE A LABEL
	JMP H201        ;AN OP CODE


; ************************
; *   LABEL PROCESSING
; ************************

H92	LDX ICOLP
	JSR NALPH
	BCS H91         ;NO
	LDA #$08
	LDY #$03
	JMP LTS1

H91	JSR LABPRC      ;CALL PROCESS LABEL ROUTINE
	STX ILSST       ;SAVE PARMS FOR EQU
	LDX #0          ;SAVE THE SYMBOL
H8845	LDA ISYM,X
	PHA
	INX
	CPX #6
	BNE H8845

	LDA ICSL        ;SAVE ICSL
	PHA
	LDA ICSE
	STA ICOLP       ;SAVE ICSE
	INC ICOLP
	JSR NFNDNB      ;CHECK FOR EQUATE
	BCC H120        ;ONLY BLANKS FOUND
	LDA ICRD,X
	CMP #'='
	BNE H120
	LDA JLABL
	BNE XXXC1
	INC JLABL
	BNE H121        ;** BRA

H120	JSR NFIND       ;SEE IF GOOD LABEL
	BCC H95         ;NOT PRESENT

	LDA KNVAL       ;IS THE NEW VALUE THE SAME AS THE OLD VALUE ?
	CMP IPC+1
	BNE MR02        ;NO
	LDA KNVAL+1
	CMP IPC
	BEQ H95A        ;OK

MR02	LDA KNVAL       ;MACRO ?
	AND KNVAL+1
	CMP #$FF
	BNE MR04        ;NO

	JSR ISMAC       ;IN THE MACRO TABLE ALREADY ?
	BCC MR03        ;NO
	JMP CALMAC      ;CALL THE MACRO

MR03	LDA PASS        ;REDEFINED ONLY IF IN PASS 1 ?
	BNE H95A

MR04	LDA #2          ;PREVIOUSLY DEFINED
	LDY #3
	LDX ILSST
	JMP LTS1

H95	LDA JLABL
	BEQ H95B
XXXC1	LDA #3
	TAY
	LDX ICSB
	JMP LTS1

H95B	INC JLABL
	LDA IPC+1       ;PUT IN SYM TAB
	STA KNVAL
	LDA IPC
	STA KNVAL+1
	JSR NSERT
H95A	LDA IMAXCL      ;TRY FOR OPCODE
	CMP ICOLP       ;OFF CARD CHECK
	BCS H8842
	JMP H990        ;YES--OFF CARD
H8842	JMP H87         ;BACK TO MAIN SECT

.END
