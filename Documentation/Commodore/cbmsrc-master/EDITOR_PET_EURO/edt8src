.PAGE 'EDT8SRC'
;
; ADDITIONAL CONTROL FUNCTIONS
; ****************************
;
USRCRT	PHA
	JSR ACDSBL
	PLA
	JMP CRTSET
;
BUSCRT	LDA KYCTRL      ; CTRL N COMES HERE
	AND #%00001000
	BEQ TXT2
;
ACNABL	LDA KYCTRL      ; CTRL A, CTRL B, INIT
	ORA #%00001001
	STA KYCTRL
	LDA #<TEXT
	LDX #>TEXT
	LDY #$0E
	JMP CRTSET
;
GRPHON	JSR ACDSBL      ; CTRL SHFT N
	AND #%00001000
	BEQ GRAPH2
;
	LDA #<GRAF
	LDX #>GRAF
	LDY #$0C
	JMP CRTSET
;
GRAPH2	LDA #<GRAF2
	LDX #>GRAF2
	LDY #$0C
	JMP CRTSET
;
ACDSBL	LDA KYCTRL      ; CTRL SHFT A
	AND #%11111000
	STA KYCTRL
	RTS
;
TX2CRT	LDA KYCTRL      ; CTRL SHFT B
	AND #%11110000
	STA KYCTRL
;
TXT2	LDA #<TEXT2
	LDX #>TEXT2
	LDY #$0E
	JMP CRTSET
;
;
; TIME- WITHOUT STOPFLAG UPDATE
; *****************************
;
UDTIM	INC CRFAC
	LDA CRFAC
	BNE UD10
	INC CRFAC+1
UD10	CMP #$6F
	BNE UD20
	LDA CRFAC+1
	CMP #2
	BEQ UD50
UD20	INC TIME+2
	BNE UD30
	INC TIME+1
	BNE UD30
	INC TIME
UD30	LDX #0
UD40	LDA TIME,X
	CMP UD70,X
	BCC UD60
	INX
	CPX #3
	BNE UD40
	LDA #0
UD45	STA TIME-1,X
	DEX
	BNE UD45
	BEQ UD60
UD50	LDA #0
	STA CRFAC
	STA CRFAC+1
UD60	RTS
;
.SKI 2
;
UD70	.BYT $4F,$1A,01
;
.SKI 2
;
KEYROW	.BYT 80,72,64,56,48 ; KEYBOARD ARRAY
	.BYT 40,32,24,16,08 ; COORDINATE INDEXES
;
.END
