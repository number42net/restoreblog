.PAGE 'EDT7SRC'
;
CTRLCK	CLV
	LDA DATA
	TAX
	AND #$7F
	PHA
	CMP #$20
	BCS CRSRT4
;
	LDA KYCTRL
	TAY
	AND #%11111001
	STA KYCTRL
	TYA
	LSR A
	BCC CRSRT4
;
	LSR A
	BCS CRSRT1
;
	LSR A
	BCC CRSRT4
;
	CPX #$9D
	BNE CRSRT4
;
	JSR CRSLFT
	JMP CRSRT3
;
.SKI 2
;
CRSLFT	LDY SCLF
	CPY PNTR
	BCC CRSLF2
;
	LDY SCRT
	LDX SCTOP
	CPX TBLX
	BCS CRSLF1
;
	DEC TBLX
	STY PNTR
	LDX TBLX
	JMP SCRSET
;
CRSLF1	LDY SCLF
	STY PNTR
	RTS
;
CRSLF2	DEC PNTR
	RTS
;
.SKI 2
;
CRSRGT	PHA
CRSRT1	CPX #$1D
	BNE CRSRT4
;
	LDY PNTR
	INC PNTR
	CPY SCRT
	BCC CRSRT3
;
	JSR NXLN
	LDY SCLF
	STY PNTR
;
CRSRT3	BIT VSET
CRSRT4	PLA
	LDY PNTR
	RTS
;
.SKI 2
;
ASMCHR	CLV             ; ALLOW CURSOR TO ADVANCE
	TAX             ; SAVE ORIGINAL
	LDA KYCTRL      ; IS THE ACCENT
	LSR A           ; OPTION ON ?
	TXA             ; RESTORE ORIGINAL
	BCC ASMCH3      ; NO, PROCEED NORMAL
;
ASMCH1	LDY PNTR        ; YES, GET CHARACTER
	LDA (PNT),Y     ; FROM SCREEN
	JSR CHRASM      ; ASSEMBLE WITH ORIGINAL
	BVC ASMCH2      ; WAS ORIGINAL AN ACCENT ?
;
	JMP DSPP        ; YES, DISPLAY NOT MOVING CURSOR
;
ASMCH2	BCC ASMCH3      ; SPACE OVER ACCENT ?
;
	LDX #$1D        ; YES, MAKE A
	JSR CRSRGT      ; CURSOR RIGHT
;
ASMCH3	LDX INSRT       ; UPDATE
	BEQ ASMCH4      ; CHARACTER INSERT
;
	DEC INSRT       ; COUNTER AND
;
ASMCH4	RTS             ; DISPLAY NORMAL
;
.SKIP 2
;
CHRASM	TAY             ; SAVE SCREEN CHR,
	TXA             ; PUT ORIGINAL
	PHA             ; ON STACK,
	AND #$7F        ; STRIP REVERSE BIT
	TAX             ; OFF AND SAVE IN X.
	TYA             ; GET SCREEN CHARACTER,
	AND #$7F        ; STRIP REVERSE BIT OFF
	PHA             ; AND PUT ON STACK
	TXA             ; GET ORIGINAL
	CLV             ; ALLOW CURSOR TO ADVANCE
	CMP #$20        ; IS IT A SPACE ?
	BEQ CHAS10      ; YES, FLAG CURSOR RIGHT
;
	JSR CHAS14      ; CLR ACCENT RECOGN FLGS
	JSR ACCENT      ; IS ORIGINAL AN ACCENT ?
	BEQ CHASM5      ; YES, CONTINUE AT <5>
;
	JSR VOWEL       ; IS ORIGINAL A VOWEL ?
	BNE CHASM7      ; NO, RETURN ORIGINAL
;
	PLA             ; GET THE SCRN CHR
	JSR ACCENT      ; IS IT AN ACCENT ?
	BNE CHASM8      ; NO, RETURN ORIGINAL
;
CHASM1	LDA VWLPTN,X    ; YES. DO BOTH,
	PHA             ; CHARACTER AND
	AND ACCMSK,Y    ; ACCENT MATCH ?
	BEQ CHASM7      ; NO, RETURN ORIGINAL
;
	LDA ACHREF,X    ; YES.
	TAX             ; CALCULATE
	PLA             ; THE
;
CHASM2	LSR A           ; INDEX
	BCC CHASM3      ; OF
;
	INX             ; THE
;
CHASM3	DEY             ; NEW
	BPL CHASM2      ; CHARACTER
;
	PLA             ; WASTE ORIGINAL, BUT
	ASL A           ; SAVE THE REVERSE BIT
	LDA CHRTBL-1,X  ; GET THE NEW CHR
	AND #$7F        ; STRIP DISASSEMBLE LOCK BIT
	BCC CHASM4      ; AND RESTORE
;
	ORA #$80        ; THE REVERSE FLG
;
CHASM4	CLC             ; CLEAR CURSOR RIGHT CTRL FLG
;
	RTS             ; A DEAD SYMBOL ? EXIT HERE
;
CHASM5	LDA MNACTL      ; GET MAIN ACCENT CONTROL
	AND ACCMSK,Y    ; MASK CURRENT ACCENT
	BEQ CHASM6      ; A NORMAL OVERSTROKE ?
;
	LDX #%00000100  ; NO. A DEAD
	AND DDACTL      ; ACCENT ?
	BEQ CHAS55      ; NO
;
	DEX             ; YES, TELL CURSOR
	BIT VSET        ; NOT TO ADVANCE
;
CHAS55	TXA             ; SET RELEVANT
	ORA KYCTRL      ; ACCENT RECOGNITION
	STA KYCTRL      ; FLAG
;
CHASM6	PLA             ; GET SCREEN CHARACTER
	JSR VOWEL       ; IS IT A VOWEL ?
	BEQ CHASM1      ; YES, ASSEMBLE.
;
	PHA             ; NO, ADJUST STACK POINTER,
;
CHASM7	PLA             ; WASTE SCREEN CHARACTER
CHASM8	PLA             ; GET ORIGINAL
	CLC             ; CLEAR CURSOR RIGHT CTRL FLG
;
	RTS             ; YES, RETURN.
;
CHASM9	LDA DATA        ; GET DATA TO BE PRINTED
	AND #$7F        ; STRIP SHIFT BIT OFF
	CMP #$20        ; IS IT A CONTROL ?
	BCC CHAS13      ; YES, CLEAR ACC RECOGN FLG
;
	RTS             ; NO, RETURN
;
CHAS10	PLA             ; GET SCREEN CHAR
	JSR ACCENT      ; IS IT AN ACCENT ?
	BNE CHAS11      ; NO, CURSOR RIGHT CTRL OFF
;
	LDA KYCTRL      ; WAS LAST CHR
	AND #%00000010  ; AN ACCENT ?
	BNE CHAS12      ; YES, CURSOR RIGHT CTRL ON
;
CHAS11	CLC             ; NO, CURSOR RIGHT CTRL OFF
;
CHAS12	PLA             ; GET ORIGINAL (SPC)
;
CHAS13	TAX             ; SAVE ORIGINAL
CHAS14	LDA #%11111001  ; CLEAR ACCENT
CHAS15	AND KYCTRL      ; RECOGNITION
	STA KYCTRL      ; FLAGS
CHAS16	TXA             ; RESTORE ORIGINAL
	RTS             ; AND RETURN
;
.SKIP 4
;
PNDCHR	LDX STRGPT      ; ANY CHARACTERS PENDING ?
	BNE CHRDS1      ; YES, RETURN
;
	INC PNTR        ; NO, INCREMENT POINTER
	JMP QTSWC       ; AND TOGGLE QUOTE SWITCH
;
.SKIP 2
;
CHRDSM	LDY PNTR        ; GET SCREEN
	LDA (PNT),Y     ; CHARACTER
	JSR DSMCHR      ; DISASSEMBLE IT
	STA DATA        ; AND STORE IN "DATA"
CHRDS1	RTS             ; BYE !
;
.SKIP 2
;
DSMCHR	CLV             ; INIT CONTROL RECOGN. FLG
	TAX             ; SAVE SCREEN CHR.
	LDA KYCTRL      ; IS THE ACCENT
	LSR A           ; OPTION ON ?
	TXA             ; RESTORE SCRN CHAR
	BCC DSMCH6      ; EXIT IF ACCENT OFF
;
	PHA             ; PUT SCRN CHAR ON STACK
	LDX STRGPT      ; GET STRING POINTER
	BNE DSMCH4      ; CHARACTER PENDENT ?
;
	AND #$7F        ; NO, A NEW CHARACTER.
	LDX #CHRTBX-CHRTBL ; IS IT  PRESENT
;
DSMCH1	CMP CHRTBL,X    ; IN THE TABLE ?
	BEQ DSMCH3      ; YES, PROCEED AT <3>
;
	DEX             ; LOOK AT  NEXT
	BPL DSMCH1      ; TABLE EMPTY ?
;
	PLA             ; YES, RETURN SCREEN CHAR
	CLC             ; SIGNAL : NO CHARACTER FOLLOWS
DSMCH2	RTS             ; ADIOS
;
DSMCH3	LDA STGREF,X    ; GET REFERENCE TO
	STA STRGPT      ; STRING TABLE AND
	TAX             ; UPDATE STRING POINTER
;
DSMCH4	LDA STRGTB-1,X  ; GET INDEX TO VOWEL TABLE
	DEC STRGPT      ; POINT TO NEXT CHR
	LSR A           ; CHECK THE CONTROL BIT
	BCC DSMCH5      ; IS IT A CONTROL CHR ?
;
	BIT VSET        ; YES, SET V FLAG
;
DSMCH5	LSR A           ; MOVE EOS BIT TO CARRY
	TAX             ; USE REMAINDER AS INDEX
	PLA             ; WASTE ORIGINAL
	AND #$80        ; KEEP RVS BIT
	ORA VWLTBL,X    ; ADD STRING CHARACTER
	BCS DSMCH7      ; END OF STRING ?
;
DSMCH6	LDX #$00        ; RESET STRING
	STX STRGPT      ; POINTER
;
DSMCH7	RTS             ; CHEERIO
;
.SKIP 2
;
ACCENT	LDY #ACCTBX-ACCTBL ; Y CHARACTERS TO LOOK AT
ACCNT1	CMP ACCTBL,Y    ; IS ACCENT PRESENT IN TABLE ?
	BEQ ACCNT2      ; YES, RETURN
;
	DEY             ; NO, CHECK NEXT CHR
	BPL ACCNT1      ; TABLE EMPTY ?
;
ACCNT2	RTS             ; YES, RETURN
;
.SKIP 2
;
VOWEL	LDX #VWLTBX-VWLTBL ; X CHARACTERS TO INSPECT
VOWEL1	CMP VWLTBL,X    ; IS VOWEL PRESENT IN TABLE ?
	BEQ VOWEL2      ; YES, RETURN
;
	DEX             ; NO, CHECK NEXT CHAR
	BPL VOWEL1      ; TABLE EMPTY ?
;
VOWEL2	RTS             ; YES, RETURN
;
.SKIP 2
;
CBMNTL	LDX #CBMASX-CBMASC ; SEARCH CHARACTER IN
;
CBMNT1	CMP CBMASC,X    ; TABLE. FOUND ?
	BEQ CBMNT6      ; YES
;
	DEX             ; NO, CHECK NEXT.
	BPL CBMNT1      ; TABLE EMPTY ?
;
	TAX             ; YES.
	AND #$7F        ; STRIP BIT 7
	CMP #$20        ; A CONTROL ?
	BCC CBMNT4      ; YES
;
	CMP #$41        ; NO. AN ALPHA ?
	BCC CBMNT3      ; NO.
;
	CMP #$5B        ; REALLY AN ALPHA ?
	BCS CBMNT3      ; NO
;
	TXA             ; YES. BEYOND 128 ?
	BMI CBMNT2      ; YES
;
	ORA #$20        ; NO, ADD $20
;
CBMNT2	AND #$7F        ; STRIP BIT 7
	RTS             ; BYE !
;
CBMNT3	TXA             ; A SHIFTED NON ALPHA ?
	BPL CBMNT5      ; NO, KEEP ORIGINAL
;
	LDX #$20        ; YES, CONVERT TO "SPACE"
;
CBMNT4	TXA             ; CONTROL COMES HERE
;
CBMNT5	RTS             ; CHEERS
;
CBMNT6	LDA NTLASC,X    ; PICK CHR FROM XREF
	RTS             ; THAT'S IT
;
.SKIP 2
.END
