.PAG 'EDT3SRC'
; ****** RESET SCROLL WINDOW TO FULL SIZE
;
SRESET	LDA #0
	TAX
	JSR TOPLF       ;SET TOP LEFT
	LDA #24         ;# OF ROWS
	LDX #79         ;# OF COLUMNS
;
;SET BOTTOM RIGHT SCROLL WINDOW
;
BOTRT	STA SCBOT
	STX SCRT
	RTS
;
;SET TOP LEFT SCROLL WINDOW
;
TOPLF	STA SCTOP
	STX SCLF
	RTS
.SKI 3
	*=BEGIN+$0202
; *** PRINT A CHAR ***
;
; ++++++ MUST START @ $E202 ++++++
;
PRT	PHA
	STA DATA        ;SAVE CHAR
	TXA             ;SAVE REGS.
	PHA
	TYA
	PHA
	JMP (VOUT)      ;GO THRU INDIRECT
PRT55	LDA #0          ;INDIRECT COMES HERE
	STA CRSW
	JSR CTRLCK      ; GET THE CHAR TO PRINT AND
	BVS LOOP2       ; AND CHECK FOR DIRECT CONTROLS
;
	NOP             ;
	CMP #$1B        ;AN ESCAPE ?
	BNE PRT560      ;NO
	JMP NXCLR       ;YES - CLEAR QUOTE,INSERT & RVS
PRT560	LDA DATA        ;A SHIFTED CHAR ?
	BPL *+5         ;NO
	JMP NXTX        ;YES
	CMP #$D         ;A NORMAL RETURN
	BNE NJT1        ;NO
	JMP NXT1        ;YES
NJT1	CMP #$20        ;A CONTROL KEY ?
	BCC NTCN        ;YES
	AND #$3F        ;NO - MAKE A SCREEN CHAR
	JSR QTSWC       ;TEST FOR QUOTE
	JMP NXT3        ;PUT ON SCREEN
; *** CONTROL KEYS ***
NTCN	LDX INSRT       ;IN INSERT MODE ?
	BEQ CNC3X       ;NO
	JMP NC3         ;YES - GO PRT IN RVS
CNC3X	CMP #$14        ;A DELETE KEY ?
	BNE NTCN1       ;NO
	LDY SCLF        ;YES - TEST FOR WRAP UP
	CPY PNTR
	BCC BK0         ;NO WRAP UP
	JSR BKLN        ;FIX WRAP UP
	BNE BK2         ;ALWAYS
BK0	DEC PNTR        ;BACK UP
	LDY PNTR
BK1	INY             ;MOVE ALL DOWN
	LDA (PNT),Y
	DEY
	STA (PNT),Y
	INY
	CPY SCRT        ;RIGHT MAX OF WINDOW
	BNE BK1
BK2	LDA #$20
	STA (PNT)Y      ;CLEAR LAST CHAR
	BNE JPL3        ;ALWAYS
NTCN1	LDX QTSW        ;IN QUOTES ?
	BEQ NC3W        ;NO
CNC3	JMP NC3         ;YES - PRT IN RVS
NC3W	CMP #$12        ;A RVS KEY ?
	BNE NC1         ;NO
	STA RVS         ;YES - SET FLAG
NC1	CMP #$13        ;A HOME KEY ?
	BNE NC2         ;NO
	LDA FHOME       ;YES - IS THIS THE SECOND
	BPL NC100       ;NO
	JSR SRESET      ;YES - RESET SCROLL WINDOW
	CLC             ;MAKE FLAG PLUS
NC100	ROR FHOME       ;CHANGE SIGN
	JSR NXTD        ;MOVE TO TOP LEFT
	JMP LOPII       ;EXIT
NC2	CMP #$1D        ;A CRSR RIGHT KEY ?
	BNE NCX2        ;NO
	INY             ;BUMP RIGHT
	STY PNTR
	DEY
	CPY SCRT        ;OFF END OF LINE ?
	BCC JPL3        ;NO
	JMP JJS2        ;YES - GO TO NEXT LINE
NCX2	CMP #$11        ;A CRSR DOWN ?
	BNE TABCHK      ;NO
	JSR NXLN
JPL3	JMP LOOP2
;
;TAB FUNCTION
TABCHK	CMP #$09        ;TEST FOR TAB
	BNE ERASTO      ;NO - CHECK ERASE TO
	JSR GETTAB      ;TAB SET UP
TAB1	LDY TABPOS
	INC TABPOS      ;GO TO NEXT POS
	CPY SCRT        ;PAST EOL?
	BCC TAB2        ;NO => CONT
	LDA SCRT
	STA PNTR        ;SET POS TO EOL
	DEC TABPOS      ;RESET TABPOS
	BNE JPL3        ;RE-ENTER
TAB2	ASL BITMSK      ;ADJUST BIT MASK
	BNE TAB3        ;IF<> 0 => CONT
	INX             ;ELSE GET NEW TAB BYTE
	CPX #10         ;END OF BUFFER?
	BEQ JPL3        ;YES => IGNOR IT
	LDA #1          ;SET MASK
	STA BITMSK
TAB3	LDA TAB,X
	AND BITMSK      ;CHK FOR TAB SET
	BEQ TAB1        ;NOT SET => TRY AGAIN
	INY             ;TAB SET => SET PNTR
	STY PNTR
ERASTO	CMP #$16        ;ERASE TO END OF LINE ?
	BNE DLINE       ;NO - CHECK FOR DELETE LINE
	LDA #$20        ;YES - ERASE WITH A SPACE
	DEY
ERT10	INY
	STA (PNT),Y
	CPY SCRT        ;DONE ?
	BCC ERT10       ;NO
	BCS JPL3        ;ALWAYS
;
DLINE	CMP #$15        ;DELETE LINE ?
	BEQ DLN10       ;YES
	JMP SCTUP       ;NO - CHECK FOR SCROLL
DLN10	LDA SCTOP       ;SAVE PRESENT SCREEN TOP
	PHA
	LDA TBLX        ;MAKE TOP THE LINE ON
	STA SCTOP
	JSR SCRUP       ;ERASE THE LINE CRSR ON
	JMP ILN20       ;FINISH OFF
.SKIP 3
; *** SHIFTED KEYS ***
;
NXTX	AND #$7F        ;MASK SHIFT
	CMP #$7F        ;A PI
	BNE NXTX1       ;NO
	LDA #$5E        ;YES - SUB CORRECT CHAR
NXTX1	CMP #$20        ;SHIFTED CONTROL KEYS ?
	BCC *+5         ;YES
	JMP NXT33       ;NO - GO DISPLAY CHAR
	CMP #$D         ;A SHIFTED RETURN ?
	BNE UP5         ;NO
	JMP NXT1        ;YES - DO A RETURN
UP5	LDX QTSW        ;IN QUOTES ?
	BNE UP6         ;YES - PRT IN RVS
	CMP #$14        ;AN INSERT
	BNE UP9         ;NO
	LDY SCRT        ;ROOM TO INSERT ?
	LDA (PNT),Y
	CMP #$20
	BNE JPL4        ;NO - SKIP IT
	CPY PNTR        ;END OF LINE ?
	BCC JPL4        ;YES - FAR BEYOND
	BEQ JPL4        ;DONT INSERT ON LAST COLUMN
INS1	LDY SCRT        ;MOVE EVERYTHING RIGHT
INS2	DEY
	LDA (PNT),Y
	INY
	STA (PNT),Y
	DEY
	CPY PNTR
	BNE INS2
	LDA #$20        ;PUT A SPACE IN
	STA (PNT),Y
	LDA SCRT        ;LIMIT INSERTS
	SEC
	SBC PNTR
	SBC INSRT
	BMI JPL4        ;NO ROOM !
	INC INSRT       ;STICK IT IN
	BNE JPL4        ;ALWAYS
UP9	LDX INSRT       ;INSERTS IN QUOTE MODE ?
	BEQ UP2         ;NO
UP6	ORA #$40        ;SHOW AS RVS
	JMP NC3
UP2	CMP #$11        ;A CRSR UP ?
	BNE NXT2        ;NO
	LDX SCTOP       ;AT TOP OF WINDOW ?
	CPX TBLX
	BCS JPL4        ;YES - NO UP
	DEC TBLX        ;NO - DO IT
	JSR STUPT       ;FIX PTR
	BNE JPL4        ;ALWAYS
NXT2	CMP #$12        ;A RVS OFF ?
	BNE NXT6        ;NO
	LDA #0          ;YES - CLEAR FLAG
	STA RVS
NXT6	CMP #$1D        ;A CRSR LEFT ?
	BNE NXT61       ;NO
	LDY SCLF        ;TEST FOR WRAP UP
	CPY PNTR
	BCC NXT8        ;NO WRAP UP TO NEXT LINE
	JSR BKLN        ;FIX WRAP
	BNE JPL4        ;ALWAYS
NXT8	DEC PNTR        ;BACK UP
	BPL JPL4        ;ALWAYS
NXT61	CMP #$13        ;A CLR SCREEN ?
	BNE TABSET      ;NO
	JSR CLSR
	BNE JPL4        ;ALWAYS EXIT
;
TABSET	CMP #$09        ;SHIFT TAB?(TOGGLE TAB)
	BNE ERASF       ;NO
	JSR GETTAB      ;YES => DO TAB SET-UP
	LDA TAB,X       ;GET TAB STOR BYT
	EOR BITMSK      ;TOGGLE TAB BIT AT CURSOR POS
	STA TAB,X
JPL4	JMP LOOP2
;
ERASF	CMP #$16        ;ERASE FROM BEGINNING ?
	BEQ ERF050      ;YES
	JMP ILINE       ;NO - CHECK FOR INSERT LINE
ERF050	LDA #$20
	LDY SCLF        ;START FROM BEGINNING OF LINE
ERF10	CPY PNTR        ;DONE ?
	BCS JPL4        ;YES
	STA (PNT),Y
	INY
	BNE ERF10       ;ALWAYS
.SKIP 3
NXLN	LSR LSXP        ;*********** WHY ? *********
	LDX TBLX
	CPX SCBOT       ;OF THE BOTTOM OF WINDOW ?
	BCC NXLN1       ;NO
	JSR SCRUP       ;YES - SCROLL
	JMP STUPT
NXLN1	INC TBLX
	JMP STUPT
NXT1	LDY SCLF        ;LEFT MIN OF WINDOW
	STY PNTR        ;BACK TO FIRST COLUMN
	JSR NXLN        ;NEXT LINE
NXCLR	LDA #0
	STA INSRT       ;CLEAR INSERTS
	STA RVS         ;CLEAR RVS FIELD
	STA QTSW        ;CLEAR QUOTE MODE
	JMP LOOP2
.SKIP 5
; **** SCROLL ROUTINES ****
;
.SKIP 2
; *** SCROLL DOWN
SCRDWN	LDX SCBOT       ;SCROLL DOWN, START BOTTOM
	INX
SCD10	DEX
	JSR SCRLY       ;SET PNT AT TO LINE
	CPX SCTOP       ;DONE
	BEQ SCR40       ;YES
	DEX             ;SET FROM PNT
	JSR SADSET      ;TO PRESENT LINE
	INX             ;MINUS ONE
SCD20	INY             ;MOVE LINES
	LDA (SAL),Y
	STA (PNT),Y
	CPY SCRT        ;DONE ?
	BCC SCD20       ;NO
	BCS SCD10       ;ALWAYS
.SKIP 2
; *** SCROLL UP
SCRUP	LDX SCTOP       ;START SCROLL AT THE TOP
	DEX
SCR10	INX
	JSR SCRLY       ;SET PNT TO BEGIN OF LINE
	CPX SCBOT       ;AT LAST LINE ?
	BCS SCR40       ;YES
	INX             ;SET FROM PNT
	JSR SADSET      ;TO PRESENT LINE
	DEX             ;PLUS ONE
SCR20	INY
	LDA (SAL),Y     ;MOVE A LINE
	STA (PNT),Y
	CPY SCRT        ;DONE ?
	BCC SCR20       ;NO
	BCS SCR10       ;ALWAYS
SCR40	JSR CLRLN       ;CLEAR LAST LINE
.SKIP 2
;**************************************************
;*        SCROLL SLOW OR STOP SCROLLING           *
;*                                                *
;*                                                *
;* IS INVOKED BY  HITTING SPACE OR SHIFTED SPACE  *
;*                                                *
;* THIS ROUTINE  CHECKS THE  STOP KEY FLAG WHICH  *
;* IS  PERIODICALLY  UPDATED  BY  THE  INTERRUPT  *
;* DRIVEN KEYSCAN  ROUTINE.  ONLY HEX VALUES  $20 *
;* (SLOW SCROLL AND HALT RELEASE), $0A (HALT  AND *
;* HALT RELEASE) AND $FF (NO KEY)  ARE  ACCEPTED. *
;*                                                *
;* REGISTERS ALTERED   : A,X,Y,P(CNVZ)            *
;* STACK REQUIREMENT   : 0  BYTES                 *
;* ROM   REQUIREMENT   : 39 BYTES                 *
;* RAM   REQUIREMENT   : 0  BYTES                 *
;* ADDITIONAL SOFTWARE : NONE                     *
;**************************************************
;
HLTSLO	LDA STPFLG      ;GET STOP KEY
	LDX #$FF        ;SET TIME (LO) & NO KEY CNST
	LDY #$00        ;SET TIME (HI) CNST
	CMP #$A0        ;WAS A SHIFTED SPACE HIT ?
	BNE SLOW        ;NO, CHECK FOR SLOW SCROLL
;
HALT	CPX STPFLG      ;WAIT UNTIL KEYS
	BNE HALT        ;ARE RELEASED
;
HALT1	LDA STPFLG      ;GET STOPFLAG AGAIN
	ASL A           ;DISCARD THE SHIFT FLAG
	CMP #$40        ;GET OUT, IF THE SPACE OR
	BEQ SLOW1       ;SHIFT SPACE WAS HITTEN AGAIN
;
	JSR EXIT        ;DON'T EXIT, IF THE STOP KEY
	BNE HALT1       ;WAS NOT ACTIVE
;
SLOW	CMP #$20        ;IF THE SPACE BAR WAS NOT
	BNE SLOW2       ;HIT, EXIT. ELSE
;
SLOW1	DEX             ;WASTE TIME
	BNE SLOW1       ;UNTIL X AND
;
	DEY             ;Y REGISTERS
	BNE SLOW1       ;ARE EMPTY
;
	STY KBFPTR      ;CLEAR THE KEYBUFFER
SLOW2	RTS             ;BYE !
;
.END
