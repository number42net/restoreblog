.PAG 'CBM2SRC 2/5/80'
	*=BEGIN
.SKI 3
; ****** JUMP TABLE ******
;
JCINT	JMP CINT
JLP2	JMP LP2
JLOOP5	JMP LOOP5
JPRT	JMP PRT
JPULS	JMP PULS
JKEY	JMP KEY
JPREND	JMP PREND
JCLSR	JMP CLSR
JTXCRT	JMP BUSCRT
JGRCRT	JMP GRPHON
JCSET	JMP USRCRT
JSCRDN	JMP SCRDWN
JSCRUP	JMP SCRUP
JKYSCN	JMP KEYSCN      ; ORIGINAL SCAN
JBELL	JMP BELL
JFUT1	JMP BELL        ; SPARE JUMP (WAS REPEAT)
	JMP TOPLF
	JMP BOTRT
;
.SKI 3
FUT001	NOP
	NOP
	RTS
;
.SKI 3
	*=BEGIN+$004B
;
CINT	JSR EXTRA0      ;EXTRA SETUP STUFF
	JSR ACNABL      ;PUT IN TEXT MODE
CLSR	LDX SCTOP       ;START AT TOP OF WINDOW
	DEX
CLS10	INX             ;NEXT LINE
	JSR SCRLY       ;SET PNT TO BEGIN OF LINE
	JSR CLRLN       ;CLEAR THE LINE
	CPX SCBOT       ;DONE ?
	BCC CLS10       ;NO
NXTD	LDX SCTOP       ;MOVE TO TOP
	STX TBLX
STU10	LDY SCLF        ;LEFT OF THE SCREEN WINDOW
	STY PNTR
STUPT	LDX TBLX        ;RESET PTR TO LINE BEGIN
	JMP SCRSET      ;SET PNT TO BEGIN OF LINE
;
	JMP SCRLY       ; OS-IV COMPATIBILITY REASON
;
	*=BEGIN+$006F   ; """"""""""""""""""""
;
	JMP SCRSET      ; """"""""""""""""""""
;
.SKI 2
	*=BEGIN+$007A   ; """"""""""""""""""""
;
;**** SET GRAPHIC MODE ****
;
TXCRT	JMP BUSCRT      ;SET 6845 TO TEXT
	NOP
	NOP
	NOP
	NOP
	NOP
GRCRT	JMP GRPHON      ;SET 6845 TO GRAPHICS
	NOP
	NOP
	NOP
CRTSET	STA SAL         ;SET PTR AT TABLE
	STX SAH
	LDA PCR
	AND #$F0        ;CHANGE CA2 ONLY
	STA FNLEN       ;SAVE TEMP
	TYA
	ORA FNLEN       ;ADD CHAR SET
	STA PCR
	LDY #17         ;18 BYTE TABLE
CRT10	LDA (SAL),Y
	STY CRTCA       ;INDEX TO REG.
	STA CRTCB
	DEY
	BPL CRT10
	RTS
.SKI 2
	*=BEGIN+$00A7
.SKIP 2
; *** INPUT ROUTINES ***
; ++++++ MUST START @ $E0A7 ++++++
;
LP2	LDY KEYBUF      ;GET KEY FROM IRQ BUFFER
	LDX #0
LP1	LDA KEYBUF+1,X
	STA KEYBUF,X
	INX
	CPX KBFPTR
	BNE LP1
	DEC KBFPTR
	TYA
	CLI
	RTS
LOOP4	JSR PATCH2      ;GO ADD BELL SOUND
LOOP3	LDA KBFPTR
	STA BLNSW
	BEQ LOOP3
	SEI
	LDA BLNON       ;IS CHAR NORMAL ?
	BEQ LP21        ;YES
	LDA GDBLN       ;NO - GET ORIGINAL NUMBER
	LDY #0
	STY BLNON
	JSR DSPP        ;PUT CHAR ON SCREEN
LP21	JSR LP2
	CMP #$83
	BNE LP22
	SEI
	LDX #9
	STX KBFPTR
LP23	LDA RUNTB-1,X
	STA KEYBUF-1,X
	DEX
	BNE LP23
	BEQ LOOP3
LP22	CMP #$D
	BNE LOOP4
	LDY SCRT        ;RIGHT MAX OF WINDOW
	STY CRSW
CLP5	LDA (PNT)Y
	CMP #$20
	BNE CLP6
	DEY
	BNE CLP5
CLP6	INY
	STY INDX
	JSR PATCH1
	NOP
	STY QTSW
	LDA LSXP
	BMI LOP5
	CMP TBLX
	BNE LOP5
	LDA LSTP
	STA PNTR
	CMP INDX
	BCC LOP5
	BCS CLP2
; ++++++ MUST START @ $E116 ++++++
;
LOOP5	TYA
	PHA
	TXA
	PHA
	JMP (VINP)      ;GO THRU INDIRECT
LOP55	LDA CRSW        ;ENTRY FROM INDIRECT
	BEQ LOOP3
LOP5	JSR CHRDSM      ; DISASSEMBLE CHARACTER
	BVS LOP53       ; SKIP, IF CONTROL CHAR
	NOP             ; FILLER
LOP51	AND #$3F
	ASL DATA
	BIT DATA
	BPL LOP54
	ORA #$80
LOP54	BCC LOP52
	LDX QTSW
	BNE LOP53
LOP52	BVS LOP53
	ORA #$40
LOP53	NOP             ; DON'T INCREMENT
	NOP             ; POINTER ON PENDING
	JSR PNDCHR      ; CHARACTERS
	CPY INDX
	BNE CLP1
CLP2	LDA #0
	STA CRSW
	LDA #$D
	LDX DFLTN       ;FIX GETS FROM SCREEN
	CPX #3          ;IS IT THE SCREEN?
	BEQ CLP2A
	LDX DFLTO
	CPX #3
	BEQ CLP21
CLP2A	JSR PRT
CLP21	LDA #$D
CLP1	STA DATA
	PLA
	TAX
	PLA
	TAY
	LDA DATA
	CMP #$DE
	BNE CLP7
	LDA #$FF
CLP7	RTS
; *** TEST FOR QUOTE MODE ***
QTSWC	CMP #$22
	BNE QTSWL
	LDA QTSW
	EOR #$1
	STA QTSW
	LDA #$22
QTSWL	RTS
; *** OUTPUT CHARS ***
NXT33	ORA #$40
NXT3	LDX RVS
	BEQ NVS
NC3	ORA #$80
NVS	JSR ASMCHR      ; ASSEMBLE ORIGINAL WITH SCRN CHR
	BVS LOOP2       ; SKIP, IF RESULT IS AN ACCENT
	NOP
NVS1	JSR DSPP
JSTS	INC PNTR
	LDY SCRT        ;RIGHT MAX WINDOW
	CPY PNTR
	BCS LOOP2
	LDX TBLX
JJS2	JSR NXLN        ;CHECK FOR SCROLL
	LDY SCLF        ;LEFT MIN OF WINDOW
	STY PNTR
LOOP2	LDA #0          ;RESET HOME KEY FLAG
	STA FHOME
LOPII	PLA             ;EXIT FROM PRT
	TAY
	LDA INSRT
	BEQ LOP2
	LSR QTSW
LOP2	PLA
	TAX
	PLA
	CLI
	RTS
BKLN	LDY SCRT        ;TEST FOR WRAP UP
	LDX SCTOP       ;AT TOP ?
	CPX TBLX
	BCC BKLN1       ;NO
	LDY SCLF        ;YES - STOP THERE
	STY PNTR
	PLA             ;SKIP A LEVEL OF SUBROUTINE
	PLA
	BNE LOOP2       ;ALWAYS
BKLN1	DEC TBLX
	STY PNTR
	JMP STUPT       ;SET FRONT LINE PTR
.SKI 3
; CLEAR ONE LINE
;
CLRLN	LDA #$20
CLR10	INY
	STA (PNT),Y
	CPY SCRT
	BCC CLR10
	RTS
.SKI 2
PATCH1	LDY SCLF
	STY PNTR
	LDY #0
	RTS
.END
