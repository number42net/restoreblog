.PAGE 'EDT6SRC'
;
	*=BEGIN+$0800   ; INVISIBLE ROM IDENTIFICATION
;
	.BYT 'CBM 8032 EURO SCREEN '
	.BYT 'EDITOR 4V2E (E-901474-03) '
	.BYT '(C) 1981 COMMODORE ELECTRONICS LTD '
	.BYT ID1,ID2,ID3,ID4,ID5
;
.SKIP 2
;
	*=BEGIN+$0900
;
; ++++++++++ MUST START @ $E900 +++++++++++++
;
; *******************************************
; *      -EURO SCREEN EDITOR KERNEL-        *
; *******************************************
;
EUREDT	.BYT $16        ; ROM KEY (INVERTED HIGH ROM ADDRESS)
	.BYT ID1,ID2,ID3,ID4,ID5 ; COUNTRY IDENTIFIER
;
JASMCH	JMP ASMCHR      ; ASSEMBLE ORIGINAL WITH SCREEN
JCHRAS	JMP CHRASM      ; ASSEMBLE ORIGINAL WITH ACCUM.
JCHDSM	JMP CHRDSM      ; DISASSEMBLE FROM SCREEN
JDSMCH	JMP DSMCHR      ; DISASSEMBLE FROM ACCUMULATOR
JACCNT	JMP ACCENT      ; CHECK FOR ACCENT
JVOWEL	JMP VOWEL       ; CHECK FOR VOWEL
JCBMNT	JMP CBMNTL      ; CBM ASCII TO NATIONAL ASCII CONV.
JSCNKY	JMP SCNKEY      ; EURO KEY SCANNER
	*=EUREDT+36     ; (FUTURE EXPANSION)
;
; ****** GENERAL KEYBOARD SCAN ******
;
SCNKEY	LDA PIAL        ;INITIALIZE
	AND #$F0        ;KEY ROW
	STA PIAL        ;MULTIPLEXER,
	LDA KYCTRL      ; INITIALIZE
	ORA #%11000000  ; SHIFT AND
	STA KYCTRL      ; CONTROL FLAGS
	LDA STRGPT      ; PROTECT STRING POINTER
	PHA             ; OF MESS UP IN KEYSCAN
	LDX #80-1       ; SCAN 80 KEYS
	LDY #$FF        ;INITIALIZE NEW
	STY NEWKEY      ;KEY INDEX BUFFER
	INY
;
SCNK1	LDA KEYIMG,Y
	ORA CTLMSK,Y
	EOR #$FF
	STA TMPIMG
;
SCNK2	LDA PIAK        ;GET A NEW ROW
	STA KEYIMG,Y
	CMP PIAK        ;ARE KEYS BOUNCING ?
	BNE SCNK2       ;IF YES, TRY AGAIN
;
	ORA TMPIMG
	STY IMGPNT
	LDY #08
;
SCNK3	LSR A           ;IS A KEY CLOSED ?
	BCS SCNK6       ;NO, CHECK NEXT
;
	PHA             ;YES, SAVE THE REST OF THE KEYS
	LDA #$7F        ;SET THE SHIFT KEY MASK
	CPX #SHIFT1-KEYTBL ;IS THE RIGHT HAND
	BEQ SCNK5       ;SHIFT KEY DOWN ?
;
	CPX #SHIFT2-KEYTBL ;NO. IS THE LEFT
	BEQ SCNK5       ;HAND SHIFT KEY DOWN ?
;
	LDA #$BF        ;NO. SET THE CONTROL KEY MASK
	CPX #CONTRL-KEYTBL ;IS IT A CONTROL KEY ?
	BEQ SCNK5       ;YES, UPDATE KEY CONTROL FLAGS
;
	LDA #$FF        ;NO. IS THERE MORE THEN ONE
	CMP NEWKEY      ;REAL KEY DEPRESSED ?
	BEQ SCNK4       ;NO, SAVE THE NEW KEY INDEX
;
	TAX             ;YES, SET KEYBOARD ERROR CONDITION
;
SCNK4	STX NEWKEY      ;UPDATE KEY INDEX BUFFER
;
SCNK5	AND KYCTRL      ;MASK CONTROL FUNCTIONS
	STA KYCTRL      ;AND UPDATE KEY CONTROL FLAGS
	PLA             ;RESTORE THE REST OF THE PRESENT ROW
;
SCNK6	DEX             ;EXIT, WHEN ALL KEYS
	BMI SCNK7       ;ARE CHECKED
;
	DEY             ;
	BNE SCNK3
;
	INC PIAL
	LDY IMGPNT
	INY
	BNE SCNK1
;
SCNK7	PLA             ; RESTORE TEMPORARY
	STA STRGPT      ; ZERO PAGE LOCATION
	LDX NEWKEY      ;GET NEW KEY INDEX. WHEN NO KEY
	BMI SCNK8
;
	LDA #RPTDLY
	STA DELAY
	JSR SCNK24
	BVS SCNK19
;
	LDX KBFPTR
	CPX KBFLIM
	BCC SCNK14
;
	BCS SCNK15
;
SCNK8	LDY #10
	JSR SCNK21
	BNE SCNK12
;
	DEY
	TYA
	BIT KYCTRL
	BVS SCNK10
;
	BPL SCNK9
;
	LSR A
;
SCNK9	AND #$92        ; CREATE "RVS" (COMPATIBILITY)
SCNK10	JSR SCNK18
	LDY #RPTDLY+1
;
SCNK11	DEY
	STY DELAY
	RTS
;
SCNK12	LDX KEYROW-1,Y
;
SCNK13	DEX
	LSR A
	BCC SCNK13
;
	JSR SCNK22
	BNE SCNK20
;
	LDY DELAY
	BNE SCNK11
;
	DEC RPTCNT
	BNE SCNK20
;
	LDA #$04
	STA RPTCNT
	JSR SCNK24
	BVS SCNK19
;
	CMP OLDKEY
	BNE SCNK10
;
	LDX KBFPTR
	BNE SCNK15
;
SCNK14	STA KEYBUF,X
	INC KBFPTR
;
SCNK15	CMP #$03
	BNE SCNK16
;
	LDY #$EF
;
SCNK16	CMP #$E0
	BCC SCNK18
;
SCNK17	LDY #$FE
;
SCNK18	STY STPFLG
;
SCNK19	STA OLDKEY
	STA NEWKEY
;
SCNK20	RTS
;
SCNK21	LDA KEYIMG-1,Y
	ORA CTLMSK-1,Y
	EOR #$FF
;
SCNK22	BNE SCNK23
;
	DEY
	BNE SCNK21
;
SCNK23	RTS
;
SCNK24	LDA KEYTBL,X
	BIT KYCTRL
	BMI SCNK25
;
	LDA KEYTBH,X
;
SCNK25	TAY
	BVS SCNK26
;
	AND #$7F
	CMP #$41
	BCC SCNK27
;
	CMP #$5B
	BCS SCNK27
;
	TYA
	AND #$BF
	TAY
;
SCNK26	CPX #STOP-KEYTBL
	BEQ SCNK27
;
	CLV
;
SCNK27	TYA
	RTS
;
;
.END
