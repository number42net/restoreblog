.PAGE 'OMEGA007'

	*=$FF40

;*--------------------------------------------------*
;* SOUND CONTROL SUBROUTIN CALL BY ONE CYCLE        *
;*--------------------------------------------------*

SOUNDM
;
;----- MISSLE

SNM200
	LDA LASER
	BEQ SNM400
	ASL A
	ASL A
	ASL A
	STA FREHI2
	DEC LASER
	BNE SNM400
	LDA #$20
	STA VCREG2
;
;-----  ENEMY BOMB

SNM400
	LDA BOMBFL
	BEQ SNM500
	ASL A
	ASL A
	ASL A
	STA FREHI1
	DEC BOMBFL
	BNE SNM500
	LDA #$20
	STA VCREG1
;
;-----  EXPLOD

SNM500
	LDA EXPLOD
	BEQ SNM600
	LDA SP1Y
	STA FREHI3
;
;-----  BACK

SNM600
	LDA EXPLOD
	ORA BOMBFL
;       ORA EMARGE
	BNE SNM700
	LDA SPRITA+5
	ORA SPRITA+6
	CMP #$02
	BCC *+4
	DEC SNDTIM
	DEC SNDTIM
	BPL SNM700
	LDA #$21
	STA VCREG1
	LDY SNDDAT
	INC SNDDAT
	INC SNDDAT
	INC SNDDAT
	LDA SNDTB1+2,Y
	STA SNDTIM
	LDA SNDTB1+1,Y
	STA FRELO1
	LDA SNDTB1,Y
	STA FREHI1
	BNE SNM700
	STA SNDDAT
	LDA #$20
	STA VCREG1
;
;-----  EMARGENCY

SNM700
;       LDA EMARGE
;       BEQ SNM800
;       LDA TIMER1
;       AND #$7F
;       STA FREHI1
;       BNE SNM800
;       DEC EMARGE
;       LDA #$21
;       STA VCREG1

SNM800
	RTS
;
;----- ENEMY ALL KILL

SND600
;
;----- THRUST OFF

SNF300
	CMP #$83
	BNE SNF500
	LDA #$00
	STA THRSND
	LDA EXPLOD
	BNE SNF399
	LDA #$80
	STA VCREG3
SNF399	
	RTS
;
;----- EXPLOD OFF

SNF500
	CMP #$85
	BNE SNF599
	LDA #$80
	STA VCREG3
	LDA #$20
	STA VCREG1
	STA VCREG2
	LDA #$00
	STA EXPLOD
	STA BOMBFL
	STA LASER
	STA SNDDAT
SNF599	
	RTS
;
;----- SPRITE MOTION DISTANCE TABLE

WALKY
	.BYTE $FE,$FE,$FE,$FF,$00,$01,$02,$02
	.BYTE $02,$02,$02,$01,$00,$FF,$FE,$FE
WALKX
	.BYTE $00,$01,$02,$02,$02,$02,$02,$01
	.BYTE $00,$FF,$FE,$FE,$FE,$FE,$FE,$FF

.END
