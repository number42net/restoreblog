.PAGE 'OMEGA002'

;*--------------------------------------------------*
;*     GAME  START  INITIALIZE                      *
;*--------------------------------------------------*

GMINIT
	LDA #00         ;SCORE CLEAR
	STA SCORE
	STA SCORE+1
	STA SCORE+2

	STA XTRA        ;EXTRA SHIP FLAG OFF

	STA ROUND       ;ROUND NUMBER CLEAR

	LDX #8-1        ;SET ENEMIES3 TABLE
	LDA #$FF
GMI010
	STA ENMY3A,X
	DEX
	BPL GMI010

	STA NUMENM      ;ENEMY ALL DONE
	RTS


;*--------------------------------------------------*
;*     ROUND  INITIALIZE                            *
;*--------------------------------------------------*

RDINIT
	LDA NUMENM      ;ENEMY ALL DONE?
	BPL *+4         ;NO
	INC ROUND       ;NEXT ROUND NUMBER
	LDX ROUND
	CPX #$03        ;PAST 3 ROUND ?
	BCC *+4         ;NO
	LDX #$04
	LDA ENMCNT-1,X  ;GET NUMBER OF ENEMIES
	STA NUMENM      ;SET IT
	LDA EN0TM0-1,X  ;GET ENEMY0 BIGEN TIME
	STA EN0WTV      ;SET IT
	LDA EN0TIM-1,X  ;GET ENEMY0 RESET INTERVAL TIME
	STA EN0WTF      ;SET IT
	LDA EN1TIM-1,X  ;GET ENEMY1 MOVE WAIT TIME
	STA EN1WTF      ;SET IT
	LDA EN2TIM-1,X  ;GET ENEMY2 MOVE WAIT TIME
	STA EN2WTF      ;SET IT
	LDA EN3TIM-1,X  ;GET ENEMY3 DISPLAY TIME
	STA EN3WTF      ;SET IT
	LDA #6
	STA EN3WTV      ;SET ENEMY3 BIGEN TIME

	LDA #01
	STA EN1WTV      ;
	STA EN2WTV      ;

	LDA RANDOM      ;GET RANDOM VALUE
	AND #$01
	STA ENROTE      ;SET ENEMIES ROTATION

	ASL A           ;SET ENYMIES 2 TABLE
	TAY
	LDA EN2TB,Y
	STA WK0
	LDA EN2TB+1,Y
	STA WK1
	LDY #10
RDI010
	LDA (WK0),Y
	STA ENMY2A,Y
	DEY
	BPL RDI010

	LDY #10         ;ERASE ENYMIES BY ROUND NO.
	LDA #$FF
RDI020
	CPX #4
	BEQ RDI030
	STA ENMY2A,Y
	DEY
	STA ENMY2A,Y
	DEY
	INX
	BNE RDI020      ;JMP
RDI030
	LDY #00         ;SET ENEMIES 2 TABLE
RDI040
	LDA EN2BGN,Y
	STA EN2YP,Y     ;EN2YP(11),EN2XPL(11),EN2XPH(11)
	INY
	CPY #11
	BNE RDI040
RDI042
	LDA EN2BGN,Y
	STA EN2XPL-11,Y
	INY
	CPY #22
	BNE RDI042
RDI044
	LDA EN2BGN,Y
	STA EN2XPH-22,Y
	INY
	CPY #33
	BNE RDI044
;
;-----  OFF ENEMY3 DISPLAY BIT

	LDX #8-1
RDI046
	LDA ENMY3A,X
	AND #$7F
	STA ENMY3A,X
	DEX
	BPL RDI046
;
;-----  KILL ENEMY3 AROUND SHIP

	LDX #8-1
RDI048
	LDA EN3YP,X     ;GET ENEMY3 Y POINT
	CMP #82         ;<82 ?
	BCC RDI054      ;YES
	CMP #106        ;>=106 ?
	BCS RDI054      ;YES
	LDA EN3XPH,X
	BNE RDI050      ;>255
	LDA EN3XPL,X
	CMP #55         ;<55 ?
	BCC RDI054      ;YES
	CMP #79         ;>=79 ?
	BCS RDI054      ;YES
	BCC RDI052
RDI050
	LDA EN3XPL,X
	CMP #24         ;>=24 ?
	BCS RDI054      ;YES
RDI052
	LDA #$FF
	STA ENMY3A,X    ;KILL ENEMY3
RDI054
	DEX
	BPL RDI048
;
;----- KILL ALL SPRITE FUNCTION

	LDY #08-1
	LDA #$FF
RDI058
	STA SPRITA,Y
	DEY
	BPL RDI058

	LDY #24-1
	LDA #01
RDI060
	STA SPRITB,Y
	DEY
	BPL RDI060
;
;-----  SET SPRITE FUNCTION

	LDA #00
	STA SPRITA+0    ;SET SPRITE 0 SHIP
	LDY #00
	STY SPRITA+5    ;SET SPRITE 5 ENEMY1A
	INY
	STY SPRITA+6    ;SET SPRITE 6 ENEMY1B

	LDA EN1WTF      ;GET ENEMY1 RESET INTERVAL TIME
	STA SPRITC+5    ;SET IT TO SPRITE 5
	STA SPRITC+6    ;SET IT TO SPRITE 6

	LDA ENROTE
	BNE RDI070
	LDA #02
	STA SPRITD+5
	LDA #07
	STA SPRITD+6
	LDA #55
	STA SP0X
	LDA #00
	BEQ RDI080      ;JMP
RDI070
	LDA #$0E
	STA SPRITD+5
	LDA #$09
	STA SPRITD+6
	LDA #47
	STA SP0X
	LDA MASKS+0
RDI080
	STA MSIGX
	LDA #82
	STA SP0Y

	LDA #199
	STA SP5X
	LDA #201
	STA SP5Y
	LDA #159
	STA SP6X
	LDA #185
	STA SP6Y

	LDA #$00        ;FLAGS INITIALIZE
	STA FIRE
	STA DFIRE
	STA THRUST
	STA FTIME
	STA EN2IDX
	STA EMARGE
	STA TIMER3
	STA ACSSEL
	STA KILLSP

	LDX #$00
RDI090
	STA $200,X
	STA $300,X
	INX
	BNE RDI090

	JSR CLRCRT      ;CLEAR SCREEN

	LDX #<GMSCRN    ;FLAME DISPLAY
	LDY #>GMSCRN
	JSR CHROUT

	JSR DSPSCR      ;DISPLAY SCORE

	JSR DSPHSC      ;DISPLAY HIGH SCORE

	JSR DSPSHP      ;DISPLAY SHIP IN FLAME

	LDA #%01100001
	STA SPENA
	RTS


;*--------------------------------------------------*
;*     DISPLAY  SCORE                               *
;*--------------------------------------------------*

DSPSCR
	LDX #<SCRN1
	LDY #>SCRN1

	STX WK0
	STY WK1
	LDX #0
	LDY #5
SCO10
	LDA SCORE,X
	AND #$0F
	STA WK2
	INC WK2
	LDA WK2
	STA (WK0),Y
	DEY

	LDA SCORE,X
	LSR A
	LSR A
	LSR A
	LSR A
	STA WK2
	INC WK2
	LDA WK2
	STA (WK0),Y
	INX
	DEY
	BPL SCO10
	RTS


;*--------------------------------------------------*
;*     DISPLAY  HI-SCORE                            *
;*--------------------------------------------------*

DSPHSC
	LDX #<SCRN2
	LDY #>SCRN2

	STX WK0
	STY WK1
	LDX #0
	LDY #5
HSC10
	LDA LSCORE,X
	AND #$0F
	STA WK2
	INC WK2
	LDA WK2
	STA (WK0),Y
	DEY

	LDA LSCORE,X
	LSR A
	LSR A
	LSR A
	LSR A
	STA WK2
	INC WK2
	LDA WK2
	STA (WK0),Y
	INX
	DEY
	BPL HSC10
	RTS


;*--------------------------------------------------*
;*     DISPLAY   SHIP IN FLAME                      *
;*--------------------------------------------------*

DSPSHP
	LDX #<SCRN3
	LDY #>SCRN3
	STX WK0
	STY WK1

	LDA #6
	STA WK3
	LDY #0

	LDA #$42
	LDX NUMSHP
	BNE DSHP20
DSHP10
	LDA #$00
DSHP20
	STA (WK0),Y
	INY
	INY
	DEC WK3
	BEQ DSHP90
	DEX
	BNE DSHP20
	BEQ DSHP10

DSHP90
	RTS


;*--------------------------------------------------*
;*   SOMETHING ON-OFF BY TIME                       *
;*--------------------------------------------------*

;-----  FLASH FLAME CONTROL

SOMONE
	LDX #$07
SOM010
	LDA LINTIM,X
	BEQ SOM020
	DEC LINTIM,X
	LDA LINTIM,X
	BEQ SOM050      ;LINE OFF
	CMP #$05
	BEQ SOM040      ;LINE HALF OFF
	CMP #$0A
	BEQ SOM030      ;LINE ON
SOM020
	DEX
	BPL SOM010
	BMI SOM100      ;JMP
SOM030
	TXA
	ASL A
	TAY
	LDA ONTB,Y
	TAX
	LDA ONTB+1,Y
	TAY
	JMP SOM060

SOM040
	TXA
	ASL A
	TAY
	LDA HOFTB,Y
	TAX
	LDA HOFTB+1,Y
	TAY
	JMP SOM060

SOM050
	TXA
	ASL A
	TAY
	LDA OFFTB,Y
	TAX
	LDA OFFTB+1,Y
	TAY
SOM060
	JSR CHROUT
;
;----- CALL BOMB SUBROUTINE

SOM100
	JSR BMBSPR
;
;----- WAIT TIMER RENEW

	LDA NUMSHP
	ORA NUMENM
	ORA SPRITA
	BMI SOM200
	LDA TIMER3
	AND #$7F
	STA TIMER3
SOM200
	RTS

;*--------------------------------------------------*
;* ENEMISE 2 MOVE                                   *
;*--------------------------------------------------*

EN2MOV
	DEC EN2WTV      ;DISPLAY TIME ?
	BEQ *+5         ;YES
	JMP E2M090      ;EXIT

	LDA EN2WTF
	STA EN2WTV      ;RESET INTERVAL TIMER

	INC EN2IDX      ;ENEMY 2 DISPLAY INDEX
	LDX EN2IDX
	CPX #11
	BCC *+4
	LDX #00
	STX EN2IDX
	LDA ENMY2A,X
	CMP #$FF
	BNE *+5
	JMP E2M090      ;NONE EXIST

	LDA ENMY2A,X    ;GET ENEMISE 2 DIRECTION
	AND #$C0        ;RIGHT?
	BNE E2M010      ;NO
E2M004
	INC EN2XPL,X    ;INC X POINT L
	BNE *+4
	INC EN2XPH,X    ;INC X POINT H
	LDA EN2XPL,X
	AND #$07        ;NEW CHRACTER NUMBER?
	BNE E2M08A      ;NO, GO TO DISPLAY
	DEC ENMY2A,X    ;DEC CHRACTER NUMBER
	LDA ENMY2A,X
	AND #$3F        ;CHRACTER NUMBER ZERO?
	BNE E2M08A      ;NO, GO TO DISPLAY
	LDA EN2TB3,X    ;GET Y SPAN
	ORA #$40        ;OR UP MODE
	LDY ENROTE      ;LEFT ROTATE?
	BEQ *+4         ;YES
	ORA #$80        ;OR DOWN MODE
	STA ENMY2A,X    ;SET NEW MOVE VALUE
E2M08A
	JMP E2M080      ;GO TO DISPLAY
E2M010
	CMP #$80        ;LEFT?
	BNE E2M020      ;NO
E2M014
	DEC EN2XPL,X    ;DEC X POINT L
	LDA EN2XPL,X
	CMP #$FF
	BNE *+4
	DEC EN2XPH,X    ;DEC X POINT H
	AND #$07        ;NEW CHRACTER NUMBER?
	BNE E2M080      ;NO, GO TO DISPLAY
	DEC ENMY2A,X    ;DEC CHRACTER NUMBER
	LDA ENMY2A,X
	AND #$3F        ;CHRACTER NUMBER ZERO?
	BNE E2M080      ;NO, GO TO DISPLAY
	LDA EN2TB3,X    ;GET Y SPAN
	ORA #$40        ;OR UP MODE
	LDY ENROTE      ;LEFT ROTATE?
	BNE *+4         ;NO
	ORA #$80        ;OR DOWN MODE
	STA ENMY2A,X    ;SET NEW MOVE VALUE
	BNE E2M080      ;JMP DISPLAY

E2M020
	CMP #$40        ;UP?
	BNE E2M030      ;NO
	DEC EN2YP,X     ;DEC Y POINT
	LDA EN2YP,X
	AND #$07        ;NEW CHRACTER NUMBER?
	BNE E2M080      ;NO, GO TO DISPLAY
	DEC ENMY2A,X    ;DEC CHRACTER NUMBER
	LDA ENMY2A,X
	AND #$3F        ;CHRACTER NUMBER ZERO?
	BNE E2M080      ;NO, GO TO DISPLAY
	LDA EN2TB2,X    ;GET X SPAN
	LDY ENROTE      ;LEFT ROTATE?
	BNE *+4
	ORA #$80        ;OR LEFT MODE
	STA ENMY2A,X    ;SET NEW MOVE VALUE
	BNE E2M080      ;JMP DISPLAY

E2M030
	INC EN2YP,X     ;INC Y POINTER
	LDA EN2YP,X
	AND #$07        ;NEW CHRACTER NUMBER?
	BNE E2M080      ;NO, GO TO DISPLAY
	DEC ENMY2A,X
	LDA ENMY2A,X
	AND #$3F        ;CHRACTER NUMBER ZERO?
	BNE E2M080      ;NO, GO TO DISPLAY
	LDA EN2TB2,X    ;GET X SPAN
	LDY ENROTE      ;LEFT ROTATE?
	BEQ *+4         ;YES
	ORA #$80        ;OR LEFT MODE
	STA ENMY2A,X    ;SET NEW MOVE VALUE

E2M080
	JSR EN2DSP
E2M090
	RTS


;*--------------------------------------------------*
;* DISPLAY ENEMY 2                                  *
;*         INPUT X-REG DISPLAY ENEMY INDEX          *
;*--------------------------------------------------*

EN2DSP
	JSR BITCHR      ;GET SCREEN ADDRESS (WK0,WK1)

	LDA ENMY2A,X
	AND #$40        ;UP DOWN MODE ?
	BNE E2D010      ;YES
;
;----- LEFT RIGHT MODE

	LDA EN2XPL,X
	AND #$07
	STA WK2
	ASL A
	ASL A
	ADC WK2
	ADC WK2         ;*6
	ADC #E2XPTN     ;PATTERN START NUMBER
	TAY
	STY WORK+$02
	INY
	STY WORK+$03
	INY
	STY WORK+$04
	INY
	STY WORK+$08
	INY
	STY WORK+$09
	INY
	STY WORK+$0A
	LDY #$FE
	STY WORK+$05
	INY
	STY WORK+$0B

	CLC
	LDA WK0
	STA WORK+$00
	ADC #40
	STA WORK+$06
	LDA WK1
	STA WORK+$01
	ADC #00
	STA WORK+$07
	JMP E2D020
;
;----- UP DOWN MODE

E2D010
	LDA EN2YP,X
	AND #$07
	STA WK2
	ASL A
	ASL A
	ADC WK2
	ADC WK2         ;*6
	ADC #E2YPTN     ;PATTERN START NUMBER
	TAY
	STY WORK+$02
	INY
	STY WORK+$03
	INY
	STY WORK+$07
	INY
	STY WORK+$08
	INY
	STY WORK+$0C
	INY
	STY WORK+$0D
	LDY #$FE
	STY WORK+$04
	STY WORK+$09
	INY
	STY WORK+$0E

	CLC
	LDA WK0
	STA WORK+$00
	ADC #40
	STA WORK+$05
	LDA WK1
	STA WORK+$01
	ADC #00
	STA WORK+$06
	CLC
	LDA WK0
	ADC #80
	STA WORK+$0A
	LDA WK1
	ADC #00
	STA WORK+$0B

E2D020
	LDX #<WORK
	LDY #>WORK
	JSR CHROUT
	RTS

E2XPTN	= $44
E2YPTN	= $74


;*--------------------------------------------------*
;* ENEMIES 3 DISPLAY CHECK                          *
;*--------------------------------------------------*

E3APTN	= $A4
E3BPTN	= $A5

EN3MOV
	CLC
	LDA CYCLE       ;DATA=0---7
	ADC #$0B
	TAX
	LDA ENMY2A,X
	CMP #$FF        ;ENEMY3 LIVE?
	BEQ E3M090      ;NO
;                       ;ALREADY DISPLAY?
	BPL E3M020      ;NO

	JSR BITCHR      ;GET SCREEN ADDRESS (WK0,WK1)
	LDY #00
	LDA (WK0),Y     ;ENEMY3 LIVE?
	CMP #E3APTN     ;ENEMY3 CHARACTER NUMBER A?
	BEQ E3M090      ;YES
	CMP #E3BPTN     ;ENEMY3 CHARACTER NUMBER B?
	BEQ E3M090      ;YES
E3M010
	LDA #$FF
	STA ENMY2A,X    ;LIVE MODE OFF
	BMI E3M090      ;JMP EXIT
;
;----- DISPLAY ENEMY3

E3M020
	JSR BITCHR      ;GET SCREEN ADDRESS (WK0,WK1)

	LDY #00
	LDA (WK0),Y
	CMP #E3APTN
	BEQ E3M010
	CMP #E3BPTN
	BEQ E3M010

	LDA WK0
	STA WORK+$00
	LDA WK1
	STA WORK+$01
	LDA ENMY2A,X
	ORA #$80
	STA ENMY2A,X    ;SET DISPLAY BIT
	AND #$01
	CLC
	ADC #E3APTN     ;ENEMY3 CHARACTER NUMBER
	STA WORK+$02
	LDA #$FF
	STA WORK+$03

	LDX #<WORK
	LDY #>WORK
	JSR CHROUT
E3M090
	RTS


;*--------------------------------------------------*
;*  BIT MAP ADDRESS CHANGE TO SCREEN ADDRESS        *
;*--------------------------------------------------*

BITCHR
	LDA #<SCREEN
	STA WK0
	LDA #>SCREEN
	STA WK1

	LDA EN2YP,X     ;GET Y POINTER
	SEC
	SBC #48
	AND #$F8        ;CUT OFF < 8
	STA WK2
	CLC             ;POINTER/8*40=POINTER/8*8*5
	ADC WK0
	STA WK0
	LDA WK1
	ADC #00
	STA WK1

	LDA #00
	ASL WK2
	ROL A
	ASL WK2
	ROL A
	CLC
	ADC WK1
	STA WK1
	CLC
	LDA WK2
	ADC WK0
	STA WK0
	LDA WK1
	ADC #00
	STA WK1         ;(WK0,WK1)=(WK0,WK1)+(YPOINT-48)/8*40

	LDA EN2XPH,X    ;X POINT HIGH
	LSR A           ;CARRY  = X POINT HIGH
	LDA EN2XPL,X    ;X POINT LOW
	ROR A
	LSR A
	LSR A           ;/8
	SEC
	SBC #03

	CLC
	ADC WK0
	STA WK0
	LDA WK1
	ADC #00
	STA WK1         ;(WK0,WK1)=(WK0,WK1)+(XPOINT-24)/8

	RTS

.END
