.PAGE 'OMEGA005'

;*--------------------------------------------------*
;* ENEMY'S MISSLE SET & MOVE                        *
;*--------------------------------------------------*

ENMMSL
	LDA SPRITA+7    ;GET MISSLE MODE
	BMI ENS000      ;LIVE?
;
;----- MISSLE MOVE

ENS200
	LDX #07
	JSR SPRMOV

	LDA WK5         ;MISSLE ARIVE LIMIT?
	BMI ENS900      ;NO

	LDA #$FF
	STA SPRITA,X
	EOR MASKS,X
	AND SPENA
	STA SPENA

	LDA MASKS,X
	EOR #$FF
	AND MSIGX
	STA MSIGX
ENS900
	RTS

;
;-----  SET NEW MISSLE

ENS000
	LDA TIMER1
	AND #$20
	BEQ ENS900

	LDX #05
	LDA SPRITA,X    ;ENEMY1A LIVE?
	BPL ENS010      ;YES
	INX
	LDA SPRITA,X    ;ENEMY1B LIVE?
	BMI ENS900      ;NO
ENS010
	LDY #00
	STY SPRITA+7    ;MODE BEGIN
	INY
	STY SPRITB+7    ;
	STY SPRITC+7    ;SPEARD=1

	LDA MSIGX
	AND MASKS,X     ;ENEMY1 X MSB ON?
	BEQ ENS020      ;NO
	LDA MSIGX
	ORA MASKS+7
	STA MSIGX       ;SET MISSLE X MSB
ENS020
	TXA
	ASL A
	TAY
	LDA SP0X,Y
	STA SP7X        ;SET MISSLE X POINT
	LDA SP0Y,Y
	STA SP7Y        ;SET MISSLE Y POINT
;
;----- MAKE MISSLE DIRECTION

	LDA #00
	STA WK1         ;MSB WORK
	STA WK4         ;X NEGATIV FLAG
	STA WK5         ;Y NEGATIV FLAG
	LDA MSIGX
	AND MASKS,X
	BEQ ENS030
	INC WK1         ;MSB ON
ENS030
	SEC
	LDA SP0X
	SBC SP0X,Y
	STA WK0
	LDA MSIGX
	AND #01
	SBC WK1
	BCS ENS040
	INC WK4         ;X NEGATIV FLAG ON
	LDA #$FF
	EOR WK0
	STA WK0
	LDA #$FF
	EOR WK1
ENS040
	STA WK1         ;WK1=X DISTANCE

	SEC
	LDA SP0Y
	SBC SP0Y,Y
	BCS ENS050
	INC WK5         ;Y NEGATIV FLAG ON
	LDA #$FF
	EOR WK2
ENS050
	STA WK2         ;WK2=Y DISTANCE

	LSR WK1
	ROR WK0         ;X/2

	LDY #00
ENS060
	CLC
	LDA WK2
	SBC WK0
	STA WK2
	BCC ENS070
	INY
	CPY #06
	BNE ENS060
ENS070
	LDA ENDRTB,Y
	STA WK6         ;DIRECTION NUMBER
	LDY WK4         ;X NEGATIV?
	BEQ ENS080      ;NO
	CLC
	LDA #00
	SBC WK6
	AND #$0F
	STA WK6
ENS080
	LDY WK5         ;Y NEGATIVE?
	BEQ ENS090      ;NO
	CLC
	LDA #08
	SBC WK6
	AND #$0F
ENS090
	STA SPRITD+7

	LDX #07
	LDY #$0E
	JMP MSLBGN

ENDRTB	.BYTE 04,05,06,06,07,07,08

.END
