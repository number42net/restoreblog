; 08/22/84 ds
;
;
;    * write job
;
;    write out data buffer
;
;
wright  cmp  #$10       ;  test if write
        beq  wrt05       
;
        jmp  vrfy        
;
wrt05   jsr  chkblk     ;  get block checksum
;
        lda  dskcnt     ;  test for write protect
        and  #16         
        bne  wrt10      ;  not  protected
;
        lda  #8         ;  write protect error
        jmp  errr        
;
wrt10   jsr  bingcr     ;  convert buffer to write image
;
        jsr  srch       ;  find header
;
        ldy  #gap1-2    ;  wait out header gap
;
wrt20   bit  dskcnt      
        bpl  wrt20       
        bit  bytclr      
;
        dey     ;  test if done yet
        bne  wrt20       
;
        lda  #$ff       ;  make output $ff
        sta  ddra2       
;
        lda  portc      ;  set write mode
        and  #$ff-$10   ;  0=wr
        sta  portc       
;
        lda  #$ff       ;  write 4 gcr sync
        ldy  #numsyn    ; 
        sta  data2       
        bit  bytclr      
;
wrtsnc  bit  dskcnt      
        bpl  wrtsnc      
        bit  bytclr      
        dey      
        bne  wrtsnc      
;
        ldy  #$bb       ;  write out overflow buffer
;
wrt30   lda  ovrflw,y   ;  get a char
wrt31   bit  dskcnt     ;  wait until ready
        bpl  wrt31       
        bit  bytclr      
;
        sta  data2      ;  stuff it
        iny      
        bne  wrt30      ;  do next char
;
; write rest of buffer
;
wrt40   lda  (bufpnt),y         ;  now do buffer
wrt41   bit  dskcnt     ;  wait until ready
        bpl  wrt41       
        bit  bytclr      
;
        sta  data2      ;  stuff it again
        iny      
; test if done
        bne  wrt40      ;  do the whole thing
;
wrt42   bit  dskcnt     ;  wait for last char to write out
        bpl  wrt42       
        bit  bytclr      
;
        lda  portc      ;  goto read mode
        ora  #$10        
        sta  portc       
;
        lda  #0         ;  make data2 input $00
        sta  ddra2       
;
        jsr  wtobin     ;  convert write image to binary
;
        ldy  jobn       ;  make job a verify
        lda  jobs,y      
        eor  #$30        
        sta  jobs,y      
;
        jmp  seak       ;  scan job que
;
;
chkblk  ldy  #0         ; index
        sty  chksum     ; zero checksum
;
chkb10  lda  (bufpnt),y          
        eor  chksum      
        sta  chksum      
        iny      
        bne  chkb10      
;
        rts     ;  return chechksum in .a
;
;
;    * wtobin
;
;    convert write image back to
;    binary data
;
;
wtobin  lda  #$60       ;  effective addr. of $011b=>lda $0060,bb
        sta  bufpnt     ;  lsb for overflow area
;
        lda  bufpnt+1   ; 
        sta  nxtbf      ;  save for next buffer.
;
        lda  #$00       ; 
        sta  bufpnt+1   ;  msb for overflow area
        sta  nxtpnt      
;
        lda  #$bb        
        sta  gcrpnt     ;  offset
        sta  bytcnt     ;  ditto
;
        jsr  get4gb     ;  get first four- id and 3 data
;
        lda  btab       ;  save bid
        sta  bid         
;
        ldy  bytcnt      
;
        lda  btab+1      
        sta  ovrflw,y   ;  store back in overflow buffer 5/4
        iny      
;
        lda  btab+2      
        sta  ovrflw,y    
        iny      
;
        lda  btab+3      
        sta  ovrflw,y    
        iny      
;
        sty  bytcnt      
;
; do overflow first and store back into overflow buffer
;
wtob14  jsr  get4gb     ; do rest of overflow buffer
;
        ldy  bytcnt      
;
        lda  btab        
        sta  ovrflw,y    
        iny      
;
        lda  btab+1      
        sta  ovrflw,y    
        iny      
        beq  wtob50      
;
        lda  btab+2      
        sta  ovrflw,y    
        iny      
;
        lda  btab+3      
        sta  ovrflw,y    
        iny      
;
        sty  bytcnt      
        bne  wtob14     ;  jmp till end of overflow buffer
;
;
;
;
wtob50           
;
        lda  btab+2      
        sta  (bufpnt),y          
        iny      
;
        lda  btab+3      
        sta  (bufpnt),y          
        iny      
;
        sty  bytcnt      
;
wtob53  jsr  get4gb      
;
        ldy  bytcnt      
;
        lda  btab        
        sta  (bufpnt),y          
        iny      
;
        lda  btab+1      
        sta  (bufpnt),y          
        iny      
;
        lda  btab+2      
        sta  (bufpnt),y          
        iny      
;
        lda  btab+3      
        sta  (bufpnt),y          
        iny      
;
        sty  bytcnt      
        cpy  #$bb        
        bcc  wtob53      
;
wtob52  lda  #$45       ;  move buffer up
        sta  savpnt      
;
        lda  bufpnt+1    
        sta  savpnt+1    
;
        ldy  #$ba        
;
wtob55  lda  (bufpnt),y          
        sta  (savpnt),y          
;
        dey      
        bne  wtob55      
;
        lda  (bufpnt),y          
        sta  (savpnt),y          
;
;load in overflow
;
        ldx  #$bb        
wtob57  lda  ovroff,y    
        sta  (bufpnt),y          
;
        iny      
        inx      
        bne  wtob57      
;
        stx  gcrflg     ;  clear buffer gcr flag
;
;
        rts      
;
;
;
;    * verify data block
;
;   convert to gcr verify image
;
;   test against data block
;
;   convert back to binary
;
;
vrfy    cmp  #$20       ;  test if verify
        beq  vrf10       
;
        jmp  sectsk      
;
vrf10            
;
        jsr  chkblk     ;  get block checksum
;
        jsr  bingcr     ;  convert to verify image
;
        jsr  dstrt       
;
        ldy  #$bb        
vrf15   lda  ovrflw,y   ;  get char
vrf16   bit  dskcnt      
        bpl  vrf16       
        bit  bytclr      
;
        eor  data2      ;  test if same
        bne  vrf20      ; verify error
;
        iny      
        bne  vrf15      ;  next byte
;
;
vrf30   lda  (bufpnt),y         ;  now do buffer
;
vrf31   bit  dskcnt      
        bpl  vrf31       
        bit  bytclr      
;
        eor  data2      ;  test if same
        bne  vrf20      ;  error
;
        iny      
        cpy  #$fd       ;  dont test off bytes
        bne  vrf30       
;
;
        jmp  done       ;  verify ok
;
vrf20   lda  #7         ;  verify error
        jmp  errr        
;
;
sectsk  jsr  srch       ;  sector seek
        jmp  done        
