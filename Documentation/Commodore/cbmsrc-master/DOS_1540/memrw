.PAGE 'MEM-RD,WRT
; MEMORY ACCESS COMMANDS
;  "-" MUST BE 2ND CHAR
MEM	LDA CMDBUF+1
	CMP #'-
	BNE MEMERR
;
	LDA CMDBUF+3    ;SET ADDRESS IN TEMP
	STA TEMP
	LDA CMDBUF+4
	STA TEMP+1
;
	LDY #0
	LDA CMDBUF+2
	CMP #'R
	BEQ MEMRD       ;READ
	JSR KILLP       ;KILL PROTECT
	CMP #'W
	BEQ MEMWRT      ;WRITE
	CMP #'E
	BNE MEMERR      ;ERROR
; EXECUTE
MEMEX	JMP (TEMP)
.SKIP
MEMRD
	LDA (TEMP),Y
	STA DATA
	LDA CMDSIZ
	CMP #6
	BCC M30
;
	LDX CMDBUF+5
	DEX
	BEQ M30
	TXA
	CLC
	ADC TEMP
	INC TEMP
	STA LSTCHR+ERRCHN
	LDA TEMP
	STA CB+2
	LDA TEMP+1
	STA CB+3
	JMP GE20
M30
	JSR FNDRCH
	JMP GE15
.SKIP
MEMERR	LDA #BADCMD     ;BAD COMMAND
	JMP CMDERR
.SKIP
MEMWRT	;WRITE
M10	LDA CMDBUF+6,Y
	STA (TEMP),Y    ;TRANSFER FROM CMDBUF
	INY
	CPY CMDBUF+5    ;# OF BYTES TO WRITE
	BCC M10
	RTS
.SKIP
.END
