.PAGE 'SER.TALK'
;
;
;
TALK	SEI             ; FIND IF OPEN CHANNEL
	JSR FNDRCH
	BCS NOTLK       ; NO ONE HOME
;
TALK1	LDX LINDX
	LDA CHNRDY,X
	BMI TLK05
;
NOTLK	RTS
;
;  CODE ADDED TO CORRECT VERIFY ERROR
TLK05	JSR TSTATN      ;TEST FOR ATN
	JSR DEBNC       ;CHECK FOR CLOCK GONE (MUST BE 80US+ FROM JMP TALK1)
	AND #DATIN
	PHP
	JSR CLKHI       ;SET CLOCK LINE HI
	PLP             ;SEE IF VERIFY ERROR...
	BEQ TLK02       ;YES...DATA LINE LOW, EOI !!!!
;
TALK2	JSR TSTATN      ; TEST ATN GONE
	JSR DEBNC       ; TEST DATA LINE NOW LOW
	AND #DATIN
	BNE TALK2       ; DID NOT RESPOND
;
;
	LDX LINDX       ; PREPARE TO SEND EOI IF NEEDED
	LDA CHNRDY,X
	AND #EOI
	BNE NOEOI       ; NO EOI
;
TLK02	JSR TSTATN
	JSR DEBNC       ; SEND AN EOI
	AND #DATIN      ; TEST IF DATA LINE IS LOW
	BNE TLK02       ; YES, WAIT TILL HI
;
TLK03	JSR TSTATN
	JSR DEBNC       ; WAIT FOR DATA LINE TO GO LOW
	AND #DATIN
	BEQ TLK03
;
NOEOI	JSR CLKLOW      ; SET CLOCK LOW
	JSR TSTATN
	JSR DEBNC       ; WAIT FOR DATA  LINE HI
	AND #DATIN
	BNE NOEOI
;
	LDA #8          ; SET UP BIT COUNTER
	STA CONT
;
ISR01	JSR DEBNC       ; LET PORT SETTLE
;
	AND #DATIN      ; TEST THAT DATA LINE IS NOW HIGH BEFORE WE SEND
	BNE FRMERX
;
ISR02	LDX LINDX       ; GET BYTE TO SEND
	LDA CHNDAT,X
	ROR A
	STA CHNDAT,X
;
	BCS ISRHI       ; SEND A 1
;
	JSR DATLOW      ; SEND A 0
	BNE ISRCLK      ; AND CLOCK IT
;
ISRHI	JSR DATHI
;
ISRCLK	JSR CLKHI       ; RISING EDGE CLOCK
;
	NOP
	NOP             ; SETTLE TIME
	NOP
	NOP
;
	JSR CLKLOW      ; PULL CLOCK LOW
	JSR DATHI       ; RELEASE DATA
;
	DEC CONT        ; MORE BITS?
	BNE ISR01       ; YES
;
ISR04	JSR TSTATN
	JSR DEBNC
	AND #DATIN
	BEQ ISR04
;
	CLI             ; GET NEXT BYTE TO SEND
	JSR GET
	SEI
;
	JMP TALK1       ; KEEP ON TALKIN
;
FRMERX	JMP FRMERR
;
;
DATHI	LDA PB          ; SET DATA OUT HI
	AND #$FF-DATOUT
	STA PB
	RTS
;
;
;
DATLOW	LDA PB
	ORA #DATOUT
	STA PB
	RTS
;
;
;
CLKLOW	LDA PB
	ORA #CLKOUT
	STA PB
	RTS
;
;
;
CLKHI	LDA PB
	AND #$FF-CLKOUT
	STA PB
	RTS
;
;
;
DEBNC	LDA PB
	CMP PB
	BNE DEBNC
	RTS
;
.END
; ADDITIONS AFTER CODE SENT TO JAPAN.
; RSR 3/30/81 ADD VERIFY ERROR CORRECTION
; RSR 3/31/81 CHANGE ORDERING TO DETECT VERIFY ERROR
