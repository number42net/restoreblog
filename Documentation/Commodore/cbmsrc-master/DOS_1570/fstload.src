        .page  
	.subttl 'fstload.src'

fstload jsr  spout	 ; output
	lda  fastsr	 ; set flag
	ora  #1
	sta  fastsr	
	jsr  set_fil	 ; setup filename for parser
	bcs  6$

	lda  cmdbuf
	cmp  #'*	 ; load last ?
	bne  7$
	
	lda  prgtrk	 ; any file ?
	bne  1$

7$	lda  #0          
	tay
	tax		 ; clear .x, .y
        sta  lstdrv      ; init drive number
	sta  filtbl	 ; set up for file name parser
        jsr  autoi	 ; init mechanism
	lda  nodrv	 ; chk status
	bne  6$		 ; no drive status

        jsr  onedrv      ; select drive
	lda  f2cnt
	pha	
	lda  #1
	sta  f2cnt
	lda  #$ff
	sta  r0		 ; set flag
	jsr  lookup	 ; locate file
	pla		 
	sta  f2cnt	 ; restore var
	bit  switch	 ; seq flag set ?
	bmi  8$

 	lda  pattyp	 ; is it a program file ?
	cmp  #2
	bne  6$		 ; not prg

8$      lda  filtrk      ; check if found. err if not
        bne  1$

6$    	ldx  #%00000010	 ; file not found
        jmp  fld_err

1$      sta  hdrs	 ; setup track
        lda  filsec      
        sta  hdrs+1

	lda  #read
	sta  cmdbuf+2	 ; save read cmd
	sta  ctl_cmd	

2$	cli		 ; let controller run
	ldx  #0
	lda  ctl_cmd	 ; get cmd
	sta  jobs,x
	jsr  stbctt	 ; whack the controller in the head
	cpx  #2
	bcc  5$
	
	jmp  fld_err

5$	sei
	lda  bufs	 ; check status
	beq  end_of_file	

	lda  fastsr	 ; clear flag
	and  #$fe
	sta  fastsr

	stx  ctl_dat	 ; send status
	jsr  hskrd

	ldy  #2
3$	lda  bufs,y
	sta  ctl_dat	 ; save data
	jsr  hskrd	 ; send data to the host
	iny
	bne  3$

	lda  bufs
	cmp  hdrs	 ; same as previous track ?
	beq  4$

	ldy  #read
	.byte skip2
4$	ldy  #fread	 ; fast read
	sty  ctl_cmd	 ; command to seek then read

	sta  hdrs	 ; next track
	lda  bufs+1	
	sta  hdrs+1	 ; next sector	
	jmp  2$

end_of_file
	ldy  #2	         ; goto data usually
	ldx  #$1f	 ; eof
	stx  ctl_dat	 ; send status
	jsr  hskrd

	lda  #1
	bit  fastsr	 ; first time through ?
	beq  1$

	lda  bufs+1	 ; number of bytes
	sec
	sbc  #3
	sta  ctl_dat	 ; send it
	pha		 ; save it
	jsr  hskrd

	ldx  bufs+2	 ; address low
	stx  ctl_dat	 ; send status
	jsr  hskrd

	ldx  bufs+3	 ; address high
	stx  ctl_dat	 ; send status
	jsr  hskrd
	pla
	tax		 ; # of bytes
	ldy  #4		 ; skip addresses
	bne  3$		 ; bra

1$	ldx  bufs+1	 ; number of bytes
	dex
	stx  ctl_dat	 ; send it
	jsr  hskrd
	
3$	lda  bufs,y
	sta  ctl_dat	 ; save data
	jsr  hskrd	 ; send data to the host
	iny
	dex
	bne  3$
	
	jmp  endcmd

fld_err	stx  ctl_dat	 ; send error
	jsr  hskrd
	jmp  exbad


set_fil ldy  #3		 ; default .y
	lda  cmdsiz	 ; delete burst load command
	sec
	sbc  #3
	sta  cmdsiz	 ; new command size

	lda  cmdbuf+4    ; drv # given ?
	cmp  #':
	bne  1$

	lda  cmdbuf+3
	tax		 ; save 
	and  #'0
	cmp  #'0         ; 0:file ?
	bne  1$

	cpx  #'1	 ; chk for error
	beq  4$			

	cpx  #'0	 ; chk for h:jsjsj ... etc
	bne  1$			

	dec  cmdsiz	 ; adjust ...
	iny
	dec  cmdsiz
	iny

1$	lda  cmdbuf+3    ; drv # given ?
	cmp  #':
	bne  2$

	dec  cmdsiz
	iny

2$	ldx  #0		 ; start at cmdbuf+0
3$      lda  cmdbuf,y	 ; extract file-name
	sta  cmdbuf,x
	iny
	inx
	cpx  cmdsiz	 ; done ?
	bne  3$		 ; delete cmd from buffer

	clc
	.byte skip1
4$	sec		 ; error
	rts
