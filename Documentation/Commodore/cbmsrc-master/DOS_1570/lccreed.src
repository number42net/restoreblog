        .page  
        .subttl 'lccreed'

;=============================================================
;=     8    =     8     =     8     =     8     =     8      =
;=============================================================
;   (5) (3  + 2)  (5) (1+  4)   (4  +1) (5)  (2 +  3)   (5)
;    a      b      c    d           e    f      g        h
;=============================================================

;    read in track,sector specified
;    in header


gcrfly  jsr  dstrt      ;  find header and start reading data
			; sync routine sets y to 0

1$	bit  pb23_a	;  4
	bmi  1$		;  3 + 2

        lda  data2      ;  4
        tax     	;  2   reg x = xxxxx000
        lda  gcrtb1,x   ;  4   nibble a
        sta  gtemp      ;  3
        txa		;  2
        and  #%00000111 ;  2
        sta  gtemp1     ;  3   extract 3 bits nibble b

2$	bit  pb23_a	;  4
	bmi  2$		;  3 + 2

        lda  data2      ;  4
        sta  gtemp2     ;  3
        and  #%11000000 ;  2   extract 2 bits nibble b
        ora  gtemp1     ;  3
        tax     	;  2   reg x = xx000xxx
        lda  gcrtba,x   ;  4   nibble b
        ora  gtemp      ;  3
        pha             ;  3
        jmp  5$		;  3   

;********************************************************************

3$	bit  pb23_a	;  4
	bmi  3$		;  3 + 2

        lda  data2      ;  4
        tax     	;  2   reg x = xxxxx000
        lda  gcrtb1,x   ;  4   nibble a
        sta  gtemp      ;  3
	txa		;  2
        and  #%00000111 ;  2
        sta  gtemp1     ;  3   extract 3 bits nibble b

4$	bit  pb23_a	;  4
	bmi  4$		;  3 + 2

	lda  data2      ;  4
        sta  gtemp2     ;  3
        and  #%11000000 ;  2
        ora  gtemp1     ;  3
        tax             ;  2   reg x = xx000xxx
        lda  gcrtba,x   ;  4   nibble b
        ora  gtemp      ;  3
        sta  (bufpnt),y ;  6
        iny     	;  2
        beq  9$		;  2

5$	lda  gtemp2     ;  3
        tax     	;  2   reg x = 00xxxxx0
        lda  gcrtb2,x   ;  4   nibble c
        sta  gtemp      ;  3
	txa		;  2
        and  #%00000001 ;  2
        sta  gtemp2     ;  3   extract 1 bits nibble d

6$	bit  pb23_a	;  4
	bmi  6$		;  3 + 2

        lda  data2      ;  4
        sta  gtemp3     ;  3
        and  #%11110000 ;  2
        ora  gtemp2     ;  3
        tax     	;  2   reg x = xxxx000x
        lda  gcrtbd,x   ;  4   nibble d
        ora  gtemp      ;  3
        sta  (bufpnt),y ;  6
        iny     	;  2
        lda  gtemp3     ;  3
        and  #%00001111 ;  2
        sta  gtemp3     ;  3   extract 4 bits nibble e

7$	bit  pb23_a	;  4
	bmi  7$		;  3 + 2

        lda  data2      ;  4
        sta  gtemp4     ;  3
        and  #%10000000 ;  2
        ora  gtemp3     ;  3
        tax     	;  2   reg x = x000xxxx
        lda  gcrtbe,x   ;  4   nibble e
        sta  gtemp      ;  3
        lda  gtemp4     ;  3
        tax     	;  2   reg x = 0xxxxx00
        lda  gcrtb3,x   ;  4   nibble f
        ora  gtemp      ;  3
        sta  (bufpnt),y ;  6
        iny     	;  2
	txa		;  2
        and  #%00000011 ;  2
        sta  gtemp4     ;  3   extract 2 bits nibble g

8$	bit  pb23_a	;  4
	bmi  8$		;  3 + 2

        lda  data2      ;  4
        sta  gtemp1     ;  3
        and  #%11100000 ;  2
        ora  gtemp4     ;  3
        tax     	;  2   reg x = xxx000xx
        lda  gcrtbg,x   ;  4   nibble g
        sta  gtemp      ;  3
        lda  gtemp1     ;  3
        tax     	;  2   reg x = 000xxxxx
        lda  gcrtb4,x   ;  4   nibble h
        ora  gtemp      ;  3
        sta  (bufpnt),y ;  6
        iny     	;  2
        jmp  gcrfly	;  4

;*******************************************************************

9$	lda  gtemp2     ;  3
        tax     	;  2   reg x = 00xxxxx0
        lda  gcrtb2,x   ;  4   nibble c
        sta  gtemp      ;  3
	txa		;  2
        and  #%00000001 ;  2
        sta  gtemp2     ;  3

10$	bit  pb23_a	;  4
	bmi  10$	;  3 + 2

        lda  data2      ;  4
        and  #%11110000 ;  2
        ora  gtemp2     ;  3
        tax     	;  2   reg x = xxxx000x
        lda  gcrtbd,x   ;  4   nibble d
        ora  gtemp      ;  3
        sta  gtemp1     ;  3   store off cs byte

        pla     	; retrieve first byte off of disk
        cmp  dbid       ; see if it is a 7
        bne  notblk     ; br, nope


	jsr  chkblk     ; calc checksum
        cmp  gtemp1      
        beq  read40      

        lda  #5         ; data block checksum error
        .byte    skip2   
notblk  lda  #4          
        .byte    skip2   

read40  lda  #1         ; read data block ok
        jmp  errr        
;.end
