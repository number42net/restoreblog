;*************************************
;
;  SET UP FOR NEW DIRECTORY LISTING
;
;*************************************
NEWDIR
	JSR BLKNB       ;BLANK NAME BUFR
	LDA #$FF
	STA TEMP
	LDX DRVNUM
	STX NBTEMP      ;DISPLAY DRIVE #
	LDA #0          ;*
	STA NBTEMP+1    ;*
	JSR HDISU       ;SET UP IND WORD
	JSR HDWBM       ;WRITE OUT OLD BAM
	JSR HDORV       ;READ IN VOL LBL
	LDY #HDDVID     ;DOS VERSION #
	LDA (HDBMP),Y
	STA NAMBUF+24
	LDA #$20        ;SPACE
	STA NAMBUF+20
	STA NAMBUF+23
	LDY #HDUVID     ;USER VOL ID
	LDA (HDBMP),Y   ;*
	STA NAMBUF+21
	INY
	LDA (HDBMP),Y   ;GET 2ND CHAR ID
	STA NAMBUF+22
	LDA #'"         ;DOUBLE QUOTES
	STA NAMBUF+19
	STA NAMBUF+1
	LDX #15         ;GET RDY TO MOVE VOL NAME
	LDY #HDUNAM+15  ;RIGHT TO LEFT
NED1
	LDA (HDBMP),Y   ;GET FROM NAME
	AND #$7F        ;NO BIT 8
	STA NAMBUF+2,X  ;STORE
	DEX
	DEY
	CPY #HDUNAM-1   ;DONE?
	BNE NED1        ;BR IF NOT
	LDA #$12        ;REVERSE VIDEO
	STA NAMBUF
	LDY #HDZONE     ;FORCE NEW BAM READ
	LDA #$7F        ;*
	STA (HDBMP),Y   ;*
	RTS
;
;
;
.END
