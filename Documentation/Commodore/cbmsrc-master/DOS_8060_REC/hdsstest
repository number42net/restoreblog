;*********************************
;* SSTST: TEST GRPNUM SSNUM & SSIND FOR
;*        RESIDENCE & RANGE.     *
;*   VARS:  GRPNUM SSNUM,SSIND,DIRBUF
;*                               *
;*   FLAGS: .N RNG  .V RES  ER   *
;*           0  OK   0 YES  ER0  *
;*           0 MAYBE 1 NO   ER1  *
;*           1 NO    0 YES  ER2  *
;*           1 NO    1 NO   ER3  *
;*********************************
;
SSTEST
;
; <BIG FILES CHANGE>
;
	LDA GRPNUM      ;GROUP NUMBER
	JSR RDSS1       ;BE SURE RIGHT GROUP
	BEQ ST50        ;BR IF LOADED
	BIT ER3         ;SET WAY OUT OF RANGE
	RTS             ;EXIT
ST50
;
	JSR SSSET       ;SET POINTER & SS #
	CMP SSNUM
	BNE ST20        ;NOT THIS SS
;
	LDY SSIND
	INY             ;<HD>
	LDA (DIRBUF)Y
	CMP #255        ;<HD>
	BEQ ST10
	DEY             ;<HD>
	BIT ER0         ;OK, RESIDENT
	RTS
ST10
	DEY             ;<HD>
	BIT ER2         ;OUT OF RANGE, RES
	RTS
ST20
	LDA SSNUM
	CMP #NSSL
	BCS ST30
	ASL A
	TAY
	LDA #4
	STA DIRBUF
	INY             ;<HD>
	LDA (DIRBUF)Y
	CMP #255        ;<HD>
	BNE ST40
ST30
	DEY             ;<HD>
	BIT ER3         ;WAY OUT OF RANGE
	RTS
ST40
	DEY             ;<HD>
	BIT ER1         ;NOT RES, RANGE???
	RTS
.END
