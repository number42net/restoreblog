;***************************************
;***************************************
;
;   COMPRESS DIRECTORY
;   CALLED DURING VERIFY
;
;***************************************
COMPRS
	LDX DRVNUM      ;GET THE DRIVE #
	LDA #0          ;CLEAR FLAG
	STA HDDFLG,X    ;*
	JSR HDLDTS      ;GET T&S OF DIR
	JSR OPNIRD      ;OPEN INTERNAL RD CHNL
	LDA LINDX       ;SAVE LINDX
	STA TEMP+4      ;*
	JSR OPNIWR      ;OPEN INTERNAL WRITE CHNL
	LDA LINDX       ;SAVE WRITE CHNL
	STA TEMP+5      ;*
	LDA SECTOR      ;SAVE NEW DIR SEC & TRK
	STA NDSEC       ;*
	LDA TRACK       ;*
	STA NDTRK       ;*
;
;  GET DIRECTORY FLAG
;
CMPRA
	LDA TEMP+4      ;INPUT LINDX
	STA LINDX
	LDA #LRF        ;EOF
	JSR TSTFLG      ;CHK IF EOF
	BNE CMPR5       ;BR IF EOF
	JSR GETPRE      ;SET UP
	LDA BUFTAB,X    ;CHK IF AT BEG OF SECTOR
	CMP #2          ;*
	BEQ CMPR1       ;BR IF SO
	INC BUFTAB,X    ;SKIP FIRST TWO BYTES
	INC BUFTAB,X    ;SKIP FIRST TWO BYTES
CMPR1
	JSR GIBYTE      ;GET NEXT BYTE
	LDA DATA        ;LOOK AT IT
	BEQ CMPR2       ;BR IF DELETED ENTRY
;
; COPY THIS DIRECTORY ENTRY TO NEW DIR
;
	LDA TEMP+5      ;OUTPUT LINDX
	STA LINDX       ;*
	JSR GETPRE      ;SETUP
	LDA BUFTAB,X    ;CHK IF AT BEG OF SECTOR
	CMP #2          ;*
	BEQ CMPR3A      ;BR IF AT BEG OF SECT
	INC BUFTAB,X    ;SKIP T&S
	INC BUFTAB,X    ;*
CMPR3A
	LDA #30         ;COPY 30 BYTES
	PHA             ;*
CMPR3
	LDA TEMP+5      ;OUTPUT LINDX
	STA LINDX
	LDA DATA        ;DATA BYTE
	JSR WRTBYT      ;PUT IT
	PLA             ;DECR # BYTES TO COPY
	SEC             ;*
	SBC #1          ;*
	BEQ CMPRA       ;BR IF DONE
	PHA             ;SAVE COUNT TO GO
	JSR GIBYTE      ;GET NEXT BYTE
	JMP CMPR3       ;LOOP
;
;  DELETED DIRECTORY ENTRY SO SKIP IT
;
CMPR2
	LDA #29         ;SKIP THIRTY BYTES
CMPR4
	PHA             ;SAVE # TO SKIP
	JSR GIBYTE      ;GET & TOSS BYTE
	PLA             ;RESTORE # TO SKIP
	SEC             ;DEC BY 1
	SBC #1
	BNE CMPR4       ;LOOP TILL DONE
	BEQ CMPRA       ;BRA
;
;  EOR ON INPUT SO WRAP UP
;
CMPR5
	LDA TEMP+5      ;OUTPUT LINDX
	STA LINDX
	LDA #0          ;NULL REST OF DIR BUF
CMPR6
	JSR PUTBYT      ;PUT IN BUFR
	BNE CMPR6       ;LOOP TILL BUFR FULL
	LDA #255        ;SET LINK = FFFF
	STA (BUFTAB,X)  ;*
	INC BUFTAB,X
	STA (BUFTAB,X)
	JSR WRTBUF      ;WRITE LAST BUFR
	JSR DBLBUF      ;SWAP & WAIT
	JSR DBLBUF      ;SWAP & WAIT
	JSR HDLDTS      ;GET DIR T&S ADDR
	JSR DELFIL      ;DELETE OLD DIR SECTORS
	LDX DRVNUM
	JSR MAPOU1      ;WRITE OUT THE BAM
	JMP FREICH      ;FREE INTERNAL CHNLS
.END
