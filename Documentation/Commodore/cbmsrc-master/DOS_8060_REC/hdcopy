; COPY FILE(S) TO ONE FILE
;
COPY	;FILENAMES, OPTIMIZE
	JSR LOOKUP      ;LOOK IP ALL FILES
	LDA F2CNT
	CMP #3
	BCC COP10
	LDA FILDRV
	CMP FILDRV+1
	BNE COP10
;
	LDA ENTIND
	CMP ENTIND+1
	BNE COP10
	LDA ENTSEC
	CMP ENTSEC+1
	BNE COP10
	LDA ENTTRK      ;<HD>
	CMP ENTTRK+1    ;<HD>
	BNE COP10       ;<HD>
;
	JSR CHKIN       ;CONCAT
	LDA #1
	STA F2PTR
	JSR OPIRFL
	JSR TYPFIL
	BEQ COP01
	CMP #PRGTYP
	BNE COP05
COP01
	LDA #MISTYP
	JSR CMDERR
COP05
	LDA #IWSA
	STA SA
	LDA LINTAB+IRSA
	STA LINTAB+IWSA
	LDA #$FF
	STA LINTAB+IRSA
	JSR APPEND
	LDX #2
	JSR CY10
	JMP ENDCMD
COP10
	JSR CY
	JMP ENDCMD
;
;
CY
	JSR CHKIO       ;CHECK FILES FOR EXISTENCE
.SKIP
	LDA FILDRV
	AND #1
	STA DRVNUM
	JSR OPNIWR      ;OPEN INTERNAL WRITE CHNL
	JSR ADDFIL      ;ADD TO DIRECTORY
	LDX F1CNT
.SKIP
CY10	STX F2PTR       ;SET UP READ FILE
	JSR OPIRFL
	LDA #IRSA       ;POINT TO INPUT FILE
	STA SA          ;*
	JSR FNDRCH      ;*
	JSR TYPFIL      ;CHK IF RELATIVE FILE
	BNE CY10A       ;BR IF NOT REL FILE
	JSR CYEXT       ;EXTEND DESTINATION FILE
CY10A
	LDA #EOISND
	STA EOIFLG
	JMP CY20
CY15
	JSR PIBYTE
CY20
	JSR GIBYTE
	LDA #LRF
	JSR TSTFLG
	BEQ CY15
	JSR TYPFIL
	BEQ CY30
	JSR PIBYTE
CY30
	LDX F2PTR
;
	INX
	CPX F2CNT
	BCC CY10        ;MORE FILES TO COPY
	LDA #IWSA
	STA SA
	JMP CLSCHN      ;CLOSE COPY CHANNEL, FILE
;
OPIRFL
	LDX F2PTR
	LDA FILDRV,X
	AND #1
	STA DRVNUM
	LDA ENTTRK,X    ;GET TRACK <HD>
	STA TRACK
	LDA ENTSEC,X
	STA SECTOR
	JSR OPNIRD
	LDX F2PTR
	LDA ENTIND,X
	JSR SETPNT
	LDX F2PTR
	LDA PATTYP,X
	AND #TYPMSK
	STA TYPE
;
	LDA #0
	STA REC
	JSR OPREAD
	LDY #1
	JSR TYPFIL
	BEQ OPIR10
	INY
OPIR10
	TYA
	JMP SETPNT
;
GIBYTE
	LDA #IRSA
	STA SA
GCBYTE
	JSR GBYTE
;
	STA DATA
	LDX LINDX
	LDA CHNRDY,X
	AND #EOISND
	STA EOIFLG
	BNE GIB20
;
	JSR TYPFIL
	BEQ GIB20
	LDA #LRF
	JSR SETFLG
GIB20
	RTS
;
;  EXTEND THE SIZE OF THE DESTINATION FILE
;  TO BE AS LARGE AS THE SOURCE FILE
;
CYEXT
	JSR SETDRN      ;SET DRIVE #
	JSR SSEND       ;FIND END OF SOURCE FILE
	LDA GRPNUM      ;SAVE THE END GRP
	PHA             ;*
	LDA SSIND       ;SAVE THE END DATA SECT #
	PHA             ;*
	LDA SSNUM       ;SAVE THE END SS #
	PHA             ;*
	LDA #IWSA       ;POINT TO THE DESTINATION FILE
	STA SA          ;*
	JSR FNDWCH      ;*
	JSR ADRELS      ;DO ADDREL SET UP
	STA RELPTR      ;ZERO REL PTR
	PLA             ;GET END SS #
	STA SSNUM       ;*
	PLA             ;GET END DATA SECTOR
	STA SSIND       ;*
	PLA             ;GET END GROUP
	STA GRPNUM      ;*
	JMP ADDR1       ;EXTEND THE DEST FILE & EXIT
.END
