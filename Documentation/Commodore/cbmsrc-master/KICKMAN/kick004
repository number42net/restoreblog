.PAGE 'KICK004'
;
;*------------------------------------------------------*
;* KICK CHECK BY MAN                                    *
;*------------------------------------------------------*

KICKCK
	LDA SPRITA
	BMI KIK010
	LDA KIKTIM
	BEQ KIK010
	LDA COLLSS
	AND #$02
	BNE KIK020
KIK010
	RTS

KIK020
	LDA SP1X
	STA WK0
	LDA #$00
	STA WK1
	LDA MSIGX
	AND #$02
	BEQ *+4
	INC WK1
	LDA SP1Y
	STA WK2

	LDX #$07
	STX SPRIDX
KIK030
	LDA COLLSS
	AND MASKS,X
	BNE KIK050
KIK040
	DEC SPRIDX
	LDX SPRIDX
	CPX #$01
	BNE KIK030
	RTS
KIK050
	LDA SPRITA,X
	AND #$F0
	CMP #$20
	BNE KIK040

	TXA
	ASL A
	TAY
	LDA SP0X,Y
	STA WK3
	LDA #$00
	STA WK4
	LDA MSIGX
	AND MASKS,X
	BEQ *+4
	INC WK4
	LDA SP0Y,Y
	STA WK5

	LDA #10         ;Y1+12 < Y2+2
	STA WK6
	LDA #11         ;Y1+2 > Y2+13
	STA WK7
	LDA #46         ;X1+48 < X2+2
	STA WK8
	LDA #11+46      ;X1+2 > X2+13
	STA WK9
	JSR CKCOLL
	BCC KIK040

	LDA WK0
	STA WK6
	LDA WK1
	LSR A
	ROR WK6
	LSR WK6
	CLC
	LDA WK6
	ADC #06
	STA WK6

	LDA WK4
	LSR A
	LDA WK3
	ROR A
	LSR A

	LDY #$0F
	CMP WK6
	BCC *+4
	LDY #$01
	STY SPRITD,X

KIC060
	LDA #$04        ;KICK SOUND
	JSR SOUND
	JMP KIK040


;*------------------------------------------------------*
;* FUUSEN CHECK ON HEAD                                 *
;*------------------------------------------------------*

HEADCK
	LDA SPRITA
	BPL HCK010
	RTS
HCK010
	LDX #$00
HCK020
	LDY HEADTB,X
	BMI HCK030
	LDA SPRITA,Y
	BPL HCK040
HCK030
	INX
	BNE HCK020      ;JMP
HCK040
	LDA COLLSS
	AND MASKS,Y
	BNE HCK050
	RTS

HCK050
	STY SPRIDX
	LDX SPRIDX
	TYA
	ASL A
	TAY
	LDA SP0X,Y
	STA WK0
	LDA #00
	STA WK1
	LDA MSIGX
	AND MASKS,X
	BEQ *+4
	INC WK1
	LDA SP0Y,Y
	STA WK2

	LDX #$07
HCK060
	LDA SPRITA,X
	AND #$F0
	CMP #$20
	BEQ HCK080
HCK070
	DEX
	CPX #$01
	BNE HCK060
	RTS

HCK080
	LDA SPRITD,X
	CMP #$05
	BCC HCK070
	CMP #$0C
	BCS HCK070

	TXA
	ASL A
	TAY
	LDA SP0X,Y
	STA WK3
	LDA #00
	STA WK4
	LDA MSIGX
	AND MASKS,X
	BEQ *+4
	INC WK4
	LDA SP0Y,Y
	STA WK5

	LDA #06
	STA WK6
	LDA #11
	STA WK7
	LDA #18
	STA WK8
	LDA #11+18
	STA WK9
	JSR CKCOLL
	BCC HCK070

	LDA SPRITA,X
	AND #$0F
	JSR SCORUP

	LDA ROUND
	CMP #01
	BNE HCK090
	LDA #$80
	STA SPRITA,X    ;BOMB START
	LDA #$00
	STA SPRITB,X    ;BOMB TIMER
	DEC NUMENM
HCK082
	RTS

HCK090
	LDA SPRITA,X
	AND #$0C
	CMP #$04        ;PACMAN?
	BNE HCK100
	LDY SPRIDX      ;MAN HEAD?
	BEQ HCK100      ;YES
	LDA SPRITA,Y
	AND #$0C        ;UNDER PACMAN?
	CMP #$04
	BEQ HCK100      ;YES
	LDA #$80
	STA SPRITA,Y
	LDA #$00
	STA SPRITB,Y
	TYA
	TAX
	JSR OFHDON      ;DEL HEAD TABLE

	LDA #$07        ;PACMAN SOUND
	JSR SOUND
	RTS

HCK100
	JSR STHDON      ;SET HEAD TABLE
	LDA SPRITA,X
	AND #$0F
	STA SPRITA,X

	JSR AJSTXY
	DEC NUMENM

	LDA #$08        ;HEAD ON SOUND
	JSR SOUND
	RTS
;
;*-----  AJUST HEAD ON FUUSEN

AJSTXY
	LDA SPRIDX
	ASL A
	TAY
	LDA SP0X,Y
	STA WK0
	LDA SP0Y,Y
	STA WK1
	LDY SPRIDX
	LDA MSIGX
	LDY SPRIDX
	AND MASKS,Y
	BEQ AJS010
	LDA MSIGX
	ORA MASKS,X
	BNE AJS020
AJS010
	LDA MSIGX
	AND MASKZ,X
AJS020
	STA MSIGX
	TXA
	ASL A
	TAY
	LDA WK0
	STA SP0X,Y
	SEC
	LDA WK1
	SBC #16
	STA SP0Y,Y
	RTS
;
;----- SET ON HEAD TABLE, X=SPR-NO.

STHDON
	LDY #06
SHD010
	LDA HEADTB,Y
	BMI SHD020
	DEY
	BPL SHD010
SHD020
	STX HEADTB,Y
	RTS
;
;----- OFF SET HEAD TABLE, X=SPR-NO.

OFHDON
	LDY #$06
FHD010
	LDA HEADTB,Y
	STA WK0
	CPX WK0
	BEQ FHD020
	DEY
	BPL FHD010
	RTS

FHD020
	DEY
	BMI FHD090
	LDA HEADTB,Y
	BMI FHD080
	STA HEADTB+1,Y
	BPL FHD020
FHD080
	STA HEADTB+1,Y
FHD090
	RTS

;*----------------------------------------------*
;* PACK MAN EAT UPPER                           *
;*----------------------------------------------*

PACPAC
	LDA HEADTB+1
	BPL PAC010
	LDA PACFLG
	BNE PAC010
	RTS
PAC010
	LDA #$FF
	STA KILLSP
	STA PACFLG

	LDY DWNCNT
	BEQ PAC020

	LDX PACIDX
	BMI PAC019
	LDA TIMER1
	AND #$07
	BNE PAC019
	LDY #$F6
	LDA TIMER1
	AND #$08
	BNE *+4
	LDY #$F9
	TYA
	STA SV07F8,X
	STA $07F8,X
PAC019
	RTS

PAC020
	LDA #$FF
	STA PACIDX
	LDX #06
PAC030
	DEX
	LDY HEADTB,X
	BMI PAC040
	LDA SPRITA,Y
	AND #$0C
	CMP #$04        ;PAC MAN?
	BNE PAC030
	STY DWNSTP
	STY PACIDX
	LDY HEADTB-1,X
	BMI PAC040
	LDA SPRITA,Y
	AND #$0C
	CMP #$04
	BEQ PAC030
	LDA #$80
	STA SPRITA,Y
	LDA #$00
	STA SPRITB,Y

	TYA
	TAX
	JSR OFHDON
	LDA #16
	STA DWNCNT

	LDA #$07        ;PACMAN SOUND
	JSR SOUND
	RTS

PAC040
	LDX HEADTB+1
	BMI PAC050
	LDA #$80
	STA SPRITA,X
	LDA #$00
	STA SPRITB,X
	JSR OFHDON
PAC050
	LDA #$00
	STA KILLSP
	STA PACFLG
	RTS


;*--------------------------------------------------*
;*  COLLISION CHECK                                 *
;*   INPUT:   WK0 SOURCE      X POINT L    (X1)     *
;*            WK1             X POINT H             *
;*            WK2             Y POINT      (Y1)     *
;*            WK3 DESTINATION X POINT L    (X2)     *
;*            WK4             X POINT H             *
;*            WK5             Y POINT      (Y2)     *
;*            WK6=J, J=N-M, (Y1)+M < (Y2)+N ? ,N<M  *
;*            WK7=K, K=P-Q, (Y1)+Q >=(Y2)+P ? ,P>Q  *
;*            WK8=R, R=S-T, (X1)+T < (X2)+S ? ,T<S  *
;*            WK9=Z, U=V-W, (X1)+W >=(X2)+V ? ,V>W  *
;*                   Z=R+U                          *
;*--------------------------------------------------*

CKCOLL
;
;-----  CHECK Y COLLISION

	SEC             ;WK5-WK6 >= WK2?
	LDA WK5
	SBC WK6
	CMP WK2
	BCS CKCL80      ;NONE COLLISION

	CLC             ;WK5+WK7 < WK2?
	LDA WK5
	ADC WK7
	CMP WK2
	BCC CKCL80      ;NONE COLLISION
;
;-----  CHECK X COLLISION

	SEC             ;(WK3,WK4)-WK8 >= (WK0,WK1)?
	LDA WK3
	SBC WK8
	STA WK3
	LDA WK4
	SBC #00
	STA WK4
	SEC
	LDA WK3
	SBC WK0
	LDA WK4
	SBC WK1
	BCS CKCL80      ;NONE COLLISION

	LDA WK3         ;(WK3,WK4)+WK9 < (WK0,WK1)?
	ADC WK9
	STA WK3
	LDA WK4
	ADC #00
	STA WK4

	SEC
	LDA WK3
	SBC WK0
	LDA WK4
	SBC WK1
	BCC CKCL80      ;NONE COLLISION
	RTS             ;COLLISION EXIT

CKCL80
	CLC
	RTS             ;NONE COLLISION EXIT


;*--------------------------------------------------*
;*  SCORE UPDATE                                    *
;*--------------------------------------------------*

SCORUP
	STX WORK        ;SAVE X-REG
	STA WORK+1

SCR010
	CLC
	SED
	LDX WORK+1
	LDA SCORE+0
	ADC SCTBL0,X
	STA SCORE+0
	LDA SCORE+1
	ADC SCTBL1,X
	STA SCORE+1
	LDA SCORE+2
	ADC #$00
	STA SCORE+2
	CLD
	JSR DSPSCR      ;SCORE DISPLAY

	LDA XTRA        ;CHECK FOR EXTRA SHIP
	BNE SCR090      ;ALREADY
	LDA SCORE+2
	CMP #$04
	BCC SCR090

	LDA #01
	STA XTRA
	INC NUMSHP
	JSR DSPSHP
	LDA #$06        ;BOUNUS SOUND
	JSR SOUND
SCR090
	LDX WORK
	RTS

SCTBL0

	.BYTE $50,$00,$50,$00,$50,$00,$50,$00
	.BYTE $50,$00,$50,$00,$50,$00,$50,$00
SCTBL1
	.BYTE $00,$01,$01,$02,$02,$03,$03,$04
	.BYTE $04,$05,$05,$06,$06,$07,$07,$08,$10

;*--------------------------------------------------*
;* BOMB DISPLAY BY SPRITE                           *
;*--------------------------------------------------*

BMBSPR
	LDX #07
BMB005
	LDA SPRITA,X
	BMI BMB020
BMB010
	DEX
	BPL BMB005
	RTS

BMB020
	CMP #$FF        ;NONE USED ?
	BEQ BMB010      ;YES

	INC SPRITB,X
	TXA             ;MAN?
	BEQ *+5
	JMP BMB100      ;NO

	LDA SPRITB
	CMP #$01
	BNE BMB030
	LDA SPENA
	AND #$FD        ;OFF SP1
	STA SPENA
	LDA #$F5        ;DOWN PTN-NO.
	STA SV07F8+0

	LDA #$03        ;DOWN SOUND
	JSR SOUND

	LDY #$0A
	LDA MSIGX
	AND #$01
	BNE BMB024
	LDA SP0X
	CMP #162
	BCS BMB024
	LDY #06
BMB024
	STY SPRITD+2
	STY SPRITD+3
	STY SPRITD+4
	STY SPRITD+5
	STY SPRITD+6
	STY SPRITD+7

	LDY #$01
	STY SPRITC+2
	STY SPRITC+3
	STY SPRITC+4
	STY SPRITC+5
	STY SPRITC+6
	STY SPRITC+7
	RTS

BMB030
	CMP #24
	BCS BMB040
	INC SP0Y
	JMP BMB090

BMB040
	CMP #24+12
	BCS BMB050
	DEC SP0Y
	JMP BMB090

BMB050
	CMP #24+12+12
	BCS BMB060
	INC SP0Y
	JMP BMB090

BMB060
	CMP #24+12+12+6
	BCS BMB070
	DEC SP0Y
	JMP BMB090

BMB070
	CMP #24+12+12+6+6
	BCS BMB080
	INC SP0Y
	JMP BMB090

BMB080
	CMP #60+1
	BCS BMB090
	DEC SPRITB
BMB090
	JSR BALESC
	RTS

BMB100
	LDA SPRITB,X
	CMP #$01
	BNE BMB110
	LDA #$FE
	STA SV07F8,X
	STA $07F8,X
	LDA #$05        ;BOMB SOUND
	JSR SOUND
	RTS
BMB110
	CMP #$10
	BCC BMB120
	LDA #$FF
	STA SPRITA,X
	LDA #$01
	STA SPRITB,X
	LDA SPENA
	AND MASKZ,X
	STA SPENA
BMB120
	JMP BMB010

;
;-----  FUUSEN ESCAPE

BALESC
	LDA SPRITB+0
	AND #$07
	BNE BLE010
	LDA SPRITB+0
	CMP #$38+1
	BCS BLE010
	LSR A
	LSR A
	LSR A
	AND #$07
	TAY
	LDA SPRITA,Y
	ORA #$10
	STA SPRITA,Y    ;ESCAPE START BY INT. TIME

BLE010
	LDX #$07
BLE020
	LDA SPRITA,X
	AND #$10
	BNE BLE040
BLE030
	DEX
	CPX #$01
	BNE BLE020
	BEQ BLE044

BLE040
	LDA SPRITA,X
	BMI BLE042
	JSR SPRMOV
	LDA WK6
	BMI BLE030
BLE042
	LDA #$FF
	STA SPRITA,X
	LDA SPENA
	AND MASKZ,X
	STA SPENA
	AND #$FC
	BEQ BLE050      ;ALL SPR. DONE
	BNE BLE030
BLE044
	LDA SPENA
	AND #$FC
	BNE BLE090
BLE050
	LDA #$40
	STA TIMER2
	LDA #$7F
	STA TIMER3
	DEC NUMSHP
	LDA #$FF
	STA SPRITA
BLE090
	RTS

;*---------------------------------------------------*
;*  ALL ENEMY DONE,  CLEAR HEAD FUUSEN               *
;*---------------------------------------------------*

ENDCHK
	LDA NUMENM
	BEQ EDC010
	RTS
EDC010
	LDA #$FF
	STA KILLSP      ;STOP MAN

	LDA DWNCNT
	BEQ EDC020      ;DOWN ING
	RTS

EDC020
	LDX #$05
	LDY HEADTB,X
	BPL EDC030
	DEC NUMENM
	LDA #$02        ;WIN SOUND
	JSR SOUND
	LDA #$4F
	STA TIMER2
	LDA #$7F
	STA TIMER3
	RTS

EDC030
	LDA #$80
	STA SPRITA,Y

	TYA
	TAX             ;X=SPR-NO.
	JSR OFHDON
	LDA #16
	STA DWNCNT
	LDA #$00
	STA DWNSTP
	RTS

;*-----------------------------------------------*
;* BALUUN DOWN ON PIERO HEAD                     *
;*-----------------------------------------------*

DOWNBL
	LDA DWNCNT
	BNE DWB010
	RTS

DWB010
	DEC DWNCNT
	BEQ DWB050
DWB020
	LDX #$FF
DWB030
	INX
	CPX #$06
	BEQ DWB039
	LDY HEADTB,X
	BMI DWB030
	CPY DWNSTP
	BNE DWB040
DWB039
	RTS

DWB040
	STX WK0         ;SAVE X-REG
	TYA
	ASL A
	TAX
	INC SP0Y,X
	LDX WK0         ;GET X-REG
	JMP DWB030

DWB050
	LDA #00
	STA KILLSP
	LDX #$07
DWB090
	RTS

.END
