.PAGE 'KICKSND'
;*---------------------------------------------------------*
;* SOUND ON-OFF                                            *
;*---------------------------------------------------------*

SOUND
;----- BGM1 SOUND ON

SND100
	CMP #$01
	BNE SND200
	LDA #<BGM1
	STA SNDIDX
	LDA #>BGM1
	STA SNDIDX+1
	LDA #$11
	STA VCREG1
	LDA #$11
	STA VCREG2
	LDA #$01
	STA SNDTIM
	RTS
;
;----- BGM2 SOUND ON (WIN)

SND200
	CMP #$02
	BNE SND300
	LDA #<BGM2
	STA SNDIDX
	LDA #>BGM2
	STA SNDIDX+1
	RTS
;
;----- BGM3 SOUND ON (DONE)

SND300
	CMP #$03
	BNE SND400
	LDA #<BGM3
	STA SNDIDX
	LDA #>BGM3
	STA SNDIDX+1
	RTS
;
;----- KICK SOUND ON

SND400
	CMP #$04
	BNE SND500
	LDA #$07
	STA BOMBFL
	LDA #$21
	STA VCREG3
SND499
	RTS
;
;----- BOMB SOUND ON

SND500
	CMP #$05
	BNE SND600
	LDA #$0F
	STA EXPLOD
	LDA #$81
	STA VCREG3
	RTS
;
;----- BOUNUS SOUND ON

SND600
	CMP #$06
	BNE SND700
	RTS
;
;----- PACMAN SOUND ON

SND700
	CMP #$07
	BNE SND800
	LDA #$0F
	STA LASER
	ASL A
	STA FREHI3
	LDA #$21
	STA VCREG3
	RTS
;
;----- FUUSE HEAD ON SOUND

SND800
	CMP #$08
	BNE SND900
	LDA #$07
	STA LASER
	LDA #$21
	STA VCREG3
	RTS
;
;-----
SND900
	RTS


;*--------------------------------------------------*
;* SOUND CONTROL SUBROUTIN CALL BY ONE CYCLE        *
;*--------------------------------------------------*

SOUNDM
;
;----- KICK SOUND

SNM400
	LDA BOMBFL
	BEQ SNM500
	ASL A
	ASL A
	ASL A
	STA FREHI3
	DEC BOMBFL
	BNE SNM500
	LDA #$20
	STA VCREG3
;
;----- BOMB SOUND

SNM500
	LDA EXPLOD
	BEQ SNM600
	ASL A
	ASL A
	ASL A
	STA FREHI3
	DEC EXPLOD
	BNE SNM600
	LDA #$80
	STA VCREG3
;
;----- BOUNUS SOUND
SNM600

;
;----- PACMAN SOUND

SNM700
	LDA LASER
	BEQ SNM800
	DEC LASER
	LDA LASER
	CMP #$08
	BCS SNM800
	ASL A
	ASL A
	ASL A
	STA FREHI3
	LDA LASER
	BNE SNM800
	LDA #$20
	STA VCREG3
;
;-----
SNM800

;
;-----  BACK GROUND MUSIC

SNMBGM
	DEC SNDTIM
	BNE SNMEXT
	CLC
	LDA SNDIDX
	ADC #$02
	STA SNDIDX
	LDA SNDIDX+1
	ADC #$00
	STA SNDIDX+1
	LDY #$00

	LDA (SNDIDX),Y  ;GET SOUND TIME
	BNE SNMB10      ;TBL-END ? (TIME=0)
	LDA #<BGM1N
	STA SNDIDX
	LDA #>BGM1N
	STA SNDIDX+1
	LDA (SNDIDX),Y
SNMB10
	STA SNDTIM

	LDY #$01
	LDA (SNDIDX),Y  ;GET FREQ. INDEX
	ASL A
	TAX
	LDA FREQTB,X
	STA FRELO1
	CLC
	ADC #$30
	STA FRELO2
	LDA FREQTB+1,X
	STA FREHI1
	ADC #$00
	STA FREHI2
SNMEXT
	RTS


;----- SOUND TABLE -----------------------------------*
;

BGM1	= *-2
BGM1N
	.BYTE T12,G4,T08,WAT,T16,G4,T16,G4,T16,G4,T16,F4S,T16,G4
	.BYTE T12,A4,T12,A4,T12,B4,T12,B4,T08,D5,T08,C5,T12,WAT
	.BYTE T16,C5,T16,C5,T16,B4,T16,A4,T12,G4,T16,A4
	.BYTE T16,B4,T16,A4,T16,G4,T32,G4,T32,G4,T32,G4
	.BYTE T32,G4,T12,G4,T16,A4,T16,B4,T16,A4
	.BYTE T16,G4,T32,G4,T32,G4,T32,G4,T32,F4S,T32,G4
	.BYTE T12,A4,T12,A4,T12,B4,T12,B4,T08,D5,T08,C5
	.BYTE T12,WAT,T16,C5,T16,C5,T16,B4,T16,A4,T12,G4
	.BYTE T16,A4,T16,B4,T16,A4,T16,G4,T32,G4,T32,G4
	.BYTE T32,G4,T32,G4,T12,G4,T16,A4,T16,B4
	.BYTE T16,A4,T16,G4,T16,G4,T16,G4,T16,F4S,T16,F4,T16,E4
	.BYTE T06,WAT,EDS

BGM2	= *-2
	.BYTE T16,C4,T32,WAT,T16,C4,T08,E4,T16,A4,T16,G4
	.BYTE T16,F4,T04,C5,TWT,WAT,EDS

BGM3	= *-2
	.BYTE T04,C5,T16,B4,T16,A4,T16,G4,T16,F4,T16,E4
	.BYTE T16,D4,T16,C4,T16,WAT,T08,C4,TWT,WAT,EDS

T64	= $02
T32	= $04
T16	= $08
T12	= $10
T08	= $18
T06	= $20
T04	= $28
TWT	= $FF
EDS	= $00

WAT	= 0
E3	= 1
F3	= E3+1
F3S	= F3+1
G3	= F3S+1
G3S	= G3+1
A3	= G3S+1
A3S	= A3+1
B3	= A3S+1
C4	= B3+1
C4S	= C4+1
D4	= C4S+1
D4S	= D4+1
E4	= D4S+1
F4	= E4+1
F4S	= F4+1
G4	= F4S+1
G4S	= G4+1
A4	= G4S+1
A4S	= A4+1
B4	= A4S+1
C5	= B4+1
C5S	= C5+1
D5	= C5S+1
D5S	= D5+1
E5	= D5S+1
F5	= E5+1
F5S	= F5+1
G5	= F5S+1
G5S	= G5+1
A5	= G5S+1
A5S	= A5+1
B5	= A5S+1
C6	= B5+1
C6S	= C6+1
D6	= C6S+1
D6S	= D6+1
E6	= D6S+1
F6	= E6+1
F6S	= F6+1
G6	= F6S+1
G6S	= G6+1
A6	= G6S+1
A6S	= A6+1
B6	= A6S+1
C7	= B6+1
C7S	= C7+1

;
;----- FREQUENCE TABLE

FREQTB
	.WORD $0000
	.WORD $0A0D,$0B72,$0C20,$0CD6,$0D9C,$0E65,$0F46,$102F
	.WORD $1125,$122A,$133F,$1464,$159A,$16E3,$183F,$19B1
	.WORD $1B38,$1CD6,$1E8D,$205E,$224B,$2455,$267E,$28C8
	.WORD $2B34,$2DC6,$307F,$3361,$366F,$39AC,$3D1A,$40BC
	.WORD $4495,$48A9,$4CFC,$51A1,$5669,$5B8C,$60FE,$66C2
	.WORD $6CDF,$7358,$7A34,$8178,$892B,$9153,$99F7,$A31F

.END
