;DDIV **DECIMAL DIVIDE FAC=ARG/FAC
; 12-20-79
;DIVISION BY ZERO ERROR IF FAC ZERO
;
DDIV	LDA FACMSD
	BNE *+5
	JMP DVOERR
;
;DONE IF ARG IS ZERO
;
	LDA ARGMSD
	BNE DDIV5
	JMP DZEROF
;
;2'S COMPLEMENT ON DIVISOR
;AND SAVE EXPONENTS
;
DDIV5	LDA FACEXP
	EOR #$FE
	CLC
	ADC #2
	PHA
	LDA ARGEXP
	PHA
;
	LDA #0
	STA ARGEXP
	STA FACEXP
	LDX #RESMSD-RESLSD
DDIV10	STA RESLSD,X
	DEX
	BPL DDIV10
	STA RESEXP
;
;IS DIVISOR GREATER THAN DIVIDEND
;
	LDY #FACMSD-FACLSD
	LDX #0
	SEC
	SED
DDIV20	LDA ARGLSD,X
	SBC FACLSD,X
	STA ARGLSD,X
	INX
	DEY
	BPL DDIV20
	LDA ARGEXP
	SBC FACEXP
	STA ARGEXP
	CLD
	PHP             ;DECREMENT FLAG
	BCS DDIV80
;
;RESTORE ARG
;
DDIV30	LDY #FACMSD-FACLSD
	LDX #0
	CLC
	SED
DDIV40	LDA ARGLSD,X
	ADC FACLSD,X
	STA ARGLSD,X
	INX
	DEY
	BPL DDIV40
	LDA ARGEXP
	ADC FACEXP
	STA ARGEXP
	CLD
;
;IS RESMSD ZERO?
;
	LDA RESMSD
	AND #$0F
	BEQ DDIV45
	PLP
	PLA
;
;ADJUST EXPONENT
;
	BCS DDIV43
	SEC
	SBC #2
	PHA
	LSR A
	CMP #$3F
	BNE DDIV41
	PLA
	PLA
	JMP DZEROF
DDIV41	PLA
DDIV43	STA ARGEXP
	PLA
	STA FACEXP
	JMP DMULDN
;
;SHIFT ARG MANTISSA LEFT ONE DIGIT
;
DDIV45	LDY #3
DDIV50	LDX #0
	CLC
	PHP
DDIV52	PLP
	ROL ARGLSD,X
	PHP
	INX
	CPX #1+ARGMSD-ARGLSD
	BCC DDIV52
	PLP
	ROL ARGEXP
;
;SHIFT RES MANTISSA LEFT ONE DIGIT
;
	LDX #0
	CLC
	PHP
DDIV62	PLP
	ROL RESLSD,X
	PHP
	INX
	CPX #1+RESMSD-RESLSD
	BCC DDIV62
	PLP
	ROL RESEXP
	DEY
	BPL DDIV50
;
;IS DIVISOR GREATER THAN DIVIDEND
;
DDIV70	LDY #FACMSD-FACLSD
	LDX #0
	SEC
	SED
DDIV72	LDA ARGLSD,X
	SBC FACLSD,X
	STA ARGLSD,X
	INX
	DEY
	BPL DDIV72
	LDA ARGEXP
	SBC FACEXP
	STA ARGEXP
	CLD
	BCC DDIV30
;
;INCREMENT RESLSD
;
DDIV80	INC RESLSD
	BNE DDIV70
.END
.LIB DCOMP
;DECIMAL COMPARE ARG:FAC
;DECEMBER 31, 1979
;
;.A= 1,C=0 IF ARG .LT. FAC
;.A= 0,C=1 IF ARG .EQ. FAC
;.A=-1,C=1 IF ARG .GT. FAC
;
DCOMP	LDA FACEXP
	EOR ARGEXP
;
;ARE MANTISSA SIGNS SAME?
;
	LSR A
	BCS DCOM10      ;NO...
;
;ARE EXOPONENT SIGNS SAME?
;
	ROL A
	BMI DCOM20      ;NO...
;
;ARE EXPONENT MAGNITUDES SAME?
;
	BNE DCOM30      ;NO...
;
;COMPARE MANATISSA MAGNITUDES
;
	LDX #FACMSD-FACLSD-1
	SED
DCOM5	LDA ARGLSD+1, X
	CMP FACLSD+1,X
	BCC DCOM7
	BNE DCOM7
	DEX
	BNE DCOM5
DCOM7	CLD
	BNE DCOM40
	TXA
	BEQ DCOM45
;
;CASE DIFFERENT MANTISSA SIGNS
;
DCOM10	LDA FACEXP
	ROR A
	JMP DCOM42
;
;CASE DIFFERENT EXPONENT SIGNS
;
DCOM20	LDA FACEXP
	ROL A
	JMP DCOM40
;
;CASE DIFFERENT EXPONENT MAGNITUDES
;
DCOM30	SEC
	LDA ARGEXP
	SBC FACEXP
;
;HANDLE NEGATIVE MANTISSA
;
DCOM40	ROL A
	EOR ARGEXP
	LSR A
;
;COMMON EXIT CODE
;
DCOM42	LDA #$FF
	BCS DCOM45
	LDA #$01
;
DCOM45	RTS
.END
.END
