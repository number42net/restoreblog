.PAG 'TEMPORARIES'
;EQUATE SCREEN EDITOR TEMPORARILY
;
CINT	=$E000+4
LP2	=$E003+4
LOOP5	=$E006+4
PRT	=$E009+4
SCNKEY
KEY	=$E00F+4
;
; NO ROUTINES IN 8032 SCREEN EDITOR
;
SCRORG	=$E00C+4
PLOT	=$E015+4
IOBASE	=$E018+4
ESCRTS	=$E01B+4
;
; NO CASSETTE ROUTINES AVALIABLE
;
TAPEH
TWRT
CSTE2
ZZZ
TRD
CSTE1
FAF
FAH
WBLK
RBLK
JTP20
	LDA #$05        ;SEND BACK ?DEVICE NOT PRESENT
	SEC
	RTS
;
; SOME NEEDED ROUTINES
;
RD300	LDA STAH
	STA SAH
	LDA STAL
	STA SAL
	LDA STAS
	STA SAS
	STA I6509
	RTS
;
CMPSTE	SEC
	LDA SAL
	SBC EAL
	LDA SAH
	SBC EAH
	LDA SAS
	SBC EAS
	RTS
;
INCSAL	INC SAL
	BNE INCR20
	INC SAH
INCR10	BNE INCR20
	INC SAS
	LDA SAS
	STA I6509
	LDA #$02        ;SKIP $0000 AND $0001
	STA SAL
INCR20	RTS
.SKI 5
;-------------------------------------
; TAPERY - GET FROM THE TAPE BUFFER
;   LDA (TAPE1)Y ;REPLACEMENT
;-------------------------------------
TAPIRY	INY
TAPERY	LDX I6509
	LDA TAPE1+2
	STA I6509
	LDA (TAPE1)Y
	STX I6509
	RTS
;-------------------------------------
; TAPEWY - PUT CHAR IN THE TAPE BUFFER
;   STA (TAPE1)Y ;REPLACEMENT
;-------------------------------------
TAPZWY	LDY #$FF        ;FIRST BYTE IN BUFFER
TAPIWY	INY             ;AUTO INC INTO BUFFER
TAPEWY	LDX I6509
	PHA
	LDA TAPE1+2
	STA I6509
	PLA
	STA (TAPE1)Y
	STX I6509
	RTS
;-------------------------------------
; FNADRY - GET FROM THE TAPE BUFFER
;   LDA (FNADR)Y ;REPLACEMENT
;-------------------------------------
FNADRY	LDX I6509
	LDA FNADR+2
	STA I6509
	LDA (FNADR)Y
	STX I6509
	RTS
.END
