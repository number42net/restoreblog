.PAGE 'LEDS'
;
;TURN ON ACTIVITY LED SPECIFIED
; BY DRVNUM
;
SETLDS	SEI
	LDA #$FF-LED1-LED0
	AND LEDPRT
	PHA
.SKIP
	LDA DRVNUM
	BEQ LEDS0
	PLA
	ORA #LED1
	BNE LEDS1
LEDS0
	PLA
	ORA #LED0
LEDS1
	STA LEDPRT
	CLI
	RTS
;
LEDSON	SEI
	LDA #LED1+LED0
	ORA LEDPRT
	STA LEDPRT
	CLI
	RTS
;
ERROFF
	LDA #0
	STA ERWORD
	STA ERLED
	RTS
;
ERRON	SEI
	TXA
	PHA             ;SAVE .X
	LDA #80
	STA ERWORD
	LDX #0
;LDA DRVNUM ;FOR 2 DRIVES
;AND #1
;TAX
	LDA LEDMSK,X
	STA ERLED
	ORA LEDPRT      ;SET LED ON
	STA LEDPRT
	PLA
	TAX	;RESTORE .X
	CLI
	RTS
.END
