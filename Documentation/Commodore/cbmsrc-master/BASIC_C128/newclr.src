	.page
	.subttl NEW / CLEAR Commands
;
; The "new" command clears the program text as well as variable space.
;

new	beq scrtch	;make sure there is a terminator.
	rts		;return in shame.

scrtch	lda #0		;get a clearer.
	tay		;set up index.
	sta (txttab),y	;clear first link.
	iny
	sta (txttab),y
	sta trcflg	;reset trace flag.
	lda txttab
	clc
	adc #2
	sta text_top	;set up (text_top), the end of text
	lda txttab+1
	adc #0
	sta text_top+1

runc	jsr stxtpt	;load (txtptr) with (txttab)-1
	lda #0		;fall thru to "CLR"

	.page

;clearc routine
;
; Clearc is a subroutine which initializes the variable and array space by
; resetting strend (the end of array storage).It falls into "stkini"
; which resets the stack.

clear	bne stkrts
clearc	jsr k_clall	;close all files
	ldy #0
	sty dsdesc	;flag 'no ds string'
	dey		;y=$ff
	sty trapno+1    ;flag no current trap line
	sty errlin	;reset last error pointers
	sty errlin+1
	sty errnum

	lda max_mem_1	;clear string space
	ldy max_mem_1+1
	sta fretop
	sty fretop+1

	lda #<stkbot	;empty run-time stack
	ldy #>stkbot
	sta tos
	sty tos+1

	lda vartab
	ldy vartab+1
	sta arytab		;this will delete all variavbles,
	sty arytab+1
	sta strend		;..and arrays
	sty strend+1

	ldx #pumony-puchrs 	;reset print using chars
1$	lda pudefs,x
	sta puchrs,x
	dex
	bpl 1$

fload	jsr restore$1		;reset pointer for data statements

	.page


; stkini routine

;   stkini resets the stack pointer. string temporaries are freed up,
;   subflg is reset, continuing is prohibited.

stkini	ldx #tempst	;initialize string temporaries
	stx temppt
	pla		;set up return address
	tay
	pla
	ldx #stkend-257
	txs
	pha
	tya
	pha
	lda #0
	sta oldtxt+1
	sta subflg	;allow subscripts
	sta bits
stkrts	rts


pudefs .byte ' ,.$'	;default:  fill, comma, dec pnt, money symbol

stxtpt	clc
	lda txttab
	adc #$ff
	sta txtptr
	lda txttab+1
	adc #$ff
	sta txtptr+1	;set up text pointers
	rts

;.end

