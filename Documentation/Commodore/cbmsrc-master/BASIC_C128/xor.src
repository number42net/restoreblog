	.page
	.subttl XOR Function

;**************************************************************
;*
;*   XOR - exclusive-or two 16 bit arguments
;*
;*	syntax : XOR (arg1, arg2)
;*
;**************************************************************

xor	lda poker	;protect the $*^%$*^(? poker value (could be in use)
	pha
	lda poker+1
	pha

	jsr chknum
	jsr getadr	;get first arg
	pha		;save MSB
	tya
	pha		;save LSB

	jsr comwrd	;check for comma, get word
	jsr chkcls	;check for closing parens

	pla
	eor poker	;xor LSB (comwrd left a copy of it's arg in POKER)
	tay
	pla
	eor poker+1	;..and MSB
	jsr nosflt	;..and go float 'em

	pla
	sta poker+1
	pla
	sta poker

	rts

;end
