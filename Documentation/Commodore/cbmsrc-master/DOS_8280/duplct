.PAGE 'DUPLICATE'
;
; DUPLICATE DISK
;
DUPLCT
	LDA #BADCMD
	JMP CMDERR      ; BAD COMMAND FOR SINGLE DISK
;
; FORMAT THE DRIVE
;
JFORMT
	LDY #0
	STY JOBNUM      ; USE JOB NUMBER 0
	JSR JOB2X       ; .X = JOBNUM*8
	TYA
	STA HDRS+2,X
	LDA #1          ; SECTOR 1
	STA HDRS+3,X
	LDA #FMTCMD     ; FORMAT COMMAND
	JSR DOJOB       ; FORMAT THE DRIVE
FORMA0
	LDA #0          ; USE JOB NUMBER 0
FORMA1
	STA JOBNUM
	JSR JOB2X       ; .X = JOBNUM*8
	LDA #0
	STA R0          ; R0 = 0, NEW TRACK
	STA HEAD        ; HEAD = 0
	STA HDRS+2,X    ; TRACK = 0
	LDA #1
	STA HDRS+3,X    ; SECTOR = 1, HEAD = 0
	STA SECTOR      ; FIRST SECTOR
	JSR SETBTS      ; SET UP BAD T, S AND H
FORMA2
	LDA #$FF
	STA JOBRTN      ; RETURN ALL ERRORS
	LDA #WVERFY
	JSR DOJOB       ; VERIFY ALL SECTORS
	CMP #2          ; ANY ERRORS?
	BCC FORMA3      ; NO
	JSR ADDBTS      ; YES, ADD TO BAD T, S AND H
FORMA3
	JSR JOB2X       ; .X = JOBNUM*8
	INC HDRS+3,X    ; NEXT SECTOR, HEAD
	LDA HDRS+3,X
	AND #%00011111
	CMP SECTRS
	BCC FORMA2      ; NEXT SECTOR
	INC HEAD
	LDA HEAD
	CMP HEADS
	BCS FORMA4      ; DONE WITH THIS TRACK
	LDA SECTOR
	PHA
	LDA #1
	STA SECTOR
	JSR ADDHED
	STA HDRS+3,X    ; HEAD  AND SECTOR
	PLA
	STA SECTOR
	BNE FORMA2      ; JUMP
FORMA4
	JSR JOB2X       ; .X = JOBNUM*8
	LDA #1
	STA HDRS+3,X    ; BACK TO SECTOR = 1, HEAD 0
	LDA #0
	STA R0          ; R0 = 0, NEW TRACK
	STA HEAD        ; HEAD  = 0
	INC HDRS+2,X    ; TRACK = TRACK+1
	LDA HDRS+2,X
	CMP MAXTRK      ; MORE TRACKS?
	BCC FORMA2      ; YES, DO THE NEXT CYLINDER
	JMP WRTBTS      ; WRITE LAST LIST (RTS)
;
; SET UP BAD T, S AND H
;
; RETURNS WITH:
;
;     BUFFER(JOBNUM)     = $FF
;     INDEX POINTER (IP) = 2
;
SETBTS
	LDY JOBNUM
	LDA #$FF
	JSR SETBUF      ; SET BUFFER TO $FF'S
	INC IP
	INC IP          ; IP = 2
	RTS
;
; SET BUFFER TO CONTENTS OF .A
;
; ON ENTRY:
;
;     .Y = BUFFER NUMBER
;     .A = BUFFER CONTENTS
;
SETBUF
	LDX BUFIND,Y
	STX IP+1
	LDY #0
	STY IP
SETBU1
	STA (IP),Y
	INY
	BNE SETBU1
	RTS
;
; ADD T, S AND H TO LIST
;
ADDBTS
	LDY #0
	LDA R0          ; NEW TRACK?
	BNE ADDBT2      ; NO, JUST ADD SECTOR, HEAD
	INC R0          ; R0 <> 0
	INC IP          ; LEAVE AN $FF
	BNE ADDBT1
	JSR WRTBTS      ; WRITE THIS LIST
ADDBT1
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+2,X    ; TRACK
	STA (IP),Y      ; MARK THE TRACK
	INC IP
	BNE ADDBT2
	JSR WRTBTS      ; WRITE THIS LIST
ADDBT2
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+3,X    ; SECTOR, HEAD
	STA (IP),Y      ; MARK THE SECTOR, HEAD
	INC IP
	BNE ADDBT3
	JSR WRTBTS      ; WRITE THIS LIST
ADDBT3
	RTS
;
; WRITE THE BAD T, S AND H LIST
;
WRTBTS
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+2,X
	PHA             ; SAVE THE CURRENT TRACK
	LDA HDRS+3,X
	PHA             ; SAVE THE CURRENT SECTOR, HEAD
	LDA SECTOR
	CLC
	ADC #1          ; NEXT SECTOR, HEAD
	CMP SECTRS
	BCS WRTBT2      ; NO MORE SECTORS
	STA HDRS+3,X    ; POSSIBLE NEXT SECTOR, HEAD
	LDA #0
	STA HDRS+2,X    ; CYLINDER 0 ONLY
WRTBT1
	LDA #$FF
	STA JOBRTN      ; RETURN ALL ERRORS
	JSR DOWRIT      ; TRY TO WRITE IT
	CMP #2          ; ANY ERRORS?
	BCC WRTBT3      ; NO
	JSR JOB2X       ; YES, .X = JOBNUM*8
	INC HDRS+3,X    ; NEXT SECTOR, HEAD
	LDA HDRS+3,X
	CMP SECTRS
	BCC WRTBT1      ; TRY THE NEXT SECTOR, HEAD
WRTBT2
	LDA #12
	JMP ERROR       ; FORMAT ERROR
WRTBT3
	LDA SECTOR      ; GET THE PREVIOUS SECTOR
	CMP #1
	BNE WRTBT4      ; HAS A PREVIOUS SECTOR
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+3,X
	LDY DRVNUM
	STA BTSSEC,Y    ; START OF BAD T, S AND H (SECTOR)
	LDA #0
	STA BTSTRK,Y    ; START OF BAD T, S AND H (TRACK)
	BEQ WRTBT5      ; JUMP
WRTBT4
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+3,X    ; CURRENT SECTOR
	PHA             ; SAVE IT
	LDA SECTOR      ; PREVIOUS SECTOR
	STA HDRS+3,X    ; NOW THE CURRENT SECTOR
	JSR DOREAD      ; READ THE PREVIOUS SECTOR
	LDY #0
	TYA
	STA IP
	STA (IP),Y      ; SET THE TRACK LINK
	INY
	PLA             ; CURRENT SECTOR
	STA (IP),Y      ; SET THE SECTOR LINK
	PHA             ; SAVE THE SECTOR AGAIN
	JSR DOWRIT      ; WRITE THE PREVIOUS BTS
	JSR JOB2X       ; .X = JOBNUM*8
	PLA             ; CURRENT SECTOR
	STA HDRS+3,X    ; RESTORE THE CURRENT SECTOR
WRTBT5
	JSR JOB2X       ; .X = JOBNUM*8
	LDA HDRS+3,X
	STA SECTOR      ; NOW THE CURRENT SECTOR
	PLA
	STA HDRS+3,X    ; SECTOR ON ENTRY
	PLA
	STA HDRS+2,X    ; TRACK ON ENTRY
	JMP SETBTS      ; SET UP FOR NEW BUFFER (RTS)
.END
