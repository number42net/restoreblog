.PAGE 'JOBS'
;
; USE LASTJOB FOR DRIVE #
; CMD IS USED FOR JOB COMMAND
;
SETLJB
	LDA LSTJOB,X
	AND #1
	ORA CMD
;
; SET JOB UP AND CHECK T&S
; .A = COMMAND FOR JOBS
; .X = JOB NUMBER
;
SETJOB
	STA CMD
	STX JOBNUM
	TXA
	ASL A
	ASL A
	ASL A
	TAX
	LDA HDRS+2,X    ; TRACK
	CMP MAXTRK
	BCS TSERR       ; TRACK TOO LARGE
	LDA HDRS+3,X    ; SECTOR
	AND #%00011111  ; MASK OUT THE HEAD
	BEQ TSERR
	CMP SECTRS
	BCS TSERR       ; SECTOR TOO LARGE
	LDA HDRS+3,X    ; HEAD
	AND #%11100000  ; MASK OUT THE SECTOR
	LSR A
	LSR A
	LSR A
	LSR A
	LSR A
	CMP HEADS
	BCS TSERR       ; HEAD TOO LARGE
	LDX JOBNUM
	LDA CMD
	STA JOBS,X
	STA LSTJOB,X
	RTS
;
; ILLEGAL TRACK AND SECTOR
;
TSERR
	LDA JOBNUM
	JSR GETHD1      ; GET THE TRACK, SECTOR AND HEAD
TSER1
	LDA #BADTS      ; BAD TRACK OR SECTOR
	JMP CMDER2
TSCHK
	LDA TRACK
	CMP MAXTRK
	BCS TSER1       ; TRACK TOO LARGE
	LDA SECTOR
	AND #%00011111  ; MASK OUT THE HEAD
	BEQ TSER1
	CMP SECTRS
	BCS TSER1       ; SECTOR TOO LARGE
	LDA SECTOR
	AND #%11100000  ; MASK OUT THE SECTOR
	LSR A
	LSR A
	LSR A
	LSR A
	LSR A
	CMP HEADS
	BCS TSER1       ; HEAD TOO LARGE
	RTS
;
; DO JOB IN .A, SET UP ERROR COUNT
; AND LSTJOB. RETURN WHEN JOB DONE OK
; JMP TO ERROR IF ERROR RETURNS
;
DOREAD
	LDA #READ
	BNE JDOJOB      ; JUMP
DOWRIT
	LDA #WRITE
JDOJOB
	ORA DRVNUM
	LDX JOBNUM
DOIT
	STA CMD
DOIT2
	LDA CMD
	JSR SETJOB
.PAGE
;
; WAIT UNTIL JOB(.X) IS DONE
; RETURN WHEN DONE
;
WATJOB
	JSR TSTJOB
	BCS WATJOB
	PHA             ; CLR JOBRTN FLAG
	LDA #0
	STA JOBRTN
	PLA
	RTS
;
; TEST IF JOB(.X) IS DONE YET
; IF NOT DONE RETURN
; IF OK THEN RETURN ELSE REDO IT
;
JTSTJB
	LDA JOBS,X
	BMI TSTJB4
	CMP #2
	BCC TSTJB3      ; FINISHED OK
	BCS TSTJB2      ; DO RETRIES
TSTJB1
	LDY REVCNT
	STY RETRY       ; RESTORE RETRY COUNT
	BIT JOBRTN
	BMI TSTJB3      ; JUST RETURN
	JMP ERROR       ; SEND THE ERROR CODE
TSTJB2
	JSR PATC        ; PATCH FOR RESTORE
	BEQ TSTJB1      ; NO MORE RETRIES
	LDA LSTJOB,X
	STA JOBS,X      ; TRY THE JOB AGAIN
	BNE TSTJB4      ; JUMP
TSTJB3
	LDY REVCNT
	STY RETRY       ; RESTORE RETRY COUNT
	CLC             ; .C = 0 FINISHED OK OR QUIT
	RTS
TSTJB4
	SEC             ; .C = 1 NOT YET
	RTS
;
; SET HEADER OF ACTIVE BUFFER OF THE
; CURRENT LINDX TO TRACK,SECTOR,ID
;
SETHDR
	JSR GETACT
JSETH
	ASL A
	ASL A
	ASL A
	TAY
	LDA TRACK
	STA HDRS+2,Y    ; SET TRACK
	LDA SECTOR
	STA HDRS+3,Y    ; SET SECTOR AND HEAD
	RTS
.END
