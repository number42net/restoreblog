.PAG 'TAPE FILES 08/11/82'
;FAH -- FIND ANY HEADER
;
;READS TAPE DEVICE UNTIL ONE OF FOLLOWING
;BLOCK TYPES FOUND: BDFH--BASIC DATA
;FILE HEADER, BLF--BASIC LOAD FILE
;FOR SUCCESS CARRY IS CLEAR ON RETURN.
;FOR FAILURE CARRY IS SET ON RETURN.
;IN ADDITION ACCUMULATOR IS 0 IF STOP
;KEY WAS PRESSED.
;
FAH	LDA VERCK       ;SAVE OLD VERIFY
	PHA
	JSR RBLK        ;READ TAPE BLOCK
	TAY             ;SAVE ERROR
	PLA
	STA VERCK       ;RESTORE VERIFY FLAG
	TYA             ;RESTORE STOP KEY FLAG
	BCS FAH40       ;READ TERMINATED
;
	LDY #0
	JSR TAPERY      ;GET HEADER TYPE
;
	CMP #EOT        ;CHECK END OF TAPE?
	BEQ FAH40       ;YES...FAILURE
;
	CMP #BLF        ;BASIC LOAD FILE?
	BEQ FAH50       ;YES...SUCCESS
;
	CMP #BDFH       ;BASIC DATA FILE?
	BNE FAH         ;NO...KEEP TRYING
;
FAH50	PHA             ;RETURN FILE TYPE IN .X
	BIT MSGFLG      ;PRINTING MESSAGES?
	BPL FAH45       ;NO...
;
	LDY #MS17-MS1   ;PRINT "FOUND"
	JSR MSG
;
;OUTPUT COMPLETE FILE NAME
;
	LDY #5
FAH55	JSR TAPERY
	JSR BSOUT
	INY
	CPY #21
	BNE FAH55
FAH45	CLC             ;SUCCESS FLAG
	PLA
	TAX             ;RETURN FILE TYPE IN .X
;
FAH40	RTS             ;FAIL--CARRY FROM CMP
.SKI 5
;TAPEH--WRITE TAPE HEADER
;ERROR IF TAPE BUFFER DE-ALLOCATED
;CARRY CLEAR IF O.K.
;
TAPEH	STA T1
;
;DETERMINE ADDRESS OF BUFFER
;
	JSR ZZZ
	BCS TH40        ;BUFFER WAS DE-ALLOCATED
;
;PRESERVE START AND END ADDRESSES
;FOR CASE OF HEADER FOR LOAD FILE
;
	LDA STAS
	PHA
	LDA STAH
	PHA
	LDA STAL
	PHA
	LDA EAS
	PHA
	LDA EAH
	PHA
	LDA EAL
	PHA
;
;PUT BLANKS IN TAPE BUFFER
;
	LDY #BUFSZ-1
	LDA #' 
BLNK2	JSR TAPEWY
	DEY
	BNE BLNK2
;
;PUT BLOCK TYPE IN HEADER
;
	LDA T1
	JSR TAPEWY
;
;PUT START LOAD ADDRESS IN HEADER
;
	LDA STAL
	JSR TAPIWY
	LDA STAH
	JSR TAPIWY
;
;PUT END LOAD ADDRESS IN HEADER
;
	LDA EAL
	JSR TAPIWY
	LDA EAH
	JSR TAPIWY
;
;PUT FILE NAME IN HEADER
;
	INY
	STY T2
	LDY #0
	STY T1
TH20	LDY T1
	CPY FNLEN
	BEQ TH30
	JSR FNADRY
	LDY T2
	JSR TAPEWY
	INC T1
	INC T2
	BNE TH20
;
;SET UP START AND END ADDRESS OF HEADER
;
TH30	JSR LDAD1
;
;SET UP TIME FOR LEADER
;
	LDA #$69
	STA SHCNH
;
	JSR TWRT2       ;WRITE HEADER ON TAPE
;
;RESTORE START AND END ADDRESS OF
;LOAD FILE.
;
	TAY             ;SAVE ERROR CODE..
	PLA
	STA EAL
	PLA 
	STA EAH
	PLA
	STA EAS
	PLA
	STA STAL
	PLA
	STA STAH
	PLA
	STA STAS
	TYA             ;RESTORE ERROR CODE
;
TH40	RTS
.SKI 5
;FUNCTION TO RETURN TAPE BUFFER
;ADDRESS IN TAPE1
;
ZZZ	LDA TAPE1+2     ;CHECK FOR ALLOCATION
	CMP #$10
	BCC ZZZ010      ;...ALLOCATED
	JSR REQ256      ;GET SPACE
	STA TAPE1+2
	STX TAPE1
	STY TAPE1+1
ZZZ010	LDA TAPE1+2
	LDX TAPE1
	LDY TAPE1+1
	BCC TH40        ;EXIT IF EXISTING
	JMP ERRORX      ;CLEAR THE CHANNELS...
.SKI 5
LDAD1	JSR ZZZ         ;GET PTR TO CASSETTE
	STA STAS        ;SAVE SEGMENTS
	STA EAS
	TXA
	STA STAL        ;SAVE START LOW
	CLC
	ADC #BUFSZ      ;COMPUTE POINTER TO END
	STA EAL         ;SAVE END LOW
	TYA
	STA STAH        ;SAVE START HIGH
	ADC #0          ;COMPUTE POINTER TO END
	STA EAH         ;SAVE END HIGH
	RTS
.SKI 5
FAF	JSR FAH         ;FIND ANY HEADER
	BCS FAF40       ;FAILED
;
;SUCCESS...SEE IF RIGHT NAME
;
	LDY #5          ;OFFSET INTO TAPE HEADER
	STY T2
	LDY #0          ;OFFSET INTO FILE NAME
	STY T1
FAF20	CPY FNLEN       ;COMPARE THIS MANY
	BEQ FAF30       ;DONE
;
	JSR FNADRY
	STA RER         ;USE A TEMP ***************
	LDY T2
	JSR TAPERY
	CMP RER         ;CMP AGAINST TEMP *********
	BNE FAF         ;MISMATCH--TRY NEXT HEADER
	INC T1
	INC T2
	LDY T1
	BNE FAF20       ;BRANCH ALWAYS
;
FAF30	LDY #0          ;RETURN FILE TYPE IN .X
	JSR TAPERY
	TAX
	CLC             ;SUCCESS FLAG
FAF40	RTS
.SKI 3
;-------------------------------------
; TAPERY - GET CHAR FROM TAPE BUFFER
;   LDA (TAPE1)Y ;REPLACEMENT
;-------------------------------------
TAPIRY	INY
TAPERY	LDX I6509
	LDA TAPE1+2
	STA I6509
	LDA (TAPE1)Y
	STX I6509
	RTS
;-------------------------------------
; TAPEWY - PUT CHAR IN THE TAPE BUFFER
;   STA (TAPE1)Y ;REPLACEMENT
;-------------------------------------
TAPZWY	LDY #$FF        ;START AT BEGINNING OF BUFFER
TAPIWY	INY             ;AUTO INC INTO BUFFER
TAPEWY	LDX I6509
	PHA
	LDA TAPE1+2
	STA I6509
	PLA
	STA (TAPE1)Y
	STX I6509
	RTS
;-------------------------------------
; FNADRY - GET CHAR FROM FNADR BUFFER
;   LDA (FNADR)Y ;REPLACEMENT
;-------------------------------------
FNADRY	LDX I6509
	LDA FNADR+2
	STA I6509
	LDA (FNADR)Y
	STX I6509
	RTS
.END
