.PAG 'TIME 9/09/81'
;***********************************
;
; TIME AND ALARM ROUTINES FOR 6526
;
;***********************************
;
; RDTIM - READ THE TIME
;  .Y = (BIT7=AM/PM,BITS4-0 HRS)
;  .X = (BITS5-0 MINUTES)
;  .A = (BITS5-0 SECONDS)
;
RDTIM	LDY CIA+TODHR   ;LATCH TOD
	LDX CIA+TODMIN
	LDA CIA+TODSEC
	BIT CIA+TOD10   ;TO RESTART TOD
	RTS
;
; SETTIM - SET TOD AND ALARM
;  C-SET => SET ALARM
;  C-CLR => SET TOD
;  REGISTERS SAME AS RDTIM
;  * THESE ROUTINES HAVE 1 SEC RESOLUTION
;
SETTIM	PHA
	ROR A           ;SET BIT ACCORDING TO CARRY
	AND #$80
	ORA CIA+CRB
	STA CIA+CRB
	LDA #0          ;SET TOD10'S TO ZERO
	STA CIA+TOD10   ;WHICH STOPS TOD
	PLA
	STA CIA+TODSEC
	STX CIA+TODMIN
	STY CIA+TODHR   ;RESTART TOD
	RTS
.END
