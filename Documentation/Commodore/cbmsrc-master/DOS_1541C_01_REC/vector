.PAG VECTOR
ECHKSM	.BYT 0          ;$E-$F CHECKSUM
NMI	JMP (VNMI)
;
PATCH	;PATCH AREA
PEA7A	STA LEDPRT      ;PATCH FOR POWER-ON ERRORS
	STA LEDOUT      ;TURN DDRB TO OUTPUT
	JMP REA7D       ;RETURN TO LED BLINK CODE
;
;---------------------------------------------------
; PATCH AREA FOR 1541 DISK WITH SLOW SERIAL RECEIVE
;
SLOWD	TXA             ; ONLY AFFECT .A
	LDX #5          ; INSERT 40US OF DELAY WITH THIS ROUTINE
SLOWE	DEX
	BNE SLOWE
	TAX
	RTS
;
;---------------------------------------------------
;
CLKDAT	JSR CLKLOW      ; NEW CLOCK LOW AND...
	JMP DATHI       ; DATA HI FOR PATCH AREA
;
;---------------------------------------------------
;
NNMI	LDA CMDBUF+2    ; NEW NMI ROUTINE CHECK FOR
	CMP #'-'
	BEQ NNMI10      ; IF UI- THEN NO DELAY
	SEC
	SBC #'+'
	BNE NMI         ; IF NOT UI+ THEN MUST BE A REAL UI COMMAND
NNMI10	STA DRVTRK+1
	RTS
;
;
;---------------------------------------------------
;      PATCH 5   *ROM-05 8/18/83*
;
;CLOCK LINE HI ON PWR ON
;
PATCH5	STX  DDRA1      ; SET DIRECTION
	LDA #$02        ; SET CLOCK HIGH
	STA PB
	LDA  #$1A       ; SET DDRA REG B
	STA DDRB1
	JMP DKIT10      ; RETURN
;
;
;---------------------------------------------------
;      PATCH 6   *ROM DS 8/18/83*
;
;FIX EOI TIMING TO WAIT ALL DEVICES
;
;
PATCH6	LDA PB          ; TEST DATA LINE
	AND #$01        ; EVERY ONE RDY ?
	BNE PATCH6
	LDA #$01        ; WAIT 255 USEC
	STA T1HC1       ; SET TIMER
	JMP ACP00       ; RETURN
;
;
;---------------------------------------------------
;     PATCH 7   *** ROM DS 03/15/85 ***
;
;
PATCH7	LDA #$FF        ; CLEAR FORMAT FLAGS
	STA FTNUM       ;
	JMP FORMAT      ; TRANSFER FORMAT TO RAM
;
;
;----------------------------------------------------
;
;     PATCH 9   *ROM DS 09/12/84*
;
PATCH9	TXA
	PHA
	TYA
	PHA
	LDX #1
PTCH91	LDY #100        ; TIMER (100*16US=1.6MS)
PTCH92	LDA POTA1       ; CHECK PHOTO
	CMP POTA1
	BNE PTCH93
	DEY
	BNE PTCH92      ; WAIT
	DEX
	BNE PTCH91
	AND #1
	BEQ PTCH93
	LDA DSKCNT      ; YES, CHECK MOTOR PHASE
	AND #3          ; PHASE A ?
	BNE PTCH93      ; NO, NOT YET
	PLA
	TAY
	PLA
	TAX
	LDA #$00        ; YES, CLEAR # OF STEPS
	STA STEPS       ;
	JMP END33       ; GO BACK TO END
PTCH93	PLA
	TAY
	PLA
	TAX
;
	INC STEPS       ; UPDATE # OF STEPS
;
	LDX DSKCNT      ; UPDATE MOTOR PHASE
	DEX             ;
	JMP PPPPPP      ; RETURN TO MAIN
;
;
;----------------------------------------------------
;
;     PATCH 10  *ROM DS 01/22/85*
;
PTCH10	JSR CNTINT      ; CONTROLLER INIT.
	LDA #1
	STA HDRS
	LDA #BUMP
	STA JOBS
	RTS
;
;DEFAULT TABLE FOR USER COMMAND
;
	*= $FFE6
	.WORD FORMAT
	.WORD TRNOFF
UBLOCK	.WORD UBLKRD
	.WORD UBLKWT
.SKIP
	.WORD $0500     ;LINKS TO BUFFER #2
	.WORD $0503
	.WORD $0506
	.WORD $0509
	.WORD $050C
	.WORD $050F
	*= $FFFA
	.WORD NNMI
	.WORD DSKINT
	.WORD SYSIRQ
.END
