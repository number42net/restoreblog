.PAGE 'COPY'
; COPY FILE(S) TO ONE FILE
;
COPY	;FILENAMES, OPTIMIZE
	JSR LOOKUP      ;LOOK IP ALL FILES
	LDA F2CNT
	CMP #3
	BCC COP10
;
	LDA FILDRV
	CMP FILDRV+1
	BNE COP10
;
	LDA ENTIND
	CMP ENTIND+1
	BNE COP10
;
	LDA ENTSEC
	CMP ENTSEC+1
	BNE COP10
;
	JSR CHKIN       ;CONCAT
	LDA #1
	STA F2PTR
	JSR OPIRFL
;
	JSR TYPFIL
	BEQ COP01
	CMP #PRGTYP
	BNE COP05
COP01
	LDA #MISTYP
	JSR CMDERR
COP05
	LDA #IWSA
	STA SA
	LDA LINTAB+IRSA
	STA LINTAB+IWSA
	LDA #$FF
	STA LINTAB+IRSA
	JSR APPEND
	LDX #2
	JSR CY10
	JMP ENDCMD
COP10
	JSR CY
	JMP ENDCMD
;
;
CY
	JSR CHKIO       ;CHECK FILES FOR EXISTENCE
.SKIP
	LDA FILDRV
	AND #1
	STA DRVNUM
	JSR OPNIWR      ;OPEN INTERNAL WRITE CHNL
	JSR ADDFIL      ;ADD TO DIRECTORY
	LDX F1CNT
.SKIP
CY10	STX F2PTR       ;SET UP READ FILE
	JSR OPIRFL
;
	LDA #IRSA       ;ADD FOR REL COPY
	STA SA
	JSR FNDRCH
	JSR TYPFIL
	BNE CY10A       ;NOT REL
	JSR CYEXT
;
CY10A	LDA #EOISND
	STA EOIFLG
	JMP CY20
CY15
	JSR PIBYTE
CY20
	JSR GIBYTE
	LDA #LRF
	JSR TSTFLG
	BEQ CY15
;
	JSR TYPFIL
	BEQ CY30
;
	JSR PIBYTE
CY30
	LDX F2PTR
;
	INX
	CPX F2CNT
	BCC CY10        ;MORE FILES TO COPY
	LDA #IWSA
	STA SA
	JMP CLSCHN      ;CLOSE COPY CHANNEL, FILE
;
OPIRFL
	LDX F2PTR
	LDA FILDRV,X
	AND #1
	STA DRVNUM
	LDA DIRTRK
	STA TRACK
	LDA ENTSEC,X
	STA SECTOR
	JSR OPNIRD
	LDX F2PTR
	LDA ENTIND,X
	JSR SETPNT
	LDX F2PTR
	LDA PATTYP,X
	AND #TYPMSK
	STA TYPE
;
	LDA #0
	STA REC
	JSR OPREAD
	LDY #1
	JSR TYPFIL
	BEQ OPIR10
	INY
OPIR10
	TYA
	JMP SETPNT
;
GIBYTE
	LDA #IRSA
	STA SA
GCBYTE
	JSR GBYTE
;
	STA DATA
	LDX LINDX
	LDA CHNRDY,X
	AND #EOISND
	STA EOIFLG
	BNE GIB20
;
	JSR TYPFIL
	BEQ GIB20
;
	LDA #LRF
	JSR SETFLG
GIB20	RTS
;
CYEXT	JSR SETDRN      ;COPY REL RECORDS
	JSR SSEND
	LDA SSIND
	PHA
	LDA SSNUM
	PHA
	LDA #IWSA
	STA SA
	JSR FNDWCH
	JSR SETDRN
	JSR SSEND
	JSR POSBUF
	LDA SSIND
	STA R1
	LDA SSNUM
	STA R0
	LDA #0
	STA R2
	STA RECPTR
	STA RELPTR
	PLA
	STA SSNUM
	PLA
	STA SSIND
	JMP ADDR1
;
.PAGE 'RENAME'
; RENAME FILE NAME IN DIRECTORY
RENAME	JSR ALLDRS      ;SET BOTH DRIVE #'S
	LDA FILDRV+1
	AND #1
	STA FILDRV+1
	CMP FILDRV
	BEQ RN10        ;SAME DRIVE #'S 
	ORA #$80        ;CHECK BOTH DRIVES FOR NAME
RN10	STA FILDRV
	JSR LOOKUP      ;LOOK UP BOTH NAMES
	JSR CHKIO       ;CHECK FOR EXISTENCE
	LDA FILDRV+1
	AND #1
	STA DRVNUM
	LDA ENTSEC+1
	STA SECTOR
	JSR RDAB        ;READ DIRECTORY SECTOR
	JSR WATJOB
	LDA ENTIND+1
	CLC             ;SET SECTOR INDEX
	ADC #3          ;...+3
	JSR SETPNT
	JSR GETACT
	TAY
	LDX FILTBL
	LDA #16
	JSR TRNAME      ;TRANSFER NAME
	JSR WRTOUT      ;WRITE SECTOR OUT
	JSR WATJOB
	JMP ENDCMD
.SKIP
.SKIP
; CHECK I/O FILE FOR EXIST
CHKIN
	LDA PATTYP+1    ;1ST FILE BEARS TYPE
	AND #TYPMSK
	STA TYPE
;
	LDX F2CNT
CK10	DEX
	CPX F1CNT 
	BCC CK20
	LDA FILTRK,X
	BNE CK10
	LDA #FLNTFD     ;INPUT FILE NOT FOUND
	JMP CMDERR
CK20
	RTS
;
CHKIO
	JSR CHKIN
CK25	LDA FILTRK,X
	BEQ CK30
	LDA #FLEXST
	JMP CMDERR
CK30	DEX
	BPL CK25
	RTS
.SKIP
.END
