	.page
	.subttl 'delete'
; delete a range of source
;
; syntax:   delete from# - to#

nrange
	jmp snerr
delete
	jsr errind	;allowed only in direct mode
	jsr chrgot
	beq nrange	;bad..no range parms.
	jsr range	;get line # range
	lda lowtr	;save it
	ldx lowtr+1
	sta index2
	stx index2+1
	jsr fndlin	;find it
	bcc del300	;skip if not found
	ldy #1
	jsr indlow	;at end of source ?
	dey
	tax		;save it in case of swap
	bne noteos
	jsr indlow
	beq del300	;both zero means end-of-source
noteos
	jsr indlow
	sta lowtr	;include to line #
	stx lowtr+1
del300
	lda index2	;check from#<to#
	sec
	sbc lowtr	;gen neg delta
	tax
	lda index2+1
	sbc lowtr+1
	tay
	bcs notdel	;no good
	txa
	clc
	adc vartab	;gen new end of source
	sta vartab
	tya
	adc vartab+1
	sta vartab+1
	ldy #0
del500
	jsr indlow	;move source down
	sta (index2),y
	iny
	bne del500
	inc lowtr+1
	inc index2+1
	lda vartab+1	;done one extra page
	cmp index2+1
	bcs del500	;no

notdel 			;entry for renumber also!
	jsr lnkprg	;fix links
	lda index1	;calc new end
	ldx index1+1
	clc
	adc #2
	sta vartab
	bcc not500
	inx
not500
	stx vartab+1
	jsr runc	 	;fix ptrs
	jmp ready		;say ready

;********************************
;*
;* input range parameters
;*
;********************************

range
	beq rng100	;a terminator from chrgot
	bcc rng100	;a number
	cmp #minutk	;a dash
	bne rngerr
rng100
	jsr linget	;get first #
	jsr fndlin	;find it & set ptrs
	jsr chrgot	;get last char
	beq rng200	;skip done
	cmp #minutk	;a dash
	bne rngerr	;syntax error
	jsr chrget	;yes - skip dash
	jsr linget	;get second #
	bne rngerr	;wasn't a number
rng200
	lda endchr	;was a # input?
	bne rngrts	;yes
	lda #$ff	;no - make max
	sta linnum
	sta linnum+1
rngrts
	rts

rngerr
	jmp snerr	;syntax error

;.end
