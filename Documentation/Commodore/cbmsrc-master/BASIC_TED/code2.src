	.page
	.subttl 'code2'
fndlin
	lda txttab
	ldx txttab+1
fndlnc
	ldy #1
	sta lowtr
	stx lowtr+1
	jsr indlow
	beq flinrt
	iny
	iny
	jsr indlow
	sta syntmp
	lda linnum+1
	cmp syntmp
	bcc flnrts
	beq fndlo1
	dey
	bne affrts
fndlo1
	dey
	jsr indlow
	sta syntmp
	lda linnum
	cmp syntmp
	bcc flnrts
	beq flnrts
affrts
	dey
	jsr indlow
	tax
	dey
	jsr indlow
	bcs fndlnc
flinrt
	clc
flnrts
	rts
	

scrath
	bne flnrts
scrtch
	lda #0
	tay
	sta (txttab),y
	iny
	sta (txttab),y
	sta trcflg	;reset trace flag
	lda txttab
	clc
	adc #2
	sta vartab
	lda txttab+1
	adc #0
	sta vartab+1

runc
	jsr stxtpt	;load (txtptr) with (txttab)-1
	lda #0
clear
	bne stkrts
clearc
	jsr clall	;close all files
	ldy #0
	sty dsdesc	;flag 'no ds string'
	dey		;y=$ff
	sty trapno+1    ;flag no current trap line
	sty errlin	;reset last error pointers
	sty errlin+1
	sty errnum
	

	lda memsiz
	ldy memsiz+1
	sta fretop
	sty fretop+1
	

	lda #<stkbot
	ldy #>stkbot
	sta tos
	sty tos+1
	

	lda vartab
	ldy vartab+1
	sta arytab
	sty arytab+1
	sta strend
	sty strend+1

	ldx #pumony-puchrs 	;reset print using chars
clrpu
	lda pudefs,x
	sta puchrs,x
	dex
	bpl clrpu
fload
	jsr rstor3	;reset pointer for data statements
stkini
	ldx #tempst
	stx temppt
	pla
	tay
	pla
	ldx #stkend-257
	txs
	pha
	tya
	pha
	lda #0
	sta oldtxt+1
	sta subflg
stkrts
	rts		;must return with .a=0



pudefs .byte ' ,.$'	;default:  fill, comma, dec pnt, money symbol

stxtpt
	clc
	lda txttab
	adc #$ff
	sta txtptr
	lda txttab+1
	adc #$ff
	sta txtptr+1
	rts

;.end
