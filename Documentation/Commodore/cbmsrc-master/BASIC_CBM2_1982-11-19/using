.PAG 'PRINT USING'
; USING
;
; ENTERED BY CMD, PRINT, OR PRINT#
; SAMPLE SYNTAX:   PRINT USING"****";A;B;C
;
USING	LDX #$FF
	STX ENDFD
	JSR CHRGET
	JSR FRMEVL      ;EVALUATE FORMAT STRING
	JSR CHKSTR      ;MUST BE STRING TYPE...
;SAVE TEMP DESCRIPTOR
	LDA FACMO
	PHA
	LDA FACMO+1
	PHA
	LDA FACMO+2
	PHA
	STA I6509
	LDY #3          ;GET 4 BYTES FROM (FACMO)
	LDA (FACMO)Y    ;START WITH THE BANK NUMBER
	TAX             ;KEEP IT HANDY-WE WILL NEED IT SOON
	DEY             ;SET UP FOR REMAINING 3 BYTES
LDFORM	STA FORM,Y      ;COPY (FACMO),N TO FORM,N-1
	LDA (FACMO)Y
	DEY
	BPL LDFORM
	STA LFOR        ;(FACMO),0 IS LENGTH - NOT STORED WITH FORM
	STX I6509       ;NOW USE (FACMO),3 AS NEW INDIRECT
	TAY
	BEQ SER         ;ERROR IF LENGTH 0
CNCJ	DEY
	LDA (FORM)Y
	CMP #'#         ;AT LEAST ONE # IN FORMAT?
	BEQ CSCOL       ;YES...
	TYA             ;NO...END OF FORMAT?
	BNE CNCJ        ;NO...
SER	JMP SNERR       ;YES...SYNTAX ERROR
CSCOL	LDA #';
EEX2	JSR SYNCHR      ;CHECK CHARACTER
	STY NOZE        ;CLEAR FLAG FOR ANAF
	STY BNR         ;SET POINTER TO BEGIN OF NO
	JSR FRMEVL      ;EVALUATE EXPRESSION
	BIT VALTYP      ;NUMERIC OR STRING?
	BPL CONV        ;BRANCH IF NUMERIC
	JSR INI         ;INIT COUNTERS AND FLAGS
	JSR ANAF        ;ANALYZE FORMAT
	LDX CHSN        ;> OR = IN FORMAT FIELD
	BEQ PRCHA       ;BRANCH IF NOT
	LDX #$00
	SEC
	LDA CFORM
	SBC HULP        ;.A=ROOM LEFT IN FIELD
	BCC PRCHA       ;BRANCH IF NO ROOM LEFT
	LDX #'=
	CPX CHSN        ;= IN FIELD
	BNE SCHS1       ;BRANCH IF NOT
	LSR A           ;.A=.A/2
	ADC #$00        ;ADD 1 IF ODD
SCHS1	TAX             ;STORE NO OF BLANKS IN X
PRCHA	LDY #$00
CHX	TXA
	BEQ CPEF        ;BRANCH IF NO BLANKS
	DEX
OBLK	LDA #$20        ;OUTPUT A BLANK
	BNE OUTC        ;ALWAYS
CPEF	CPY HULP        ;END OF STRING REACHED?
	BCS OBLK        ;OUTPUT BLANK IF YES
	JSR SAV12
	INY
OUTC	JSR CDOUT       ;OUTPUT CHARACTER
	BNE CHX         ;BRANCH IF NOT READY
	JMP REAY
CONV	JSR FOUT        ;CONVERT MFP TO DECIMAL
	JSR FOUTST      ;BUILD DESCRIPTOR FOR FOUT STRING
	JSR INI         ;INIT COUNTERS AND FLAGS
	JSR FFORM       ;OUTPUT ONE FORMATTED NUMBER
REAY	JSR CHRGOT      ;GET OLD CHARACTER
	CMP #',         ;COMMA?
	BEQ EEX2        ;CONTINUE PRINT USE IF YES
	SEC
	ROR NOZE        ;SET FLAG FOR ANAF
	JSR ANAF        ;PRINT REST OF FORMAT
	PLA             ;RESTORE DESCRIPTOR
	TAX
	PLA
	TAY
	PLA
	JSR MAPTXT
	JSR FRETMP
	JSR CHRGOT
	CMP #'; ;SEMI-COLON?
	BEQ NOCR        ;BRANCH IF YES
	JMP OCRLF       ;END OF PRINT USING
NOCR	JMP CHRGET      ;RTS TO BASIC
SAV12	JSR MAPINX
	LDA (INDEX)Y
	JMP MAPTXT
.PAG
;FFORM
;
;OUTPUT A NUMBER TO FORMAT
;
;NUMBER IS IN FBUFFR...FORMAT CHECKED BY ANAF
;
FFORM	LDA PUFILL
	STA BLFD        ;SET WORKING FILL CHAR
	LDA #$FF
ANA	STA SNO         ;SAVE BLANK OR - IN SNO
	JMP INSY
STP	STX POINT       ;SET POINT POINTER
INSY	CPY HULP        ;END OF NO REACHED?
	BEQ EOA         ;BRANCH IF YES
	LDA FBUFFR,Y    ;GET CHARACTER OF NO
	INY
	CMP #$20        ;BLANK?
	BEQ INSY        ;YES...SPAN
	CMP #'-         ;SIGN NO NEGATIVE
	BEQ ANA         ;YES...
	CMP #'.         ;DECIMAL POINT?
	BEQ STP         ;YES...
	CMP #'E         ;IS CHAR 'E'?
	BEQ LSG         ;YES...
	STA FBUFFR,X    ;MOVE NUMBER
	STX ENR         ;UPDATE END-NO POINTER
	INX
	BIT POINT       ;POINT POINTER SET?
	BPL INSY        ;YES...
	INC VN          ;COUNT DIGITS BEFORE POINT
	JMP INSY
LSG	LDA FBUFFR,Y
	CMP #'-         ;SIGN OF EXPONENT NEGATIVE
	BNE NOMN        ;NO...
	ROR USGN        ;MAKE SIGN NEGATIVE
NOMN	INY
	STY UEXP        ;SET EXPONENT POINTER
EOA	LDA POINT       ;DECIMAL FOUND?
	BPL RTTS        ;YES...
	STX POINT       ;NO...ADD POINT
RTTS	JSR ANAF        ;ANALYZE FORMAT
	LDA VF
	CMP #$FF
	BEQ ERST        ;FIELD OVERFLOW
	LDA FESP        ;EXPONENT IN FIELD
	BEQ CFF         ;CONVERT TO F FORMAT IF NOT
	LDA UEXP        ;EXPONENT IN NUMBER?
	BNE ETE         ;YES...
	LDX ENR
	JSR ET2         ;ADD EXPONENT TO NUMBER
	DEC FBUFFR+2,X
	INX
	STX UEXP
	JSR ALG         ;DELETE LEADING ZEROS
	BEQ HUP         ;ALL ZERO
ETE	LDY POSP        ;+ OR - IN FORMAT?
	BNE SSWE        ;YES...
	LDY SNO         ;+?
	BMI SSWE        ;YES...
	LDA VF
ERST	BEQ ERRF        ;NO ROOM FOR SIGN
	DEC VF          ;RESERVE ROOM
	BNE RSPA
	LDA NF          ;ONE #?
	BEQ ERRF        ;YES...ERROR
RSPA	INC SWE
SSWE	JSR SHPN        ;SHIFT DECIMAL POINT
	JSR UROUND      ;ROUND NUMBER
	JSR SHPN        ;SHIFT AGAIN IF NECESSARY
HUP	JMP CHOUT       ;OUTPUT NUMBER
CFF	LDY UEXP        ;EXPONENT IN NO?
	BEQ FTF         ;NO...
	STA HULP        ;DELETE EXPONENT
	SEC             ;ADJUST DECIMAL POINT
	ROR ETOF        ;SET E-TO-F FLAG
	LDY POINT
	LDA USGN        ;EXEC NOS3 OR NOS4
	BPL NO4         ;DEPENDS ON SIGN OF EXP
	JSR NOS3
	JMP RNDD
NO4	JSR NOS4
FTF	LDY POINT       ;AT START OF NO?
	BEQ RNDD        ;YES...
	JSR CHO         ;NO = 0 ?
	BEQ DEVN        ;YES...NO ROUND
RNDD	JSR UROUND
	JMP DEVN2
DEVN	DEC VN          ;ADJUST...NO WAS 0
DEVN2	SEC
	LDA VF
	SBC VN
	BCC ERRF        ;NO FIT...ERROR
	STA SWE         ;SAVE DIFFERENCE
	LDY POSP        ;+ OR -?
	BNE AHP         ;YES...
	LDY SNO         ;GET SIGN
	BMI AHP         ;POSITIVE...
	TAY
	BEQ ERRF        ;NO ROOM FOR SIGN
	DEY
	BNE LDVN        ;SWE<>1
	LDA NF
	ORA VN          ;BOTH ZERO?
	BNE HUP         ;NO...
ERRF	LDA #'*         ;FORMAT ERROR
STOUT	JSR CDOUT       ;FILL FIELD WITH STARS
	BNE STOUT
	RTS
AHP	TAY             ;OUTPUT NO IF SWE=0
	BEQ HUP
LDVN	LDA VN
	BNE HUP         ;VN<>0
	DEC SWE         ;ADD EXTRA 0
	INC NOZE        ;BEFORE DECIMAL POINT
	JMP HUP
.PAG
; SHPN
;
; SHIFT DECIMAL POINT
;
SHPN	SEC
	LDA VF
	SBC VN
	BEQ RDY         ;FORMAT O.K
	LDY POINT
	BCC PNTL        ;VF<VN
	STA HULP
INCY	CPY ENR         ;END OF NO REACHED?
	BEQ ITY
	BCS NOS1        ;YES...
ITY	INY
NOS1	INC VN
NOS4	JSR EADO        ;ADJUST EXPONENT
	DEC HULP        ;READY?
	BNE INCY        ;NO...
	JMP POIT
PNTL	EOR #$FF
	ADC #$01
	STA HULP        ;=VN-VF
DECY	CPY BNR         ;BEGIN OF NO?
	BEQ INZ         ;YES...
	DEY
	DEC VN
	JMP INZ2
INZ	INC NOZE        ;ADD LEADING ZEROS
INZ2	LDA #$80
NOS3	JSR EADJ        ;ADJUST EXPONENT
	DEC HULP        ;READY?
	BNE DECY        ;NO...
POIT	STY POINT       ;DECIMAL POINT POINTER
RDY	RTS
.PAG
; EAXP
;
; ADJUST EXPONENT
;
SEXP	BNE RETRN       ;NO OVER/UNDERFLOW
	EOR #$09        ;.A IS 0 OR 9
	STA FBUFFR,X DIGIT BACK IN EXP
	DEX             ;= 0 OR 9
	CPX UEXP
	RTS
EADO	LDA #$00
EADJ	LDX UEXP
	INX
	BIT ETOF        ;E-TO-F FLAG ON?
	BMI TAG2        ;YES...
	EOR USGN
	BEQ TAG2        ;++ OR --
TAG1	JSR TAG3        ;INC EXP, OVERFLOW?
	JSR SEXP        ;DIGIT 0 IF YES
	BCS TAG1        ;TRY SECOND DIGIT
	JMP OVERR       ;EXP>99
TAG2	LDA FBUFFR,X
	DEC FBUFFR,X    ;DECREMENT EXP
	CMP #'0         ;UNDERFLOW ON DIGIT?
	JSR SEXP        ;SET DIGIT=9 IF YES...
	BCS TAG2        ;TRY 2ND DIGIT
	BIT ETOF        ;FLAG OFF?
	BPL ET3         ;YES...
	STY POINT       ;DECIMAL POINT POINTER
RETRN	PLA
	PLA
	RTS             ;RETURN TO FFORM/MAIN
ET3	LDA USGN
	EOR #$80        ;REVERSE SIGN EXP
ET2	STA USGN
	LDA #'0
	STA FBUFFR+1,X  ;EXP<0 HERE
	LDA #'1
	STA FBUFFR+2,X
RETS	RTS
TAG3	LDA FBUFFR,X    ;GET DIGIT OF EXP
	INC FBUFFR,X    ;INCREMENT DIGIT
	CMP #'9         ;OVERFLOW
	RTS             ;RETURN .Z SET
.PAG
; ANSUB : LOAD FORMAT FIELD CHAR IN .A
;
ANSUB	CLC
	INY             ;BEGIN FORMAT?
	BEQ ANS010      ;YES...
	CPY LFOR        ;END?
	BCC ANS020      ;NO...
ANS010	LDY NOZE        ;<>0?
	BNE RETRN       ;YES...
ANS020	JSR SAV1        ;SET UP TO UPDATE BANK #
	INC CFORM       ;POINTER TO FIELD
	RTS
SAV1	LDA FORM+2
	STA I6509
	LDA (FORM)Y
	JMP MAPTXT
.SKI 4
;INI : INIT COUNTERS AND FLAGS
;
INI	JSR FREFAC      ;FREE TEMP DESCRIPTOR
	STA HULP        ;LENGTH STRING
	LDX #$0A        ;PRINTED IN HULP
	LDA #$00
STZ	STA SWE,X       ;INIT WORKING REGISTERS
	DEX
	BPL STZ
	STX FLAG        ;COMMA FLAG =FF
	STX POINT       ;POINT POINTER=FF
	STX DOLR        ;DOLLAR FLAG=FF
	TAX             ;X=Y=0
	TAY
	RTS
.PAG
; ROUND NUMBER
;
UROUND	CLC
	LDA POINT
	ADC NF          ;OVERFLOW?
	BCS RRTS        ;YES...
	SEC
	SBC NOZE        ;UNDERFLOW?
	BCC RRTS        ;YES...
	CMP ENR         ;ANYTHING TO ROUND?
	BEQ CBN         ;YES...
	BCS RRTS        ;NO...
CBN	CMP BNR         ;AGAIN...
	BCC RRTS        ;NO...
	TAX
	LDA FBUFFR,X    ;GET DIGIT
	CMP #'5         ;<5 ?
ROUN	BCC RRTS        ;YES...NO ROUND
CON1	CPX BNR         ;BEGIN OF NO REACHED?
	BEQ ADD1        ;YES..ADD 1
	DEX
	JSR TAG3        ;INCREMENT DIGIT
	STX ENR         ;NEW END OF NO POINTER
	BEQ CON1        ;BRANCH ON OVERFLOW
	RTS
ADD1	LDA #'1
	STA FBUFFR,X
	INX
	STX POINT
	DEC NOZE        ;# OF 0'S BEFORE .
	BPL RRTS        ;NO UNDERFLOW
	INC NOZE        ;UNDERFLOW...BACK TO 0
	INC VN
RRTS	RTS
.PAG
; ALG : DELETE LEADING ZEROS OF NO
;
ALG	LDY POINT       ;START WITH A .?
	BEQ SZER        ;YES...
CHO	LDY BNR
CMO	LDA FBUFFR,Y    ;START WITH A 0?
	CMP #'0
	RTS
NBR	INC POINT       ;ADJUST POINT
	JSR EADO        ;AND EXPONENT
	INC BNR         ;AND POINTER TO BEGIN OF NO
	CPY ENR         ;END OF NUMBER?
	BEQ RRTS        ;YES...
	INY
SZER	JSR CMO         ;ZERO IN NO?
	BEQ NBR         ;YES...
	RTS             ;NO...
.SKI 4
; CHOUT : PRINT NUMBER
;
LZER	CPX ENR         ;END OF # REACHED?
	BEQ ZOUT        ;YES...OUTPUT ZERO
TELX	INX
	LDA FBUFFR,X    ;GET DIGIT
	.BYT $2C        ;BRANCH AWLAYS
ZOUT	LDA #'0         ;OUTPUT ZERO
OUTS	LSR FLAG        ;CLEAR COMMA FLAG
OUT	JSR CDOUT       ;OUTPUT CHARACTER
	BNE AFRM        ;NOT READY...
	RTS
.SKI 4
CHOUT	LDA DOLR        ;DOLLAR FLAG SET?
	BMI CHOU        ;NO...
	INC NOZE        ;MAKE ROOM FOR $
CHOU	LDX BNR         ;START OF #
	DEX
	LDY BEGFD       ;BEGIN OF FIELD
AFRM	JSR SAV1
	INY
	CMP #',         ;COMMA?
	BNE PUNT        ;NO...
	BIT FLAG        ;COMMA FLAG ON?
	BMI BOUT        ;YES, DO A FILL CHAR
	LDA PUCOMA
	JMP OUT         ;NO,...OUTPUT A COMMA CHAR
BOUT	LDA BLFD
	JMP OUT
PUNT	CMP #'.         ;DECIMAL POINT?
	BNE AFPLUS
	LDA PUDOT       ;YES...
	JMP OUT
AFPLUS	CMP #'+         ;PLUS?
	BEQ ISPL        ;YES...
	CMP #'-         ;MINUS?
	BEQ ISPL1       ;YES...
	CMP #'^         ;UP ARROW?
	BNE PNDD        ;NO...
	LDA #'E
	JSR CDOUT
	LDY UEXP
	JSR CMO         ;FIRST DIG OF EXP ZERO?
	BNE MINT        ;NO...
	INY
	JSR CMO         ;SECOND DIGIT?
	BEQ POST        ;YES
MINT	LDA #'-
	BIT USGN
	BMI MOUT
POST	LDA #'+
MOUT	JSR CDOUT       ;OUTPUT SIGN EXP
	LDX UEXP
	LDA FBUFFR,X
	JSR CDOUT       ;OUTPUT FIRST DIG EXP
	LDY ENDFD
	JMP TELX
ISPL1	LDA SNO         ;POSITIVE?
	BMI BOUT        ;YES...OUT BLANK OR *
ISPL	LDA SNO         ;OUTPUT SGN
	JMP OUT
ZEROT	LDA NOZE        ;# OF ZEROS
	BNE *+5
	JMP LZER
	DEC NOZE        ;COUNT LEADING ZEROS
	LDA DOLR        ;$ FLAG SET?
	BPL *+5
	JMP ZOUT        ;NO...OUTPUT ZERO
	SEC
	ROR DOLR        ;CLEAR DOLLAR FLAG
	LDA PUMONY
	JMP OUTS        ;OUTPUT MONEY SYMBOL
PNDD	LDA SWE         ;# OF BLANKS
	BEQ ZEROT
	DEC SWE         ;COUNT !
PNDX	BNE BOUT        ;OUT BLANK OR *
	LDA POSP        ;+ OR - IN FIELD?
	BMI PNDX        ;YES...OUT BLANK OR *
TAT	JSR SAV1
	CMP #',         ;COMMA?
	BNE ISPL1       ;NO...OUT SIGN
	LDA BLFD        ;YES...
	JSR CDOUT       ;OUT BLANK OR *
	INY
	JMP TAT
CDOUT	JSR OCHR        ;CHAR OUT
	DEC CFORM       ;COUNT IT
	RTS
.PAG
; ANAF :ANALYZE FORMAT
;
ANAF	LDY ENDFD       ;FORMAT POINTER
GFOR	JSR ANSUB
	JSR COMP        ;CHECK SPECIAL CHARS
	BNE PCHAR       ;NO MATCH...
	STY BEGFD       ;SAVE BEGINNING OF FIELD
	BCC FFOUN       ;# WAS FOUND
	TAX             ;SAVE CHAR
SFUR	JSR ANSUB       ;GET NEXT FORMAT CHAR
	BCS NONO        ;STOP ON WRAP-AROUND
	JSR COM1        ;COMPARE SPECIALS
	BEQ FOUN1       ;FOUND SOME...
NONO	LDY BEGFD
	TXA
PCHAR	JSR OCHR        ;OUT CHARACTER
	JMP GFOR
FOUN1	BCS SFUR        ;NO #...KEEP LOOKING
	LDY BEGFD
FFOUN	LDX NOZE
	BEQ *+5
	JMP TRTS
	STX CFORM
	DEY
HYO2	DEC CFORM       ;CORRECT COUNTER
HYO	JSR ANSUB       ;GET NEXT FORMAT CHAR
	BCS EFO         ;END OF FORMAT
	CMP #',         ;COMMA?
	BEQ HYO         ;YES...IGNORE IT
	JSR ISP         ;+ OR - IN FORMAT?
	BCC HYO2        ;YES...
	CMP #'.         ;DECIMAL POINT?
	BNE AVF1        ;NO...
	INX
	CPX #2          ;MORE THAN 1 DECIMAL?
	BCC HYO         ;NO...
ERO	JMP SNERR       ;YES...SYNTAX ERROR
AVF1	JSR COM2        ;=, >, OR # IN FIELD
	BNE LLAR        ;NO...
	BCC HYO1        ;WAS #
	STA CHSN        ;WAS > OR =
HYO1	INC VF,X
	JMP HYO
LLAR	CMP #'$         ;DOLLAR?
	BNE EXPO        ;NO...
	BIT DOLR        ;TEST FLAG
	BPL HYO1        ;NO...
	CLC
	ROR DOLR        ;SET FLAG
	DEC VF
	JMP HYO1
EXPO	CMP #'^         ;UP ARROW?
	BNE ISP         ;NO...
	LDX #$02
CHKK	JSR ANSUB       ;MUST BE 4 UP ARROWS
	BCS ERO
	CMP #'^         ;UP ARROW?
	BNE ERO
	DEX
	BPL CHKK
	INC FESP        ;SET EXP FLAG
	JSR ANSUB       ;NEXT FORMAT CHAR
	BCS EFO         ;END OF FORMAT
ISP	CMP #'+         ;PLUS?
	BNE CHOM        ;NO...
	LDA SNO         ;SIGN NEG?
	BPL SPOS        ;YES...
	LDA #'+
	STA SNO
SPOS	LDA POSP        ;+ OR - ALREADY?
	BNE ERO
	ROR POSP        ;MAKE POSP NEG/CLC
	STY ENDFD       ;END FIELD POINTER
	INC CFORM       ;CORECT COUNTER
TRTS	RTS
CHOM	CMP #'-         ;MINUS?
	BEQ SPOS        ;YES...
	SEC
EFO	STY ENDFD       ;END FIELD POINTER
	DEC ENDFD
	RTS
.PAG
; COMP :COMPARE .A WITH SYMBOLS
;
COMP	CMP #'+
	BEQ RT
	CMP #'-
	BEQ RT
COM1	CMP #'.
	BEQ RT
COM2	CMP #'=
	BEQ RT
	CMP #'>
	BEQ RT
	CMP #'#
	BNE RT
	CLC
RT	RTS
.END
