.PAGE   'INTERFACE SYSTEM CALLS.'
;
STORST
	CLC
	JMP KRDST       ;SET STATUS
;
READST
	SEC
	JMP KRDST       ;RETURN I/O STATUS BYTE
;
KTRANS
	SEC
	BCS OPN10       ;TRANSMIT CMD TO DEVICE
;
OPEN
	CLC
OPN10	JSR KOPEN       ;OPEN LOGICAL FILE
	BCS IOERR
	RTS
;
GETIN
	JSR KGETIN      ;GET A BYTE
	BCS IOERR
	RTS
;
BASIN
	JSR KBASIN      ;GET A BYTE
	BCS IOERR
	RTS
;
BSOUT
	JSR KBSOUT      ;SEND A BYTE
	BCS IOERR
	RTS
;
CHKIN
	JSR KCHKIN      ;OPEN CHANNEL IN
	BCS IOERR
	RTS
;
CHKOUT
	JSR KCHOUT      ;OPEN   -     OUT
	BCS IOERR
	RTS
;
LOAD
	JSR KLOAD       ;LOAD FROM FILE
	BCS IOERR
	RTS
;
SAVE
	JSR KSAVE       ;SAVE TO    -
	BCS IOERR
	RTS
;
TCLOSE
	CLC
	JMP KCLOSE
;
CLOSE
	SEC
	JMP KCLOSE
;
CLALL
	CLC             ;C-CLR => CLOSE 'EM ALL
	JMP KCLALL      ;CLOSE ALL FILES
.SKIP 4
;
; IOERR - .A = ERROR NUMBER
;         CY = 1
;         PRINT ERROR MESSAGE AND GO TO READY.
;
IOERR	CLC
	ROL A
	PHA
	LDA #DOSLFN     ;CLOSE CMD CHANNEL IN CASE
	JSR TCLOSE
	PLA
	TAX
	JMP ERROR
.SKI 4
ENDPRG
.END
