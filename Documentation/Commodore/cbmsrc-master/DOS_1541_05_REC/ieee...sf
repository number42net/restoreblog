.PAG 'IEEE...SF'
;ATN IRQ PROCESS
; IRQ ON ATN, LISTEN TO PET
; CLEAR STACK 
.SKI 3
ATNIRQ
	LDA IEEED       ;CLR ATNIRQ
	LDA #1
	STA ATNPND
	RTS
ATNSRV
	SEI
	LDA #0
	STA ATNPND
	LDX #TOPWRT
	TXS
	LDA #DAV+EOI+NDAC
	ORA PB          ;FREE CONTROL LINES
	AND #$FF-TRSEL-NRFD
	STA PB
	LDA #0
	STA DDRA1
	LDA #RDD        ;SET RECEIVE DIR
	STA DDRB1
	LDA #$FF
	STA IEEED       ;FREE DATA LINES
ATN10
	LDA PB
	AND #$FF-NDAC
	ORA #NRFD+ATNA
	STA PB
ATN20	BIT PB
	BVC ATN30       ;DAV LO
	BMI ATN20       ;ATN LO ATNI HI
	BPL ATN50       ;ATN HI
ATN30	LDA #$FF-NRFD   ;NRFD LO
	AND PB
	STA PB
	AND #EOI
	STA EOIFLG      ;SAVE EOI
	LDA IEEED
	EOR #$FF
	STA ICMD        ;SAVE COMMAND
	LDA #NDAC       ;NDAC HI
	ORA PB
	STA PB
DCDE	LDY #0
	LDA ICMD
	AND #%01100000
	CMP #$40        ;TALK?
	BEQ DCDE60
	CMP #$20        ;LISTEN?
	BEQ DCDE20
	CMP #$60        ;SECONDARY?
	BEQ DCDE70
	BNE DCDE80      ;OTHER
DCDE20	LDA ICMD
	CMP LSNADR
	BEQ DCDE40      ;MY LISTEN ADDRESS
	CMP #UNLSN
	BNE DCDE30
	STY LSNACT
DCDE30	STY ADRSED      ;NOT PRIMARY ADDRSED
	JMP DCDE80
DCDE40	STA LSNACT
	STY TLKACT
DCDE50	LDA #32
	STA SA          ;DEFAULT SA
	STA ORGSA
	STA ADRSED      ;PRIMARY ADDRESSED
	BNE DCDE80
DCDE60	STY TLKACT
	LDA ICMD
	CMP TLKADR
	BNE DCDE30
	STA TLKACT
	STY LSNACT
	BEQ DCDE50
DCDE70	LDA ADRSED
	BEQ DCDE80      ;NOT ADDRESSED
	LDA ICMD
	STA ORGSA
	PHA
	AND #$F
	STA SA
	PLA
	AND #$F0        ;CLOSE?
	CMP #$E0
	BNE DCDE80
	CLI
	JSR CLOSE
	SEI
DCDE80
ATN40	BIT PB
	BVC ATN40
	JMP ATN10
ATN50	LDA LSNACT
	BEQ ATN60
	LDA #$FF-NRFD-ATNA
	AND PB
	STA PB
	JSR LISTEN
	JMP IDLE
ATN60	LDA #$FF-ATNA-NDAC
	AND PB
	ORA #NDAC
	STA PB
	LDA TLKACT
	BEQ ATN70
	JSR TALK
ATN70	JMP IDLE
.SKI 5
LISTEN
	SEI
	LDA #NRFD       ;RFD: HI
	ORA PB
	STA PB
LSN10	BIT PB          ;DAV: LO
	BMI NOLATN      ;ATN IS NOW HIGH
	BVS LSN10
	JSR FNDWCH      ; WAS LDX SA
	BCS LSN15
.SKIP
	LDA CHNRDY,X
	ROR A           ;OK, OPEN FOR LISTEN
	BCS LSN30
LSN15	LDA ORGSA       ; WAS TXA
	AND #$F0        ;SA=OPEN?
	CMP #$F0
	BEQ LSN30
LSN20	LDA SA
	CMP #1
	BEQ LSN25
LSN21	BIT PB
	BMI NOLATN      ;ATN IS NOW HIGH
	BVC LSN21
	LDA #$FF-NDAC
	AND PB
	STA PB
	RTS
.SKIP
NOLATN
	JMP ATNSRV
LSN25
	SEI
	LDA #$FF-NRFD   ; ACCEPT ALL DATA
	AND PB
	STA PB          ;RFD LOW
	LDA #NDAC
	ORA PB          ; DAC HI
	STA PB
.SKIP
LSN26	BIT PB          ; DAV HI
	BMI NOLATN      ;ATN IS NOW HIGH
	BVC LSN26
	LDA PB
	AND #$FF-NDAC   ; DAC LOW
	STA PB
	LDA #NRFD       ; RFD HI
	ORA PB
	STA PB
LSN28	BIT PB          ; WAIT DAV LOW
	BMI NOLATN      ;ATN IS NOW HIGH
	BVC LSN28
	JMP LSN25       ; DO UNTIL ATN PULLED
LSN30	LDA #$FF-NRFD
	AND PB
	STA PB
	AND #EOI
	STA EOIFLG
	LDA IEEED
	EOR #$FF
	STA DATA
	SEI
	LDA #NDAC
	ORA PB
	STA PB
LSN40	BIT PB
	BMI NOLATN      ;ATN IS NOW HIGH
	BVC LSN40
	LDA #$FF-NDAC
	AND PB
	STA PB
	CLI
	JSR PUT
LSTRTN
	JMP LISTEN
.SKI 3
TLK25
	LDA #DAV+EOI
	ORA PB
	STA PB
	JMP IDLE
TALK
	SEI
	JSR FNDRCH
	BCS NOTLK       ; TEST IF CHANNEL READY
TALK1	LDX LINDX
	LDA CHNRDY,X
	BMI TLK05
NOTLK	RTS
TLK05
	LDA #TDD
	STA DDRB1
	LDA #$FF
	STA DDRA1
	LDA PB
	ORA #TRSEL
	STA PB
;
	LDA #NRFD
TLK10	BIT PB          ;RFD: HI
	BMI NOTATN      ;ATN IS NOW HIGH
	BEQ TLK10
	LDA CHNDAT,X
	EOR #$FF
	STA IEEED
	LDA CHNRDY,X
	ORA #$FF-EOI-DAV ;DAV: LO
	AND PB
	STA PB
TLK20
	LDA PB
	BMI NOTATN      ;ATN IS NOW HIGH
	AND #NRFD+NDAC
	CMP #NRFD+NDAC
	BEQ TLK25
	AND #NRFD
	BNE TLK20
TLK30
	CLI
	JSR GET
	SEI
	LDA #NDAC
TLK35	BIT PB
	BMI NOTATN      ;ATN IS NOW HIGH
	BEQ TLK35
TLKRTN	LDA #$FF
	STA IEEED
	LDA #DAV+EOI
	ORA PB
	STA PB
	LDA #NDAC
TLK40	BIT PB
	BMI NOTATN      ;ATN IS NOW HIGH
	BNE TLK40
	BEQ TALK1
;
NOTATN
	JMP ATNSRV
;
ITERR	;IEEE TALKER ERROR RECOVERY
	LDA PB
	ORA #DAV
	STA PB
	RTS
;
;
ILERR	;IEEE LISTENER ERROR RECOVERY
	LDA #NRFD
	ORA PB
	AND #$FF-ATNA
	STA PB
	RTS
;
.END
