.PAGE 'LCC.INIT'
;
;
;
;     INITIALIZATION OF CONTROLLER
;
;
;
CNTINT	LDA #%01101111  ; DATA DIRECTION
	STA DDRB2
	AND #$FF-$08-$04-$03 ; TURN MOTOR OFF,SET PHASE A, LED OFF
	STA DSKCNT
;
;
	LDA PCR2        ; SET EDGE AND LATCH MODE
	AND #$FF-$01    ; NEG EDGE PLEASE
;
;
;  CA2: SOE OUTPUT HI DISABLE S.O. INTO 6502
;
	ORA #$0E
;
;
; CB1 INPUT ONLY
;
; CB2 MODE CONTROL R/W
;
	ORA #$E0
	STA PCR2
;
;
	LDA #$41        ; CONT IRQ, LATCH MODE
	STA ACR2
;
	LDA #0
	STA T1LL2
	LDA #TIM        ; 20 MS /IRQ
	STA T1HL2
	STA T1HC2       ; GET 6522'S ATTENTION
;
	LDA #$7F        ; CLEAR ALL IRQ SOURCES
	STA IER2
;
	LDA #$80+$40
	STA IFR2        ; CLEAR BIT
	STA IER2        ; ENABLE IRQ
;
;
	LDA #$FF        ; NO CURRENT DRIVE
	STA CDRIVE
	STA FTNUM       ; INIT FORMAT FLAG
;
	LDA #$08        ; HEADER BLOCK ID
	STA HBID
;
	LDA #$07        ; DATA BLOCK ID
	STA DBID
;
	LDA #<INACT
	STA NXTST
	LDA #>INACT
	STA NXTST+1
;
	LDA #200
	STA MINSTP
;
	LDA #4
	STA AS
;
	LDA #$4
	STA AF
;
.END
