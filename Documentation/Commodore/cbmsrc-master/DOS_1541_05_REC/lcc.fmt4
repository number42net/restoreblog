.PAGE 'LCC.FMT4'
;
;
;
SYNCLR	LDA PCR2        ; WRITE ENTIRE TRACK WITH SYNC
	AND #$FF-$E0
	ORA #$C0
	STA PCR2
;
	LDA #$FF        ; OUTPUT MODE DDR
	STA DDRA2
;
	STA DATA2       ; SYNC CHAR
;
	LDX #$28        ; $28*256 BYTES
	LDY #0
;
SYC10	BVC *
	CLV
;
	DEY
	BNE SYC10
;
	DEX
	BNE SYC10
;
	RTS             ; LEAVE WRITE ON
;
;
;
WRTNUM	LDX NUM         ; WRITE OUT NUM BYTES
	LDY NUM+1
;
WRTN10	BVC *
	CLV
;
	DEX
	BNE WRTN10
;
	DEY
	BPL WRTN10
;
	RTS
;
;
;
FMTERR	DEC CNT         ; TEST FOR RETRY
	BEQ FMTE10
;
	JMP END
;
FMTE10
;
	LDY #$FF
	STY FTNUM       ; CLEAR FORMAT
;
	INY
	STY GCRFLG
;
	JMP ERRR
;
;
;
MOVUP	LDA BUFF0,Y     ;  MOVE UP 69 BYTES
	STA BUFF0+69,Y  ; MOVE FROM TOP DOWN
	DEY
	BNE MOVUP
;
	LDA BUFF0       ; DO LAST BYTE
	STA BUFF0+69
	RTS
;
;
;
;
MOVOVR	LDY #68         ; MOVE OVRBUF INTO (BUFFER)
;
MOVO10	LDA OVRBUF+256-TOPWRT,Y
	STA (BUFPNT),Y
;
	DEY
	BPL MOVO10
;
	RTS
;
;
;
KILL	LDA PCR2        ; DISABLE WRITE
	ORA #$E0
	STA PCR2
	LDA #$00        ; MAKE PORT INPUT NOW
	STA DDRA2
;
	RTS
;
;
;
CLEAR	LDA PCR2        ; ENABLE WRITE
	AND #$FF-$E0
	ORA #$C0
	STA PCR2
;
	LDA #$FF        ; MAKE PORT AN OUTPUT
	STA DDRA2
;
	LDA #$55        ; WRITE A 1F PATTERN
	STA DATA2
;
	LDX #$28        ; $28*256 CHARS
	LDY #00
CLER10	BVC *
	CLV
	DEY
	BNE CLER10
;
	DEX
	BNE CLER10
;
	RTS
;
;*****************************
;*
;*
;*     FBTOG
;*     FORMAT BINARY TO GCR CONVERSION
;*
;*     CONVERTS BUFFER TO GCR WITH OUT HBID
;*
;***************************
;
FBTOG	LDA #0          ; POINT AT BUFFER
	STA BUFPNT
	STA SAVPNT
	STA BYTCNT
;
	LDA #256-TOPWRT ; PUT GCR IN OVRFLOW BUFFER
	STA GCRPNT
;
	LDA BUFPNT+1    ; SAVE BUFFER POINTER
	STA SAVPNT+1
;
	LDA #>OVRBUF
	STA BUFPNT+1    ; STORE IN OVERBUF
;
FBG10	LDY BYTCNT      ; GET POINTER
;
	LDA (SAVPNT),Y
	STA BTAB
	INY
;
	LDA (SAVPNT),Y
	STA BTAB+1
	INY
;
	LDA (SAVPNT),Y
	STA BTAB+2
	INY
;
	LDA (SAVPNT),Y
	STA BTAB+3
	INY
	BEQ FBG15       ; TEST IF DONE
;
	STY BYTCNT      ; SAVE POINTER
;
	JSR PUT4BG      ; CONVERT AND STORE
;
	JMP FBG10
;
FBG15	JMP PUT4BG      ; DONE, RETURN
;
.END
