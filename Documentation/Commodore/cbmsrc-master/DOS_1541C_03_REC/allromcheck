IP=0 ;ZPAGE POINTER
	*=$F800
START	SEI
	LDY #0
	LDA #$20
LOOP
	STA $8000,Y     ;CLEAR SCREEN
	STA $8100,Y
	STA $8200,Y
	STA $8300,Y
	INY
	BNE LOOP
CALC
	LDA #$B0        ;SET FOR $A SLOT
	STA IP+1
	LDA #0
	STA IP          ;ZERO LO INDIRECT
	TAY
	LDX #16         ;16 PAGES IN 4K ROM
	CLC
RT10	DEC IP+1        ;DO IT BACKWARDS
RT20	ADC (IP),Y      ;TOTAL CHECKSUM IN A
	INY
	BNE RT20
	DEX
	BNE RT10
	ADC #0          ;ADD IN LAST CARRY
	PHA
	AND #$F
	TAX
	LDA HEX,X
	STA $8029       ;POKE LOW NYBBLE
	PLA
	LSR A
	LSR A
	LSR A
	LSR A
	TAX
	LDA HEX,X
	STA $8028       ;POKE HIGH NYBBLE
	JSR WAIT
	LDA $8028
	EOR #$40
	STA $8028
	LDA $8029
	EOR #$40
	STA $8029
	JSR WAIT
	JMP CALC
;
WAIT
	LDY #0
W2
	LDX #0
W3
	DEX
	BNE W3
	DEY
	BNE W2
	RTS
HEX	.BYTE '0123456789'
	.BYTE 1,2,3,4,5,6 ;SCREEN ALPHA
	*=$FFFC
	.WORD START
.END
